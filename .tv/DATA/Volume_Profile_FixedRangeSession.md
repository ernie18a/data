<!-- tradingview-pine-id: PUB;7c59c68819ea494caa8c230eb5f43f52 -->
<!-- tradingview-pine-version: 7.0 -->
<!-- tradingviewscripts-format: 1 -->
# Volume Profile Fixed/Range/Session

Source: https://www.tradingview.com/script/x2pcVS1a-Volume-Profile-Fixed-Range-1CG/

## Description

Fixed Range Volume Profile (Open Source)
This open-source indicator serves a dual purpose: it is a highly customizable, high-performance volume profile tool for traders, and it acts as the official implementation guide for developers looking to integrate the [Volume Profile Library](https://www.tradingview.com/script/A2ob3yE0-Volume-Profile-Library-1CG/) into their own indicators and strategies. 
Leveraging intrabar arrays for volume accuracy, this tool provides precise Point of Control (POC) and Value Area calculations alongside box and polyline visuals.
Why This Indicator Is Necessary
The volume profile in Pine Script is not accessible programmatically. By reviewing the source code of this indicator, developers can see exactly how to integrate the library engine into their own projets:

[*] Setup the `VolumeProfileLibrary` engine state using `var`.
[*] Fetch 1-minute intrabar data efficiently using `request.security_lower_tf()`.
[*] Safely pass data into the engine for accurate volume distribution.
[*] Extract the calculated POC and Value Area levels for external logic.

Traditional vs. Polyline Rendering
[image]https://www.tradingview.com/x/uGWZgWru/[/image]
This indicator breaks away from the limitations of standard histograms by offering multiple rendering modes:

[*] Traditional (Boxes): The classic stacked volume row display, with options to split each row into estimated buy/sell volumes and dim areas outside the Value Area.
[*] Modern (Polylines): Render the profile as Polylines or Curved Polylines. This modern aesthetic connects row endpoints with sleek lines and fills the internal regions with stunning spatial color gradients.

Use As An External Input Source
[image]https://www.tradingview.com/x/z43KyRkG/[/image]
You do not need to fork this code to use its data! This indicator plots the most recent POC, Value Area High, and Value Area Low invisibly to the chart. 
Because these plots are exposed to the Data Window, you can use this script as a direct "Source" input for other indicators and strategies in your workflow. This allows you to trigger alerts or build strategies based on exact Volume Profile levels without writing a single line of code.
Additional Features

[*] Flexible Range Anchoring: Select between absolute ranges ('From Time', 'Between Times') or recurring windows ('Daily Anchor', 'Daily Session') that automatically reset every day.
[*] Intrabar Accuracy: Dives into 1-minute intrabar data to distribute volume accurately within the chart's bars.
[*] Session History: Keep historical sessions alive on the chart, allowing you to instantly visually backtest how the volume profile evolved day over day.

How to Use

[*] Add the indicator to your chart.
[*] Open the settings menu and configure your preferred Timezone. All fixed time pickers and session windows respect this timezone automatically.
[*] Select your Range Mode (e.g. 'Daily Session' for the RTH open).
[*] Customize your resolution (`Rows`), rendering styles, and gradients.

---

## Source Code

````pine
//@version=6

//+-------------------------------------------------------+
//                       INFORMATION                      |
//+-------------------------------------------------------+
// #region Information

// FixedRangeVolumeProfile
//
// Example consumer of VolumeProfileLibrary. Gathers all user inputs, resolves
// recurring-range timezones through UtilityLibrary1CG, passes fixed picker timestamps
// through unchanged, requests 1-minute intrabar data for volume accuracy, and
// hands the per-bar work to VP.update(). The library owns the profile
// drawings; this script owns inputs, the request.security_lower_tf() call,
// the no-volume guard, the optional debug table, and chart-invisible latest
// POC/value-area plots available in the Data Window and to downstream consumers.

// #endregion Information

indicator("Volume Profile Fixed/Range/Session", "VolProfile", overlay = true, max_boxes_count = 500, max_lines_count = 500,
    max_labels_count = 500, max_polylines_count = 100)

import OneCleverGuy/UtilityLibrary1CG/1 as UTIL
import OneCleverGuy/VolumeProfileLibrary/4 as VP

//+-------------------------------------------------------+
//                          INPUTS                        |
//+-------------------------------------------------------+
// #region Inputs

// #region Tooltips ***************************************

string tt_timezone        = "Timezone applied to the daily anchor, daily session, and level labels. Fixed time pickers use the chart timezone."
string tt_rangeMode       = "From Time: one profile from Range Start to now. Between Times: one profile between Range Start and Range End."
                          + " Daily Anchor: resets every day at the anchor time. Daily Session: drawn only inside the session, every day."
string tt_rangeStart      = "Profile start for From Time and Between Times modes. TradingView displays this picker in the chart timezone."
string tt_rangeEnd        = "Profile end for Between Times mode. TradingView displays this picker in the chart timezone."
string tt_dailyAnchor     = "Time of day the profile resets in Daily Anchor mode."
string tt_session         = "Session window for Daily Session mode. Day-of-week flags are ignored."
string tt_historyCount    = "Completed sessions to keep on the chart in the recurring modes. As many as fit Pine's drawing"
                          + " limits stay fully drawn; older sessions retain their range box when enabled and their selected level drawings."
                          + " Fewer rows, no buy/sell split, or a lighter line fill let more sessions stay fully drawn."
                          + " The statistics table compares full sessions with the requested count."
string tt_valueArea       = "Percentage of total volume covered by the value area. Calculated even when profile visuals are hidden."
string tt_showVisuals     = "Show profile drawings on the chart. Calculations, statistics, and Data Window values remain available when disabled."
string tt_splitBuySell    = "Split each profile into estimated buy and sell volume using the close location inside each source bar."
string tt_widthPercent    = "Percentage of the range width the largest row fills."
string tt_display         = "Boxes draws the row histogram. Polylines connect row endpoints with straight segments."
                          + " Curved Polylines uses curved segments for the same profile. When the buy/sell split is disabled, polyline"
                          + " modes draw one total-volume line."
string tt_rowCount        = "Number of price rows used for volume distribution, statistics, and both renderers."
                          + " Fewer rows let more sessions stay fully drawn."
string tt_outsideVA       = "Dim box rows outside the value area, and the transparency applied to them."
string tt_lineFill        = "Region fill for the polyline displays. None draws outlines only. Solid fills each region with"
                          + " its profile color. Gradient fades from the origin-facing edge toward the profile line using 3 to 8 bands;"
                          + " the band count adapts automatically so the requested sessions fit Pine's polyline limit."
string tt_rowColors       = "Buy, sell, and single-color profile colors. Opacity is respected by boxes and polylines."
string tt_rangeBox        = "Range highlight box visibility and fill color."
string tt_poc             = "Point of Control line visibility, color, width, and style."
string tt_pocLabel        = "Origin label drawn at the right end of each POC line: prefix text, show origin date, text color, and size."
string tt_valueAreaLines  = "Value Area High and Low lines share one toggle, color, width, and style."
string tt_valueAreaLabels = "Origin labels drawn at the right end of each value-area line: VAH/VAL prefix text, show origin date, text color, and size."
string tt_dateFormat      = "Date format applied to POC, VAH, and VAL origin labels."
string tt_extendLevels    = "Extend each completed session's POC and value-area lines until the end of the following session."
                          + " The newest completed session's lines follow the latest bar until the next session begins. Recurring modes only."
string tt_debug           = "Show the current session statistics, retaining the last session values between sessions."

// #endregion Tooltips

// #region Timezone ***************************************

var string iGroupTimezone = "Timezone"
i_timezone = input.enum(UTIL.Timezones.ny, "Timezone", group = iGroupTimezone, tooltip = tt_timezone)

// #endregion Timezone

// #region Range Mode *************************************

var string iGroupRangeMode = "Range Mode"
i_rangeMode = input.enum(VP.RangeMode.DailySession, "Range Mode", group = iGroupRangeMode, tooltip = tt_rangeMode)
bool a_rec  = i_rangeMode == VP.RangeMode.DailyAnchor or i_rangeMode == VP.RangeMode.DailySession

// #endregion Range Mode

// #region Fixed Anchors **********************************

var string iGroupFixed = "Fixed Anchors"
i_rangeStart = input.time(timestamp("2024-01-01 00:00 +0000"), "Range Start", group = iGroupFixed, tooltip = tt_rangeStart, display = display.none, active = i_rangeMode == VP.RangeMode.FromTime or i_rangeMode == VP.RangeMode.BetweenTimes)
i_rangeEnd   = input.time(timestamp("2024-02-01 00:00 +0000"), "Range End",   group = iGroupFixed, tooltip = tt_rangeEnd,   display = display.none, active = i_rangeMode == VP.RangeMode.BetweenTimes)

// #endregion Fixed Anchors

// #region Recurring Anchors ******************************

var string iGroupRecurring = "Recurring Anchors"
i_anchorQuarter = input.enum(UTIL.QuarterHours.t1600, "Daily Anchor", group = iGroupRecurring, tooltip = tt_dailyAnchor, active = i_rangeMode == VP.RangeMode.DailyAnchor)
i_session       = input.session("0930-1600", "Session",              group = iGroupRecurring, tooltip = tt_session,     active = i_rangeMode == VP.RangeMode.DailySession)
i_historyCount  = input.int(2, "Previous Sessions", minval = 0, maxval = VP.MAX_HISTORY_COUNT, group = iGroupRecurring, tooltip = tt_historyCount, display = display.none, active = a_rec)

// #endregion Recurring Anchors

// #region Profile ****************************************

var string iGroupProfile = "Profile"
i_valueArea    = input.float(70.0, "Value Area %", minval = 0.0, maxval = 100.0, step = 1.0, group = iGroupProfile, tooltip = tt_valueArea, display = display.none)
i_rowCount     = input.int(30, "Rows", minval = VP.MIN_ROW_COUNT, maxval = VP.MAX_ROW_COUNT, group = iGroupProfile, tooltip = tt_rowCount, display = display.none)
i_widthPercent = input.float(100.0, "Width %", minval = 1.0, maxval = 100.0, step = 5.0, group = iGroupProfile, tooltip = tt_widthPercent, display = display.none)

// #endregion Profile

// #region Display ****************************************

var string iGroupDisplay = "Display"
i_showVisuals = input.bool(true, "Show Profile Visuals", group = iGroupDisplay, tooltip = tt_showVisuals, display = display.none)
i_display     = input.enum(VP.ProfileDisplay.Boxes, "Display", group = iGroupDisplay, tooltip = tt_display, active = i_showVisuals)
bool a_box    = i_showVisuals and i_display == VP.ProfileDisplay.Boxes
bool a_ply    = i_showVisuals and i_display != VP.ProfileDisplay.Boxes

i_dimOutsideValueArea = input.bool(true, "Dim Outside VA", group = iGroupDisplay, inline = "VA", tooltip = tt_outsideVA, display = display.none, active = a_box)
i_outTransparency     = input.int(75, " ",                 group = iGroupDisplay, inline = "VA", minval = 0, maxval = 100, display = display.none, active = a_box and i_dimOutsideValueArea)

i_lineFill            = input.enum(VP.PolylineFill.Gradient, "Line Fill", group = iGroupDisplay, tooltip = tt_lineFill, active = a_ply)

// #endregion Display

// #region Appearance *************************************

var string iGroupAppearance = "Appearance"
i_splitBuySell        = input.bool(true, "Split Buy/Sell", group = iGroupAppearance, tooltip = tt_splitBuySell, display = display.none, active = i_showVisuals)
i_buyColor            = input.color(color.new(#26a69a, 35), "Row Colors", group = iGroupAppearance, inline = "RC", tooltip = tt_rowColors, display = display.none, active = i_showVisuals)
i_sellColor           = input.color(color.new(#ef5350, 35), " ",          group = iGroupAppearance, inline = "RC", display = display.none, active = i_showVisuals)
i_totalColor          = input.color(color.new(#2962ff, 35), " ",          group = iGroupAppearance, inline = "RC", display = display.none, active = i_showVisuals)

i_showRangeBox        = input.bool(true,       "Range Box", group = iGroupAppearance, inline = "RB", tooltip = tt_rangeBox, display = display.none, active = i_showVisuals)
bool a_rbx            = i_showVisuals and i_showRangeBox
i_rangeFill           = input.color(#2962ff12, " ",         group = iGroupAppearance, inline = "RB", display = display.none, active = a_rbx)

i_showPoc             = input.bool(true,    "Point of Control", group = iGroupAppearance, inline = "PC", tooltip = tt_poc, display = display.none, active = i_showVisuals)
bool a_poc            = i_showVisuals and i_showPoc
i_pocColor            = input.color(#ff0000,             " ",   group = iGroupAppearance, inline = "PC", display = display.none, active = a_poc)
i_pocWidth            = input.enum(UTIL.LineSize.normal, " ",   group = iGroupAppearance, inline = "PC", display = display.none, active = a_poc)
i_pocStyle            = input.enum(UTIL.LineStyle.solid, " ",   group = iGroupAppearance, inline = "PC", display = display.none, active = a_poc)

i_showPocLabel        = input.bool(true, "POC Label",        group = iGroupAppearance, inline = "PL", tooltip = tt_pocLabel, display = display.none, active = a_poc)
bool a_pcl            = a_poc and i_showPocLabel
i_pocTextColor        = input.color(#ffffff,          " ",   group = iGroupAppearance, inline = "PL", display = display.none, active = a_pcl)
i_pocTextSize         = input.enum(UTIL.TextSize.small, " ", group = iGroupAppearance, inline = "PL", display = display.none, active = a_pcl)
i_pocPrefix           = input.string("POC", "Prefix",        group = iGroupAppearance, inline = "PLT", display = display.none, active = a_pcl)
i_showPocDate         = input.bool(true, "Show Date",        group = iGroupAppearance, inline = "PLT", display = display.none, active = a_pcl)

i_showValueAreaLines  = input.bool(false, "Value Area Lines", group = iGroupAppearance, inline = "VL", tooltip = tt_valueAreaLines, display = display.none, active = i_showVisuals)
bool a_val            = i_showVisuals and i_showValueAreaLines
i_valueAreaColor      = input.color(#2962ff, " ",             group = iGroupAppearance, inline = "VL", display = display.none, active = a_val)
i_valueAreaWidth      = input.enum(UTIL.LineSize.normal, " ", group = iGroupAppearance, inline = "VL", display = display.none, active = a_val)
i_valueAreaStyle      = input.enum(UTIL.LineStyle.solid, " ", group = iGroupAppearance, inline = "VL", display = display.none, active = a_val)

i_showValueAreaLabels = input.bool(true, "VA Labels",        group = iGroupAppearance, inline = "VB", tooltip = tt_valueAreaLabels, display = display.none, active = a_val)
bool a_vlb            = a_val and i_showValueAreaLabels
i_valueAreaTextColor  = input.color(#ffffff, " ",            group = iGroupAppearance, inline = "VB", display = display.none, active = a_vlb)
i_valueAreaTextSize   = input.enum(UTIL.TextSize.small, " ", group = iGroupAppearance, inline = "VB", display = display.none, active = a_vlb)
i_vahPrefix           = input.string("VAH", "VAH Prefix",    group = iGroupAppearance, inline = "VBT", display = display.none, active = a_vlb)
i_valPrefix           = input.string("VAL", "VAL Prefix",    group = iGroupAppearance, inline = "VBT", display = display.none, active = a_vlb)
i_showVaDate          = input.bool(true, "Show Date",        group = iGroupAppearance, inline = "VBT", display = display.none, active = a_vlb)

bool a_lbl_date       = (a_pcl and i_showPocDate) or (a_vlb and i_showVaDate)
i_dateFormat          = input.enum(VP.DateFormat.DayMonth, "Label Date Format", group = iGroupAppearance, tooltip = tt_dateFormat, display = display.none, active = a_lbl_date)

bool a_ext            = i_showVisuals and a_rec and (i_showPoc or i_showValueAreaLines)
i_extendLevels        = input.bool(false, "Extend Lines One Extra Session", group = iGroupAppearance, tooltip = tt_extendLevels, display = display.none, active = a_ext)

// #endregion Appearance

// #region Debug ******************************************

var string iGroupDebug = "Debug"
i_debug = input.bool(false, "Show Statistics Table", group = iGroupDebug, tooltip = tt_debug, display = display.none)

// #endregion Debug

// #endregion Inputs

//+-------------------------------------------------------+
//                      GLOBAL VARIABLES                  |
//+-------------------------------------------------------+
// #region Global Variables

int DEBUG_TABLE_COLUMNS = 2
int DEBUG_TABLE_ROWS    = 8
color DEBUG_TABLE_BACKGROUND = color.new(#111827, 10)
color DEBUG_TABLE_TEXT       = #f3f4f6
string DEBUG_EMPTY_VALUE     = "—"

var VP.ProfileState profileState = VP.createState()
var bool hasSeenVolume = false

// #endregion Global Variables

//+-------------------------------------------------------+
//                         FUNCTIONS                      |
//+-------------------------------------------------------+
// #region Functions

// #region Shared Helper Functions ************************

formatPrice(float _price) =>
    (na(_price) ? DEBUG_EMPTY_VALUE : str.tostring(_price, format.mintick))

// #endregion Shared Helper Functions

// #region Debug Table Functions **************************

// #region Debug Table Helpers //

writeDebugRow(table _table, int _row, string _label, string _value) =>
    table.cell(_table, 0, _row, _label, bgcolor = DEBUG_TABLE_BACKGROUND, text_color = DEBUG_TABLE_TEXT,
        text_halign = text.align_left, text_size = size.small)
    table.cell(_table, 1, _row, _value, bgcolor = DEBUG_TABLE_BACKGROUND, text_color = DEBUG_TABLE_TEXT,
        text_halign = text.align_right, text_size = size.small)
    int(na)

formatSessionPlan(int _fullSessions, int _requestedSessions) =>
    str.tostring(_fullSessions) + "/" + str.tostring(_requestedSessions)

// #endregion Debug Table Helpers

// @function renderDebugTable                     - Fill the table from the retained statistics and the resolved drawing plan.
// @param    _table        (table)                - Table to write into.
// @param    _state        (VP.ProfileState)      - State retaining statistics and the current drawing plan.
// @param    _cfg          (VP.ProfileConfig)     - Sanitised configuration holding the requested history count.
// @returns                (int)                  - Typed fallback; side effects only.
renderDebugTable(table _table, VP.ProfileState _state, VP.ProfileConfig _cfg) =>
    bool hasStatistics = not na(_state.mostRecentPoc)
    writeDebugRow(_table, 0, "Bars",               hasStatistics ? str.tostring(_state.mostRecentBarCount) : DEBUG_EMPTY_VALUE)
    writeDebugRow(_table, 1, "Total Volume",       hasStatistics ? str.tostring(_state.mostRecentTotalVolume, format.volume) : DEBUG_EMPTY_VALUE)
    writeDebugRow(_table, 2, "POC Buy Volume",     hasStatistics ? str.tostring(_state.mostRecentPocBuyVolume, format.volume) : DEBUG_EMPTY_VALUE)
    writeDebugRow(_table, 3, "POC Sell Volume",    hasStatistics ? str.tostring(_state.mostRecentPocSellVolume, format.volume) : DEBUG_EMPTY_VALUE)
    writeDebugRow(_table, 4, "POC",                hasStatistics ? formatPrice(_state.mostRecentPoc) : DEBUG_EMPTY_VALUE)
    writeDebugRow(_table, 5, "VA High",            hasStatistics ? formatPrice(_state.mostRecentValueAreaHigh) : DEBUG_EMPTY_VALUE)
    writeDebugRow(_table, 6, "VA Low",             hasStatistics ? formatPrice(_state.mostRecentValueAreaLow) : DEBUG_EMPTY_VALUE)
    writeDebugRow(_table, 7, "Full Sessions / Requested",
        formatSessionPlan(_state.effectiveHistoryCount, _cfg.historyCount))
    int(na)

// #endregion Debug Table Functions

// #endregion Functions

//+-------------------------------------------------------+
//                        EXECUTION                       |
//+-------------------------------------------------------+
// #region Execution

// #region Shared Runtime Context *************************

string globalTimezone = i_timezone.toTimezone()
string anchorHHMM     = i_anchorQuarter.toHhmm()

int rangeStartTime = i_rangeStart
int rangeEndTime   = i_rangeEnd

// Charts at or below 1 minute request their own timeframe, which returns
// single-entry arrays and avoids a lower-timeframe runtime error.
string intrabarTimeframe = (timeframe.in_seconds() > 60 ? "1" : timeframe.period)
[ltfHighs, ltfLows, ltfCloses, ltfVolumes] = request.security_lower_tf(syminfo.tickerid, intrabarTimeframe,
    [high, low, close, volume])

// #endregion Shared Runtime Context

// #region Volume Guard ***********************************

if not na(volume) and volume > 0
    hasSeenVolume := true

if barstate.islast and not hasSeenVolume
    runtime.error("FixedRangeVolumeProfile requires a symbol that provides volume data. "
      + "The current symbol has no volume on this chart.")

// #endregion Volume Guard

// #region Component Config Assembly **********************

var VP.ProfileStyle profileStyle = VP.ProfileStyle.new(
    buyColor                     = i_buyColor,                            sellColor            = i_sellColor,
    totalColor                   = i_totalColor,                          rangeBackgroundColor = i_rangeFill,
    outsideValueAreaTransparency = i_outTransparency,
    pocColor                     = i_pocColor,                            pocWidth             = i_pocWidth.toLineWidth(),
    pocStyle                     = i_pocStyle.toLineStyle(),              pocTextColor         = i_pocTextColor,
    pocTextSize                  = i_pocTextSize.toTextSizeString(),      pocLabelPrefix       = i_pocPrefix,
    showPocDate                  = i_showPocDate,
    valueAreaColor               = i_valueAreaColor,                      valueAreaWidth       = i_valueAreaWidth.toLineWidth(),
    valueAreaStyle               = i_valueAreaStyle.toLineStyle(),        valueAreaTextColor   = i_valueAreaTextColor,
    valueAreaTextSize            = i_valueAreaTextSize.toTextSizeString(),vahLabelPrefix     = i_vahPrefix,
    valLabelPrefix               = i_valPrefix,                           showValueAreaDate    = i_showVaDate,
    dateFormat                   = i_dateFormat
)

var VP.ProfileConfig profileConfig = VP.ProfileConfig.new(
    rangeMode            = i_rangeMode,            rangeStartTime           = rangeStartTime,
    rangeEndTime         = rangeEndTime,           anchorHHMM               = anchorHHMM,
    sessionString        = i_session,              timezone                 = globalTimezone,
    rowCount             = i_rowCount,             valueAreaPercent         = i_valueArea,
    splitBuySell         = i_splitBuySell,         showVisuals              = i_showVisuals,
    widthPercent         = i_widthPercent,         profileDisplay           = i_display,
    polylineFill         = i_lineFill,             historyCount             = i_historyCount,
    showRangeBox         = i_showRangeBox,         dimOutsideValueArea      = i_dimOutsideValueArea,
    showPoc              = i_showPoc,              showPocLabel             = i_showPocLabel,
    showValueAreaLines   = i_showValueAreaLines,   showValueAreaLabels      = i_showValueAreaLabels,
    extendLevelsExtraSession = i_extendLevels,     style                    = profileStyle
)

// #endregion Component Config Assembly

// #region Central Orchestration **************************

bool useIntrabar = not timeframe.isseconds
profileState := VP.update(profileState, profileConfig, useIntrabar, ltfHighs, ltfLows, ltfCloses, ltfVolumes)
[mostRecentPoc, mostRecentValueAreaHigh, mostRecentValueAreaLow] = VP.getMostRecentLevels(profileState)
[mostRecentPocVol, mostRecentPocBuyVol, mostRecentPocSellVol]   = VP.getMostRecentPocVolumes(profileState)
plot(mostRecentPoc, "Most Recent POC", display = display.data_window, format = format.price, editable = false)
plot(mostRecentValueAreaHigh, "Most Recent Value Area High", display = display.data_window, format = format.price, editable = false)
plot(mostRecentValueAreaLow, "Most Recent Value Area Low", display = display.data_window, format = format.price, editable = false)
plot(mostRecentPocBuyVol, "Most Recent POC Buy Volume", display = display.data_window, format = format.volume, editable = false)
plot(mostRecentPocSellVol, "Most Recent POC Sell Volume", display = display.data_window, format = format.volume, editable = false)

// #endregion Central Orchestration

// #endregion Execution

//+-------------------------------------------------------+
//                          DEBUG                         |
//+-------------------------------------------------------+
// #region Debug

if i_debug and barstate.islast
    var table debugTable = table.new(position.top_right, DEBUG_TABLE_COLUMNS, DEBUG_TABLE_ROWS,
        bgcolor = DEBUG_TABLE_BACKGROUND, frame_color = color.new(DEBUG_TABLE_TEXT, 65), frame_width = 1,
        border_color = color.new(DEBUG_TABLE_TEXT, 80), border_width = 1)
    renderDebugTable(debugTable, profileState, profileConfig)

// #endregion Debug
````
