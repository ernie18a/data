<!-- tradingview-pine-id: PUB;4849a6f8dbc443a2a8ae220fe7d92783 -->
<!-- tradingviewscripts-format: 1 -->
# ErrorFunctions

Source: https://www.tradingview.com/script/brRyBZnc-ErrorFunctions/

## Description

Library  "ErrorFunctions"
A collection of functions used to approximate the area beneath a Gaussian curve.

Because an ERF (Error Function) is an integral, there is no closed-form solution to calculating the area beneath the curve. Meaning all ERFs are approximations; precisely wrong, but mostly accurate. How close you need to get to the actual area depends entirely on your use case, with more precision being less efficient.

The internal precision of floats in Pine Script is 1e-16 (16 decimals, aka. double precision). This library adapts well known algorithms designed to efficiently reach double precision. Single precision alternates are also included. All of them were made free to use, modify, and distribute by their original authors.

HASTINGS
Adaptation of a single precision ERF by Cecil Hastings Jr, published through Princeton University in 1955. It was later documented by Abramowitz and Stegun as equation 7.1.26 in their 1972 Handbook of Mathematical Functions. Fast, efficient, and ideal when precision beyond a few decimals is unnecessary.

GILES
Adaptation of a single precision Inverse ERF by Michael Giles, published through the University of Oxford in 2012. It reverses the ERF, estimating an X coordinate from an area. It too is fast, efficient, and ideal when precision beyond a few decimals is unnecessary.

LIBC
Adaptation of the double precision ERF & ERFC in the standard C library (aka. libc). It is also the same ERF & ERFC that SciPy uses. While not quite as efficient as the Hastings approximation, it's still very fast and fully maximizes Pines precision.

BOOST
Adaptation of the double precision Inverse ERF & Inverse ERFC in the Boost Math C++ library. SciPy uses these as well. These reverse the ERF & ERFC, estimating an X coordinate from an area. It too isn't quite as efficient as the Giles approximation, but still fast and fully maximizes Pines precision.

While these algorithms are not exported directly, they are available through their exported counterparts.

- - -

ERROR FUNCTIONS

erf(x, precise)
  An Error Function estimates the theoretical error of a measurement.
  Parameters:
    x (float): (float) Upper limit of the integration.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between -1 and 1.

erfc(x, precise)
  A Complementary Error Function estimates the difference between a theoretical error and infinity.
  Parameters:
    x (float): (float) Lower limit of the integration.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between 0 and 2.

erfinv(x, precise)
  An Inverse Error Function reverses the erf() by estimating the original measurement from the theoretical error.
  Parameters:
    x (float): (float) Theoretical error.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between 0 and ± infinity.

erfcinv(x, precise)
  An Inverse Complementary Error Function reverses the erfc() by estimating the original measurement from the difference between the theoretical error and infinity.
  Parameters:
    x (float): (float) Difference between the theoretical error and infinity.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between 0 and ± infinity.

- - -

DISTRIBUTION FUNCTIONS

pdf(x, m, s)
  A Probability Density Function estimates the probability density. For clarity, density is not a probability.
  Parameters:
    x (float): (float) X coordinate for which a density will be estimated.
    m (float): (float) Mean
    s (float): (float) Sigma
  Returns: (float) Between 0 and ∞.

cdf(z, precise)
  A Cumulative Distribution Function estimates the area under a Gaussian curve between negative infinity and the Z Score.
  Parameters:
    z (float): (float) Z Score.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between 0 and 1.

cdfinv(a, precise)
  An Inverse Cumulative Distribution Function reverses the cdf() by estimating the Z Score from an area.
  Parameters:
    a (float): (float) Area between 0 and 1.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between -∞ and +∞

cdfab(z1, z2, precise)
  A Cumulative Distribution Function from A to B estimates the area under a Gaussian curve between two Z Scores (A and B).
  Parameters:
    z1 (float): (float) First Z Score.
    z2 (float): (float) Second Z Score.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between 0 and 1.

ttt(z, precise)
  A Two-Tailed Test estimates the area under a Gaussian curve between symmetrical ± Z scores and ± infinity.
  Parameters:
    z (float): (float) One of the symmetrical Z Scores.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between 0 and 1.

tttinv(a, precise)
  An Inverse Two-Tailed Test reverses the ttt() by estimating the absolute Z Score from an area.
  Parameters:
    a (float): (float) Area between 0 and 1.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between 0 and ∞.

ott(z, precise)
  A One-Tailed Test estimates the area under a Gaussian curve between an absolute Z Score and infinity.
  Parameters:
    z (float): (float) Z Score.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between 0 and 1.

ottinv(a, precise)
  An Inverse One-Tailed Test Reverses the ott() by estimating the Z Score from a an area.
  Parameters:
    a (float): (float) Area between 0 and 1.
    precise (bool): Double precision (true) or single precision (false).
  Returns: (float) Between 0 and ∞.

---

## Source Code

````pine
// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © liquid-trader

//@version=6

// @description A collection of functions used to approximate the area beneath a Gaussian curve.
library("ErrorFunctions")


// ----------------------------------------------- LIBRARY CONSTANTS ------------------------------------------------ {

// @variable 64-bit float overflow: ∞.
export const float INFINITY = 2 * 1.0e308

// @variable Pre-computed high precision math.sqrt(2) = 1.414213562373095048801688724209698078
export const float ROOT_TWO = 1.414213562373095048801688724209698078

// @variable Pre-computed high precision math.sqrt(2 * math.pi) = 2.506628274631000502415765284811045253
export const float ROOT_TWO_PI = 2.506628274631000502415765284811045253

// @variable Largest finite value representable as a 64-bit float: 1.7976931348623157e308
export const float MAX_VALUE = 1.7976931348623157e308

// @variable Smallest *practical* double precision value near 0.
export const float LIMIT_0 = 0.0000000000000001

// @variable Largest *practical* double precision value near 1.
export const float LIMIT_1 = 0.9999999999999999

// @variable Largest *practical* double precision value near 2.
export const float LIMIT_2 = 1.9999999999999998

// }


// ------------------------------------------------- LIBRARY HELPERS ------------------------------------------------ {

// @function Alternate to [math.sign()](#fun_math.sign) that only returns -1 or 1; notable for negative values at or
// near zero. The built-in function returns ± 0 when values are zero (or near it) rather than ± 1.
sign(float number) =>
    sign = math.sign(number)
    na(sign) ? na : 1 / sign < 0 ? -1 : 1

// @function Shifts decimal 6 digits to the right. Pine comparators eval to 1.0e-10 and shifting the decimal affords
// 1.0e-16 evaulations, making comparators sensitive to smaller values.
method e6(float number) =>
    na(1 / number) ? number : nz(number * 1.0e6, sign(number) * MAX_VALUE)

// }


// ---------------------------------------------------- HASTINGS ---------------------------------------------------- {

// This section is an adaptation of the Hastings ERF approximation, noted by Abramowitz and Stegun as equation 7.1.26.
// It's fast, efficient, and precise to about 6-7 digits (1.5e-7). Ideal for computationally sensitive use cases
// where precision beyond a few decimals is unnecessary.
// https://personal.math.ubc.ca/~cbm/aands/page_299.htm

// @function Helper for `erf` and `erfc`, called when `precise` is [false](#const_false).
hastings_erf(float x, float ax) =>
    t = 1 / (1 + 0.3275911 * ax)
    y = 1 - ((((1.061405429 * t + -1.453152027) * t + 1.421413741) * t + -0.284496736) * t + 0.254829592) * t * math.exp(-x * x)
    sign(x) < 0 ? -y : y

// }


// ----------------------------------------------------- GILES ------------------------------------------------------ {

// This section is an adaptation of a single precision Inverse ERF, made available by Oxford, authored by Mike Giles.
// It's fast, efficient, and precise to about 6-7 digits (7e−7). Ideal for computationally sensitive use cases
// where precision beyond a few decimals is unnecessary.
// https://people.maths.ox.ac.uk/gilesm/files/gems_erfinv.pdf

// @function Helper for `erfinv` and `erfcinv`, called when `precise` is [false](#const_false).
giles_erf_inv(float x) =>
    t = -math.log((1 - x) * (1 + x)) // -log(1 - x^2)
    if t.e6() < 5.0e6
        t -= 2.5
        (((((((( 2.81022636e-08 * t + 3.43273939e-07) * t + -3.5233877e-06) * t + -4.39150654e-06) * t + 0.00021858087) * t + -0.00125372503) * t + -0.00417768164) * t + 0.246640727) * t + 1.50140941) * x
    else
        t := math.sqrt(t) - 3
        ((((((((-0.000200214257 * t + 0.000100950558) * t +  0.00134934322) * t + -0.003673428440) * t + 0.00573950773) * t + -0.00762246130) * t +  0.00943887047) * t + 1.001674060) * t + 2.83297682) * x

// }


// ------------------------------------------------------ LIBC ------------------------------------------------------ {

// This section is an adaptation of `s_erf.c` in the standard C math library (libc), authored by Sun Microsystems.
// Their Freely Distributable Math Library (FDLIBM) is permissable to use, modify, and distribute. It is also the
// same ERF & ERFC that SciPy uses. More specifically, SciPy uses `Faddeeva.cc` in which `FADDEEVA_RE(erf)` and
// `FADDEEVA_RE(erfc)` default to the libc ERF, which is `s_erf.c`. The internal precision of floats in
// Pine Script is 1e-16 (16 decimal places). The libc ERF precision is 2e-16 (15-16 decimal places).
// https://www.netlib.org/fdlibm/s_erf.c

// @function Helper for `sun_erf` and `sun_erfc`.
sun_erf_polynomial(int i, float t) =>
    [ p, q ] = switch i
        0 => [ (((    -2.37630166566501626084e-05 * t + -5.77027029648944159157e-03) * t + -2.84817495755985104766e-02) * t + -3.25042107247001499370e-01) * t +  1.28379167095512558561e-01,
               ((((   -3.96022827877536812320e-06 * t +  1.32494738004321644526e-04) * t +  5.08130628187576562776e-03) * t +  6.50222499887672944485e-02) * t +  3.97917223959155352819e-01) * t +  1 ]
        1 => [ (((((  -2.16637559486879084300e-03 * t +  3.54783043256182359371e-02) * t + -1.10894694282396677476e-01) * t +  3.18346619901161753674e-01) * t + -3.72207876035701323847e-01) * t +  4.14856118683748331666e-01) * t + -2.36211856075265944077e-03,
               (((((   1.19844998467991074170e-02 * t +  1.36370839120290507362e-02) * t +  1.26171219808761642112e-01) * t +  7.18286544141962662868e-02) * t +  5.40397917702171048937e-01) * t +  1.06420880400844228286e-01) * t +  1 ]
        2 => [ (((((( -9.81432934416914548592e+00 * t + -8.12874355063065934246e+01) * t + -1.84605092906711035994e+02) * t + -1.62396669462573470355e+02) * t + -6.23753324503260060396e+01) * t + -1.05586262253232909814e+01) * t + -6.93858572707181764372e-01) * t + -9.86494403484714822705e-03,
               (((((((-6.04244152148580987438e-02 * t +  6.57024977031928170135e+00) * t +  1.08635005541779435134e+02) * t +  4.29008140027567833386e+02) * t +  6.45387271733267880336e+02) * t +  4.34565877475229228821e+02) * t +  1.37657754143519042600e+02) * t +  1.96512716674392571292e+01) * t + 1 ]
        3 => [ (((((  -4.83519191608651397019e+02 * t + -1.02509513161107724954e+03) * t + -6.37566443368389627722e+02) * t + -1.60636384855821916062e+02) * t + -1.77579549177547519889e+01) * t + -7.99283237680523006574e-01) * t + -9.86494292470009928597e-03,
               (((((( -2.24409524465858183362e+01 * t +  4.74528541206955367215e+02) * t +  2.55305040643316442583e+03) * t +  3.19985821950859553908e+03) * t +  1.53672958608443695994e+03) * t +  3.25792512996573918826e+02) * t +  3.03380607434824582924e+01) * t + 1 ]
    p / q


// @function Helper for `sun_erf` and `sun_erfc`.
sun_erf_xp(float x, float p) =>
    const int c = int(math.pow(10, 8))
    f = math.floor(x * c) / c
    math.exp(-f * f - 0.5625) * math.exp((f - x) * (f + x) + p)


// @function Helper for `erf`, called when `precise` is [true](#const_true).
sun_erf(float x, float ax, float ix) =>
    if ix < 0.84375e6
        if ix * 1.0e10 < 3.7252902984619141e7 // math.pow(2, -28) * 1.0e16
            if x * 1.0e306 <= 2.8480945388892178
                0.125 * (8 * x + 1.02703333676410069053 * x) // Avoid underflow
            else
                1.28379167095512586316e-01 * x + x
        else
            sun_erf_polynomial(0, x * x) * x + x
    else if ix < 1.25e6
        const float c = 8.45062911510467529297e-01
        p = sun_erf_polynomial(1, ax - 1)
        x < 0 ? -c - p : c + p
    else // ax < 6
        p = sun_erf_polynomial(ix < 2.857142857142857e6 ? 2 : 3, 1 / (x * x))
        r = sun_erf_xp(x, p)
        x < 0 ? -r / x - 1 : 1 - r / x


// @function Helper for `erfc`, called when `precise` is [true](#const_true).
sun_erfc(float x, float ax, float ix) =>
    if ix < 0.84375e6
        if ix * 1.0e10 < 1.3877787807814457e-1 // math.pow(2, -56) * 1.0e16
            1 - x
        else
            p = sun_erf_polynomial(0, x * x) * x
            ix < 0.25e6 ? 1 - (x + p) : 0.5 - (p + (x - 0.5))
    else if ix < 1.25e6
        const float c = 8.45062911510467529297e-01
        p = sun_erf_polynomial(1, ax - 1)
        x < 0 ? 1 + c + p : 1 - c - p
    else // ax < 28
        p = sun_erf_polynomial(ix * 1.0e10 < 2.8571414947509766 ? 2 : 3, 1 / (x * x))
        r = sun_erf_xp(x, p)
        x > 0 ? r / x : 2 + r / x

// }


// ----------------------------------------------------- BOOST ------------------------------------------------------ {

// This section is an adaptation of `erf_inv.hpp` in the Boost Math C++ library, originally authored by John Maddock
// with permissability to use, modify, and distriubte. The SciPy `erfinv` function  wraps the `erf_inv` routine
// from the Boost library. The internal precision of floats in Pine Script is 1e-16 (16 decimal places).
// The Boost Inverse ERF has a range of precision, the least of which is 7e-17 (16 decimal places).
// https://github.com/boostorg/math/blob/boost-1.88.0/include/boost/math/special_functions/detail/erf_inv.hpp

// @function Helper for `boost_erf_inv_imp`.
boost_erf_inv_polynomial(int i, float t, float y) =>
    [ p, q ] = switch i
        0 => [ ((((((   -0.005387729650712429329650  * t +   0.008226878746769157431550 ) * t +  0.021987868111116889916500 ) * t +  -0.036563797141176266400600) * t +  -0.012692614766297402903400) * t +   0.033480662540974461503300) * t + -0.00836874819741736770379 ) * t + -0.000508781949658280665617,
               ((((((((  0.000886216390456424707504  * t +  -0.002333937593741900167760 ) * t +  0.079528368734157168001800 ) * t +  -0.052739638234009971395400) * t +  -0.712289023415428475530000) * t +   0.662328840472002992063000) * t +  1.56221558398423026363000 ) * t + -1.565745582341758468090000) * t + -0.970005043303290640362) * t + 1 ]
        1 => [ (((((((  -3.671922547077293485460000  * t +  21.129465544834052625800000 ) * t + 17.445385985570866523000000 ) * t + -44.638232444178696081800000) * t + -18.851064805871425189500000) * t +  17.644729840837401548600000) * t +  8.37050328343119927838000 ) * t +  0.105264680699391713268000) * t + -0.202433508355938759655,
               (((((((   1.721147657612002827240000  * t + -22.643693341313972173600000 ) * t + 10.826866735546015900800000 ) * t +  48.560921310873993546800000) * t + -20.143263468048518880100000) * t + -28.660818049980002997400000) * t +  3.97134379533438690950000 ) * t +  6.242641248542475377120000) * t +  1 ]
        2 => [ (((((((((-0.681149956853776992068e-9  * t +   0.285225331782217055858e-7 ) * t + -0.679465575181126350155e-6 ) * t +   0.002145589953888052771690) * t +   0.029015791000532906043200) * t +   0.142869534408157156766000) * t +  0.33778553891203589892400 ) * t +  0.387079738972604337464000) * t +  0.117030156341995252019) * t + -0.163794047193317060787) * t + -0.131102781679951906451,
               ((((((    0.011059242293464891210000  * t +   0.152264338295331783612000 ) * t +  0.848854343457902036425000 ) * t +   2.593019216236202713740000) * t +   4.778465929458437783820000) * t +   5.381683457070068554250000) * t +  3.46625407242567245975000 ) * t +  1 ]
        3 => [ (((((((   0.266339227425782031962e-11 * t +  -0.230404776911882601748e-9 ) * t +  0.460469890584317994083e-5 ) * t +   0.000157544617424960554631) * t +   0.001871234928195592233450) * t +   0.009508047013259196036190) * t +  0.01855733065142310723240 ) * t + -0.002224265292134479272810) * t + -0.0350353787183177984712,
               (((((     0.764675292302794483503e-4  * t +   0.002638616766570159929590 ) * t +  0.034158914367094772793400 ) * t +   0.220091105764131249824000) * t +   0.762059164553623404043000) * t +   1.365334981755406309700000) * t +  1 ]
        4 => [ (((((((   0.99055709973310326855e-16  * t +  -0.281128735628831791805e-13) * t +  0.462596163522878599135e-8 ) * t +   0.449696789927706453732e-6) * t +   0.149624783758342370182e-4) * t +   0.000209386317487588078668) * t +  0.00105628862152492910091 ) * t + -0.001129514387455802788630) * t + -0.0167431005076633737133,
               (((((     0.282243172016108031869e-6  * t +   0.275335474764726041141e-4 ) * t +  0.000964011807005165528527 ) * t +   0.016074608709367650469500) * t +   0.138151865749083321638000) * t +   0.591429344886417493481000) * t +  1 ]
        5 => [ ((((((   -0.116765012397184275695e-17 * t +   0.145596286718675035587e-11) * t +  0.411632831190944208473e-9 ) * t +   0.396341011304801168516e-7) * t +   0.162397777342510920873e-5) * t +   0.254723037413027451751e-4) * t + -0.779190719229053954292e-5) * t + -0.0024978212791898131227,
               (((((     0.509761276599778486139e-9  * t +   0.144437756628144157666e-6 ) * t +  0.145007359818232637924e-4 ) * t +   0.000690538265622684595676) * t +   0.016941083812097590647800) * t +   0.207123112214422517181000) * t +  1 ]
        6 => [ ((((((   -0.348890393399948882918e-21 * t +   0.135880130108924861008e-14) * t +  0.947846627503022684216e-12) * t +   0.225561444863500149219e-9) * t +   0.229345859265920864296e-7) * t +   0.899465114892291446442e-6) * t + -0.28398759004727721098e-6 ) * t + -0.000539042911019078575891,
               (((((     0.231558608310259605225e-11 * t +   0.161809290887904476097e-8 ) * t +  0.399968812193862100054e-6 ) * t +   0.468292921940894236786e-4) * t +   0.002820929847262646819810) * t +   0.084574623400189943691400) * t +  1 ]
    p / q + y


// @function Helper for `boost_erf_inv` and `boost_erfc_inv`.
boost_erf_inv_imp(float p, float q) =>
    if p.e6() <= 0.5e6
        boost_erf_inv_polynomial(0, p, 0.0891314744949340820313) * (p * (p + 10)) // max err: 2.001849e-18
    else if 0.25e6 <= q.e6()
        math.sqrt(-2 * math.log(q)) / boost_erf_inv_polynomial(1, q - 0.25, 2.249481201171875) // max err: 7.403372e-17
    else // q < 0.25
        x = math.sqrt(-math.log(q))
        switch
            x < 3  => x * boost_erf_inv_polynomial(2, x - 1.125, 0.80722045898437500000) // max err: 1.089051e-20
            x < 6  => x * boost_erf_inv_polynomial(3, x - 3.000, 0.93995571136474609375) // max err: 8.389174e-21
            x < 18 => x * boost_erf_inv_polynomial(4, x - 6.000, 0.98362827301025390625) // max err: 1.481312e-19
            x < 44 => x * boost_erf_inv_polynomial(5, x - 18.00, 0.99714565277099609375) // max err: 5.697761e-20
            =>        x * boost_erf_inv_polynomial(6, x - 44.00, 0.99941349029541015625) // max err: 1.279746e-20


// @function Helper for `erfinv`.
boost_erf_inv(float x, float ax) =>
    p = ax
    q = 1 - p
    r = boost_erf_inv_imp(p, q)
    sign(x) < 0 ? -r : r


// @function Helper for `erfcinv`.
boost_erfc_inv(float x) =>
    n = sign(x - 1) < 0
    q = n ? x : 2 - x
    p = 1 - q
    r = boost_erf_inv_imp(p, q)
    n ? r : -r

// }


// ------------------------------------------------ ERROR FUNCTIONS ------------------------------------------------- {

// @function **Error Function**\
// Estimates the theoretical error of a measurement.
// @param x Upper limit of the integration [0, ±∞].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between -1 and 1.
export erf(float x, bool precise = true) =>
    ax = math.abs(x)
    ix = ax.e6()

    // Nan, Infinity, Zero
    if na(x) or na(1 / x)
        math.sign(x) // na, -1, 0, 1

    // Practical precision limit
    else if 6.0e6 <= ix
        x < 0
         ? -LIMIT_1
         :  LIMIT_1

    // Approximation
    else
        math.min(math.max(precise
         ? sun_erf(x, ax, ix)
         : hastings_erf(x, ax)
         , -LIMIT_1) , LIMIT_1)


// @function **Complementary Error Function**\
// Estimates the difference between a theoretical error and infinity.
// @param x Lower limit of the integration [0, ±∞].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between 0 and 2.
export erfc(float x, bool precise = true) =>
    ax = math.abs(x)
    ix = ax.e6()

    // Nan, Infinity, Zero
    if na(x) or na(1 / x)
        1 - math.sign(x) // na, 2, 1, 0

    // Practical precision limit
    else if 6.0e6 <= ix
        x < 0
         ? LIMIT_2
         : LIMIT_0

    // Approximation
    else
        math.min(math.max(precise
         ? sun_erfc(x, ax, ix)
         : 1 - hastings_erf(x, ax)
         , LIMIT_0), LIMIT_2)


// @function **Inverse Error Function**\
// Reverses the `erf` by estimating the original measurement from the theoretical error.
// @param x Theoretical error [-1, 1].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between 0 and ± infinity.
export erfinv(float x, bool precise = true) =>
    ax = math.abs(x)
    ix = ax.e6()

    // Nan, Infinity, Out Of Range
    if na(x) or ix > 1.0e6
        na

    // Zero
    else if na(1 / x)
        0

    // Approximation limit
    else if 1.0e6 <= ix
        x < 0
         ? -INFINITY
         :  INFINITY

    // Approximation
    else
        precise
         ? boost_erf_inv(x, ax)
         : giles_erf_inv(x)


// @function **Inverse Complementary Error Function**\
// Reverses the `erfc` by estimating the original measurement from the difference between the theoretical error and infinity.
// @param x Complement of the theoretical error [0, 2].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between 0 and ± infinity.
export erfcinv(float x, bool precise = true) =>
    x1 = 1 - x // [0, 2] -> [1, -1]
    ix = math.abs(x1).e6()

    // Nan, Infinity, Out Of Range
    if na(x) or ix > 1.0e6
        na

    // One
    else if na(1 / x1)
        0

    // Approximation limit
    else if 1.0e6 <= ix
        x < 0
         ? -INFINITY
         :  INFINITY

    // Approximation
    else
        precise
         ? boost_erfc_inv(x) // Takes [0, 2]
         : giles_erf_inv(x1) // Takes [1,-1]

// }


// --------------------------------------------- DISTRIBUTION FUNCTIONS --------------------------------------------- {

// @function **Probability Density Function**\
// Estimates the probability *density*. For clarity, **density is not a probability**.\
// It's simply the Y coordinate of a Gaussian curve at coordinate X.
// @param x X coordinate for which a density will be estimated [0, ±∞].
// @param mean Mean ( µ )
// @param sigma Sigma ( σ ). While either (or both) Sigma and Variance can be passed into the PDF, only one is required.
// @param variance Variance ( σ² ). While either (or both) Sigma and Variance can be passed into the PDF, only one is required.
// @returns Between 0 and ∞.
export pdf(float x, float mean, float sigma = na, float variance = na) =>
    d = x - mean
    v = nz(variance, sigma * sigma)
    s = nz(sigma, math.sqrt(variance))
    math.exp( -(d * d) / (2 * v) ) / (s * ROOT_TWO_PI)


// @function **Cumulative Distribution Function**\
// Estimates the area under a Gaussian curve between Negative Infinity and the Z Score.
// @param z Z Score [0, ±∞].
// @param precise Double precision ([true](#const_true), default) or single precision ([false](#const_false)).
// @returns Between 0 and 1.
export cdf(float z, bool precise = true) =>
    a  = erf(z / ROOT_TWO, precise)
    a := a * a == 1 ? a * 1.0e16 * 1.0e-16 : a // Keep area within epsilon
    (a + 1) * 0.5


// @function **Complementary Cumulative Distribution Function**\
// Estimates the area under a Gaussian curve between the Z Score and Positive Infinity.
// @param z Z Score [0, ±∞].
// @param precise Double precision ([true](#const_true), default) or single precision ([false](#const_false)).
// @returns Between 0 and 1.
export ccdf(float z, bool precise = true) =>
    a  = erfc(z / ROOT_TWO, precise)
    a := a == 0 and not na(1 / a) ? a + 1.0e-16 : a // Keep area within epsilon
    a * 0.5


// @function **Inverse Cumulative Distribution Function**\
// Reverses the `cdf`by estimating the Z Score from an area.
// @param a Normalized area [0, 1].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between -∞ and +∞
export cdfinv(float a, bool precise = true) =>
    erfinv(2 * a - 1, precise) * ROOT_TWO


// @function **Inverse Complementary Cumulative Distribution Function**\
// Reverses the `ccdf`by estimating the Z Score from an area.
// @param a Normalized area [0, 1].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between -∞ and +∞
export ccdfinv(float a, bool precise = true) =>
    erfcinv(2 * a, precise) * ROOT_TWO


// @function **Cumulative Distribution Function** from **A** to **B**\
// Estimates the area under a Gaussian curve between two Z Scores (A and B).
// @param z1 First Z Score [0, ±∞].
// @param z2 Second Z Score [0, ±∞].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between 0 and 1.
export cdfab(float z1, float z2, bool precise = true) =>
    cdf(math.max(z1, z2), precise) - cdf(math.min(z1, z2), precise)


// @function **One-Tailed Test**\
// Estimates the area under a Gaussian curve between an absolute Z Score and Positive Infinity.
// @param z Z Score [0, ±∞].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between 0 and 0.5.
export ott(float z, bool precise = true) =>
    ccdf(math.abs(z), precise)


// @function **Two-Tailed Test**\
// Estimates the area under a Gaussian curve between symmetrical ± Z Scores and ± Infinity.
// @param z One of the symmetrical Z Scores [0, ±∞].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between 0 and 1.
export ttt(float z, bool precise = true) =>
    az  = math.abs(z)
    two = 8.3 < az ? 1 : 2 // Allows the result to be 1.0e-16 when the Z score is sufficiently large.
    ccdf(az, precise) * two


// @function **Inverse One-Tailed Test**\
// Reverses the `ott` by estimating the absolute Z Score from an area.
// @param a Half of a normalized area [0, 0.5].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between 0 and ∞.
export ottinv(float a, bool precise = true) =>
    a < 0 or 0.5 < a ? na : ccdfinv(a, precise)


// @function **Inverse Two-Tailed Test**\
// Reverses the `ttt` by estimating the absolute Z Score from an area.
// @param a Normalized area [0, 1].
// @param precise Double precision ([true](#const_true)) or single precision ([false](#const_false)).
// @returns Between 0 and ∞.
export tttinv(float a, bool precise = true) =>
    a < 0 or 1 < a ? na : erfcinv(a, precise) * ROOT_TWO

// }
````
