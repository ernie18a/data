<!-- tradingview-pine-id: PUB;82e54baf76db4df79493b2287db52efe -->
<!-- tradingviewscripts-format: 1 -->
# PriceFormat

Source: https://www.tradingview.com/script/An7YRDFo-PriceFormat/

## Description

Library for automatically converting price values to formatted strings
matching the same format that TradingView uses to display open/high/low/close prices on the chart.

█ OVERVIEW
This library is intended for Pine Coders who are authors of scripts that display numbers onto a user's charts.  Typically, 𝚜𝚝𝚛.𝚝𝚘𝚜𝚝𝚛𝚒𝚗𝚐() would be used to convert a number into a string which can be displayed in a label / box / table, but this only works well for values that are formatted as a simple decimal number.  The purpose of this library is to provide an easy way to create a formatted string for values which use other types of formats besides the decimal format.

The main functions exported by this library are:

[*] 𝚏𝚘𝚛𝚖𝚊𝚝𝙿𝚛𝚒𝚌𝚎() - creates a formatted string from a price value
[*] 𝚖𝚎𝚊𝚜𝚞𝚛𝚎𝙿𝚛𝚒𝚌𝚎𝙲𝚑𝚊𝚗𝚐𝚎() - creates a formatted string from the distance between two prices
[*] 𝚝𝚘𝚜𝚝𝚛𝚒𝚗𝚐() - an alternative to the built-in 𝚜𝚝𝚛.𝚝𝚘𝚜𝚝𝚛𝚒𝚗𝚐(𝚟𝚊𝚕𝚞𝚎, 𝚏𝚘𝚛𝚖𝚊𝚝)

This library also exports some auxiliary functions which are used under the hood of the previously mentioned functions, but can also be useful to Pine Coders that need fine-tuned control for customized formatting of numeric values:

[*] Functions that determine information about the current chart:
𝚒𝚜𝙵𝚛𝚊𝚌𝚝𝚒𝚘𝚗𝚊𝚕𝙵𝚘𝚛𝚖𝚊𝚝(), 𝚒𝚜𝚅𝚘𝚕𝚞𝚖𝚎𝙵𝚘𝚛𝚖𝚊𝚝(), 𝚒𝚜𝙿𝚎𝚛𝚌𝚎𝚗𝚝𝚊𝚐𝚎𝙵𝚘𝚛𝚖𝚊𝚝(), 𝚒𝚜𝙳𝚎𝚌𝚒𝚖𝚊𝚕𝙵𝚘𝚛𝚖𝚊𝚝(), 𝚒𝚜𝙿𝚒𝚙𝚜𝙵𝚘𝚛𝚖𝚊𝚝()
[*] Functions that convert a 𝚏𝚕𝚘𝚊𝚝 value to a formatted string:
𝚊𝚜𝙳𝚎𝚌𝚒𝚖𝚊𝚕(), 𝚊𝚜𝙿𝚒𝚙𝚜(), 𝚊𝚜𝙵𝚛𝚊𝚌𝚝𝚒𝚘𝚗𝚊𝚕(), 𝚊𝚜𝚅𝚘𝚕𝚞𝚖𝚎()

█ EXAMPLES

• Simple Example

This example shows the simplest way to utilize this library.
[image]https://www.tradingview.com/x/EODKCVwB/[/image][pine]
//@version=6
indicator("Simple Example")

import n00btraders/PriceFormat/1

var table t = table.new(position.middle_right, 2, 1, bgcolor = color.new(color.blue, 90), force_overlay = true)
if barstate.isfirst
    table.cell(t, 0, 0, "Current Price: ", text_color = color.black, text_size = 40)
    table.cell(t, 1, 0, text_color = color.blue, text_size = 40)

if barstate.islast
    string lastPrice = close.formatPrice()      // Simple, easy way to format price
    table.cell_set_text(t, 1, 0, lastPrice)
[/pine]

• Complex Example

This example calls all of the main functions and uses their optional arguments.
[image]https://www.tradingview.com/x/25ZKGKh4/[/image][pine]
//@version=6
indicator("Complex Example")

import n00btraders/PriceFormat/1

// Enum values that can be used as optional arguments
precision = input.enum(PriceFormat.Precision.DEFAULT)
language = input.enum(PriceFormat.Language.ENGLISH)

// Main library functions used to create formatted strings
string formattedOpen = open.formatPrice(precision, language, allowPips = true)
string rawOpenPrice = PriceFormat.tostring(open, format.price)

string formattedClose = close.formatPrice(precision, language, allowPips = true)
string rawClosePrice = PriceFormat.tostring(close, format.price)

[distance, ticks] = PriceFormat.measurePriceChange(open, close, precision, language, allowPips = true)

// Labels to display formatted values on chart
string prices = str.format("Open:   {0}  ({1})\n\nClose:   {2}  ({3})", formattedOpen, rawOpenPrice, formattedClose, rawClosePrice)
string change = str.format("Change (close - open):\n\n{0}   /   {1}", distance, ticks)
label.new(chart.point.now(high), prices, yloc = yloc.abovebar, textalign = text.align_left, force_overlay = true)
label.new(chart.point.now(low), change, yloc = yloc.belowbar, style = label.style_label_up, force_overlay = true)
[/pine]

█ NOTES

• Function Descriptions

The library source code uses [Markdown](https://www.tradingview.com/script/b6aw56xH-Markdown-The-Pine-Editor-s-Hidden-Gem/) for the exported functions. Hover over a function/method call in the Pine Editor to display formatted, detailed information about the function/method.
[image]https://www.tradingview.com/x/FGBvXd5I/[/image]

• Precision Settings

The Precision option in the chart settings can change the format of how prices are displayed on the chart.  Since the user's selected choice cannot be known through any Pine built-in variable, this library provides a 𝙿𝚛𝚎𝚌𝚒𝚜𝚒𝚘𝚗 enum that can be used as an optional script input for the user to specify their selected choice.
[image]https://www.tradingview.com/x/4hFX3XYc/[/image]

• Language Settings

The Language option in the user menu can change the decimal/grouping separators in the prices that are displayed on the chart.  Since the user's selected choice cannot be known through any Pine built-in variable, this library provides a 𝙻𝚊𝚗𝚐𝚞𝚊𝚐𝚎 enum that can be used as an optional script input for the user to specify their selected choice.
[image]https://www.tradingview.com/x/eEmm8BiM/[/image]

█ EXPORTED FUNCTIONS

method formatPrice(price, precision, language, allowPips)
  Formats a price value to match how it would be displayed on the user's current chart.
  Namespace types: series float, simple float, input float, const float
  Parameters:
    price (float): The value to format.
    precision (series Precision): A Precision.* enum value.
    language (series Language): A Language.* enum value.
    allowPips (simple bool): Whether to allow decimal numbers to display as pips.
  Returns: Automatically formatted price string.

measurePriceChange(startPrice, endPrice, precision, language, allowPips)
  Measures a change in price in terms of both distance and ticks.
  Parameters:
    startPrice (float): The starting price.
    endPrice (float): The ending price.
    precision (series Precision): A Precision.* enum value.
    language (series Language): A Language.* enum value.
    allowPips (simple bool): Whether to allow decimal numbers to display as pips.
  Returns: A tuple of formatted strings: [string distance, string ticks].

method tostring(value, format)
  Alternative to the Pine `str.tostring(value, format)` built-in function.
  Namespace types: series float, simple float, input float, const float
  Parameters:
    value (float): (series float) The value to format.
    format (string): (series string) The format string.
  Returns: String in the specified format.

isFractionalFormat()
  Determines if the default behavior of the chart's price scale is to use a fractional format.
  Returns: True if the chart can display prices in fractional format.

isVolumeFormat()
  Determines if the default behavior of the chart's price scale is to display prices as volume.
  Returns: True if the chart can display prices as volume.

isPercentageFormat()
  Determines if the default behavior of the chart's price scale is to display percentages.
  Returns: True if the chart can display prices as percentages.

isDecimalFormat()
  Determines if the default behavior of the chart's price scale is to use a decimal format.
  Returns: True if the chart can display prices in decimal format.

isPipsFormat()
  Determines if the current symbol's prices can be displayed as pips.
  Returns: True if the chart can display prices as pips.

method asDecimal(value, precision, minTick, decimalSeparator, groupingSeparator, eNotation)
  Converts a number to a string in decimal format.
  Namespace types: series float, simple float, input float, const float
  Parameters:
    value (float): The value to format.
    precision (int): Number of decimal places.
    minTick (float): Minimum tick size.
    decimalSeparator (string): The decimal separator.
    groupingSeparator (string): The thousands separator, aka digit group separator.
    eNotation (bool): Whether the result should use E notation.
  Returns: String in decimal format.

method asPips(value, priceScale, minMove, minMove2, decimalSeparator, groupingSeparator)
  Converts a number to a string in decimal format with the last digit replaced by a superscript.
  Namespace types: series float, simple float, input float, const float
  Parameters:
    value (float): The value to format.
    priceScale (int): Price scale.
    minMove (int): Min move.
    minMove2 (int): Min move 2.
    decimalSeparator (string): The decimal separator.
    groupingSeparator (string): The thousands separator, aka digit group separator.
  Returns: String in decimal format with an emphasis on the pip value.

method asFractional(value, priceScale, minMove, minMove2, fractionalSeparator1, fractionalSeparator2)
  Converts a number to a string in fractional format.
  Namespace types: series float, simple float, input float, const float
  Parameters:
    value (float): The value to format.
    priceScale (int): Price scale.
    minMove (int): Min move.
    minMove2 (int): Min move 2.
    fractionalSeparator1 (string): The primary fractional separator.
    fractionalSeparator2 (string): The secondary fractional separator.
  Returns: String in fractional format.

method asVolume(value, precision, minTick, decimalSeparator, groupingSeparator, spacing)
  Converts a number to a string in volume format.
  Namespace types: series float, simple float, input float, const float
  Parameters:
    value (float): The value to format.
    precision (int): Maximum number of decimal places.
    minTick (float): Minimum tick size.
    decimalSeparator (string): The decimal separator.
    groupingSeparator (string): The thousands separator, aka digit group separator.
    spacing (string): The whitespace separator.
  Returns: String in volume format.

---

## Source Code

````pine
// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © n00btraders

//@version=6

// @description  Library for automatically converting price values to formatted strings
//               matching the same format that TradingView charts use to display prices.
library("PriceFormat")




//#region ------------------------------ Constants ------------------------------

// Max value of 16 for `float precision` parameters.
// This is the length of the following `MAX_DECIMALS_FORMAT` and `MAX_DECIMALS_TRAILING_ZEROS_FORMAT` constants.
// Note: uses the same max value as the `precision` parameter of the `indicator()`, `strategy()`, and `plot()` built-in functions.
const int MAX_PRECISION = 16

// Format string for a number of decimal places (excluding trailing zeros) which can be used in `str.tostring()`.
const string MAX_DECIMALS_FORMAT = str.repeat("#", MAX_PRECISION)

// Format string for a number of decimal places (including trailing zeros) which can be used in `str.tostring()`.
const string MAX_DECIMALS_TRAILING_ZEROS_FORMAT = str.repeat("0", MAX_PRECISION)

// Extended version of `MAX_DECIMALS_FORMAT`.
const string EXTENDED_MAX_DECIMALS_FORMAT = str.repeat(MAX_DECIMALS_FORMAT, 3)

// Length of the `EXTENDED_MAX_DECIMALS_FORMAT` constant.
const int EXTENDED_MAX_DECIMALS_LENGTH = str.length(EXTENDED_MAX_DECIMALS_FORMAT)

// Custom constant for Chinese Yuan Offshore because there is no `currency.CNH` built-in constant.
const string CURRENCY_CNH = "CNH"

// @enum  Represents the possible formats of prices for the current symbol.
enum Format
    DECIMAL
    FRACTIONAL
    PERCENTAGE
    PIPS
    VOLUME

//#endregion




//#region ------------------------------ Exported Enums ------------------------------

// @enum  Precision options available in the TradingView chart settings.
export enum Precision
    DEFAULT        = "Default"
    INTEGER        = "Integer"
    DECIMAL_1      = "1 decimal"
    DECIMAL_2      = "2 decimals"
    DECIMAL_3      = "3 decimals"
    DECIMAL_4      = "4 decimals"
    DECIMAL_5      = "5 decimals"
    DECIMAL_6      = "6 decimals"
    DECIMAL_7      = "7 decimals"
    DECIMAL_8      = "8 decimals"
    DECIMAL_9      = "9 decimals"
    DECIMAL_10     = "10 decimals"
    DECIMAL_11     = "11 decimals"
    DECIMAL_12     = "12 decimals"
    DECIMAL_13     = "13 decimals"
    DECIMAL_14     = "14 decimals"
    DECIMAL_15     = "15 decimals"
    FRACTIONAL_2   = "1/2"
    FRACTIONAL_4   = "1/4"
    FRACTIONAL_8   = "1/8"
    FRACTIONAL_16  = "1/16"
    FRACTIONAL_32  = "1/32"
    FRACTIONAL_64  = "1/64"
    FRACTIONAL_128 = "1/128"
    FRACTIONAL_320 = "1/320"

// @enum  Language options available in the TradingView user menu.
export enum Language
    ENGLISH             = "English"
    SPANISH             = "Español"
    INDIAN_ENGLISH      = "English (India)"
    GERMAN              = "Deutsch"
    FRENCH              = "Français"
    ITALIAN             = "Italiano"
    POLISH              = "Polski"
    TURKISH             = "Türkçe"
    RUSSIAN             = "Русский"
    PORTUGUESE          = "Português"
    INDONESIAN          = "Bahasa Indonesia"
    MALAY               = "Bahasa Melayu"
    THAI                = "ภาษาไทย"
    VIETNAMESE          = "Tiếng Việt"
    JAPANESE            = "日本語"
    KOREAN              = "한국어"
    CHINESE_SIMPLIFIED  = "简体中文"
    CHINESE_TRADITIONAL = "繁體中文"
    ARABIC              = "العربية"
    HEBREW              = "עברית"

//#endregion




//#region ------------------------------ Symbol Information Analysis ------------------------------

// @function  Determines if the default behavior of the chart's price scale is to display prices in fractional format.
// @returns   True if the chart can display prices in fractional format.
export isFractionalFormat() =>
    // Decimal format:     `syminfo.pricescale` = 10ⁿ
    // Fractional format:  `syminfo.pricescale` = 2ⁿ
    // Reference: https://www.tradingview.com/charting-library-docs/latest/api/interfaces/Charting_Library.LibrarySymbolInfo/#pricescale
    //
    // Based on the above information, the chart uses fractional formatting if the price scale is a power of 2:
    //     math.log(syminfo.pricescale) / math.log(2) % 1 == 0
    //
    // But the above condition does not work for a price scale of 320 (not a power of 2), which is a valid price scale
    // because the "1/320" fraction is a selectable choice in the Precision drop-down of the TradingView chart settings.
    //
    // Therefore, instead of checking for a power of 2, this function will check if the price scale is NOT a power of 10:
    //     math.log10(syminfo.pricescale) % 1 != 0
    //
    // The above condition can be simplified to check if the price scale is not divisible by 10.
    // Note: must also check if price scale > 1 because the number 1 is not divisible by 10,
    // but a price scale of 1 is used for the decimal format, not the fractional format.
    syminfo.pricescale > 1 and syminfo.pricescale % 10 != 0



// @function  Determines if the default behavior of the chart's price scale is to display prices as volume.
// @returns   True if the chart can display prices as volume.
export isVolumeFormat() =>
    // Crypto market cap charts & TVL charts display as volume
    bool cryptoVolumeType = str.endswith(syminfo.description, "$") or str.endswith(syminfo.description, "Total Value Locked")
    // Economic charts use volume format even for percent indicators like interest rates
    syminfo.type == "economic" or (syminfo.type == "crypto" and cryptoVolumeType)



// @function  Determines if the default behavior of the chart's price scale is to display prices as a percentage.
// @returns   True if the chart can display prices as percentages.
export isPercentageFormat() =>
    // Currency should be "NONE" to display as percent
    bool unspecifiedCurrency = na(syminfo.currency) or syminfo.currency == currency.NONE
    // Government Bond Yields display as percentages but Government Bonds are displayed with a decimal or fractional format
    bool isGovernmentBond = str.endswith(syminfo.description, "Government Bonds")
    // Crypto dominance charts can be displayed as percentages
    bool isPercentage = str.contains(syminfo.description, " percent ") or str.endswith(syminfo.description, "%")
    unspecifiedCurrency and ((syminfo.type == "bond" and not isGovernmentBond) or (syminfo.type == "crypto" and isPercentage))



// @function  Determines if the default behavior of the chart's price scale is to display prices in decimal format.
// @returns   True if the chart can display prices in decimal format.
export isDecimalFormat() =>
    // Does not need to check for pips format because that is already a decimal format
    not isFractionalFormat() and not isVolumeFormat() and not isPercentageFormat()



// @function        Convenience function for `syminfo.currency` comparison.
// @param currency  (const string) Quote currency from a base/quote currency pair.
// @returns         True if `syminfo.currency` matches the specified quote currency.
quote(const string currency) =>
    syminfo.currency == currency



// @function        Convenience function for `syminfo.basecurrency` comparison.
// @param currency  (const string) Base currency from a base/quote currency pair.
// @returns         True if `syminfo.basecurrency` matches the specified base currency.
base(const string currency) =>
    syminfo.basecurrency == currency



// @function  Determines if the current symbol's prices can be displayed as pips.
// ___
// **Remarks** \
// ICE Data Services ([syminfo.prefix](https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.prefix)
// = FX_IDC) provides data for thousands of forex symbols, \
// which makes it difficult to verify the result of this function for all available symbols. \
// This function will only focus on providing accuracy for the
// [Major](https://www.tradingview.com/markets/currencies/rates-major/),
// [Minor](https://www.tradingview.com/markets/currencies/rates-minor/),
// and [Exotic](https://www.tradingview.com/markets/currencies/rates-exotic/) currency pairs. \
// \
// For all other Forex exchanges / data providers, this function provides 99.9%+ accuracy \
// for determining if a symbol can display prices as pips.
// ___
// @returns  True if the chart can display prices as pips.
export isPipsFormat() =>

    // A forex market can display pips if there is a "Pip size" listed when viewing Security Info on the current chart.
    // If the market does not display pips then only a "Tick size" will be listed in the Security Info.
    //
    // This function attempts to automatically determine if the current symbol can display pips.
    // Note: Pine does not provide a `syminfo.minmove2` built-in variable, so this function cannot simply check `syminfo.minmove2 == 10`.
    // Reference: https://www.tradingview.com/charting-library-docs/latest/connecting_data/Symbology/#how-to-display-pips

    if syminfo.type != "forex"
        // Only forex markets will display pips.
        // Note: CFDs for forex pairs will also be 'forex' type.
        false

    else if syminfo.pricescale == 1
        // Market prices without any decimal places will not display pips
        false

    else if str.contains(syminfo.tickerid, "\"currency-id\"")
        // If chart data is converted to another currency, the ticker identifier will have a 'currency-id' modifier.
        // This causes "Pip size" to be removed from Security Info, therefore the chart would not display pips.
        false

    else if syminfo.prefix == "FOREXCOM"
        // Custom rules for FOREX.com
        bool XXXCNH = quote(CURRENCY_CNH) and not base(currency.USD)    // any XXX/CNH pair (except USD/CNH)
        quote(currency.JPY) or (syminfo.pricescale >= 1e5 and not XXXCNH)

    else if syminfo.prefix == "SAXO"
        // Custom rules for Saxo
        bool baseCurrencyDKKorPLN = base(currency.DKK) or base(currency.PLN)
        bool baseCurrencyNOKorSEK = base(currency.NOK) or base(currency.SEK)
        switch syminfo.pricescale
            1e3 => quote(currency.HUF) or (quote(currency.JPY) and not baseCurrencyDKKorPLN)    // 3 decimal places:  any XXX/HUF or XXX/JPY pair (except DKK/JPY & PLN/JPY)
            1e4 => quote(currency.CZK) or (quote(currency.JPY) and baseCurrencyNOKorSEK)        // 4 decimal places:  any XXX/CZK pair or NOK/JPY or SEK/JPY
            1e5 => not quote(currency.PLN) or not base(currency.CZK)                            // 5 decimal places:  any pair except CZK/PLN
            1e6 => base(currency.JPY) and not quote(currency.HKD)                               // 6 decimal places:  any JPY/XXX pair (except JPY/HKD)
            => syminfo.pricescale >= 1e3

    else if syminfo.prefix == "SKILLING"
        // Custom rules for Skilling
        bool currencyCZKorRUB = quote(currency.CZK) or quote(currency.RUB)
        syminfo.pricescale == 1e3 or (syminfo.pricescale == 1e4 and currencyCZKorRUB) or syminfo.pricescale >= 1e5

    else if syminfo.prefix == "FX_IDC"
        // Custom rules for ICE Data Services
        if syminfo.ticker != syminfo.basecurrency + syminfo.currency
            // The ticker will not match the base/quote currency pair if it has a reference rate (e.g., USDEUX = U.S. DOLLAR / EURO REFERENCE RATE)
            syminfo.pricescale >= 1e5 and not quote(currency.EUR) and not quote(currency.AUD)
        else
            // Checks if the base and/or quote currency matches any of the currencies that appear on this page: https://www.tradingview.com/markets/currencies/rates-major/
            array<string> majorCurrencies = array.from(currency.USD, currency.CAD, currency.EUR, currency.GBP, currency.CHF, currency.AUD, currency.NZD, currency.JPY)
            bool hasMajorCurrency = majorCurrencies.includes(syminfo.currency) or majorCurrencies.includes(syminfo.basecurrency)
            (syminfo.pricescale == 1e3 and quote(currency.JPY)) or (syminfo.pricescale >= 1e5 and hasMajorCurrency)

    else
        // All other exchanges (with a few simple exceptions) will display pips for most, if not all, available currency pairs
        switch syminfo.prefix
            "CITYINDEX"   => false
            "GBEBROKERS"  => false
            "JFX"         => false
            "RUS"         => false
            "TRADENATION" => false
            "BLACKBULL"   => syminfo.pricescale != 1e4 or not quote(currency.DKK)
            "FX"          => syminfo.pricescale != 1e3 or not quote(currency.HUF)
            "ICMARKETS"   => (syminfo.pricescale != 1e3 or not quote(currency.RUB)) and (syminfo.pricescale != 1e4 or not quote(currency.HKD))
            => true



// @function               Determines the default format that is used to display prices on the current chart. \
//                         Since charts themselves do not display prices in pips, the option to do so is controlled via an explicit parameter. \
//                         Note: the result of the function will not change on a bar-by-bar basis because it is based on \
//                         the `syminfo.*` variables, so the returned value can be declared as `var` to minimize recalculations.
// @param allowPipsFormat  (simple bool) Whether the pips format is allowed to be used for formatting prices.
// @returns                A `Format` enum value.
determinePriceFormat(simple bool allowPipsFormat = false) =>
    isPipsFormat() and allowPipsFormat ? Format.PIPS : isFractionalFormat() ? Format.FRACTIONAL : isVolumeFormat() ? Format.VOLUME : isPercentageFormat() ? Format.PERCENTAGE : Format.DECIMAL

//#endregion




//#region ------------------------------ Custom Formatters ------------------------------

// @function  Converts a number to a string in decimal format. \
//            \
//            The result will be rounded to the min tick size if no precision is specified. \
//            If a precision is specified, the result will be rounded to that amount of decimal places. \
//            \
//            Regular decimal numbers will include trailing zeros but E notation will trim any trailing zeros.
// ___
// **Parameters**
// ```
// • series int precision
// • series float minTick
// • series string decimalSeparator
// • series string groupingSeparator
// • series bool eNotation
// ```
// `precision` - Number of decimal places to use.  Should be a value >= 0 and <= 16 or a value of [na](https://www.tradingview.com/pine-script-reference/v6/#var_na)
// (in which case min tick size determines # of decimal places).  Default value is [na](https://www.tradingview.com/pine-script-reference/v6/#var_na). \
// `minTick` - Minimum tick value.  Should be a value > 0.  Only used when the precision argument is [na](https://www.tradingview.com/pine-script-reference/v6/#var_na).
// Default value is [syminfo.mintick](https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.mintick). \
// `decimalSeparator` - The character to use for separating the integer and fractional part of the result.  Default value is "." (period). \
// `groupingSeparator` - The character to use for separating groups of digits in the result.  Should be different from the decimal separator.  Default value is "" (empty string). \
// `eNotation` - Determines if the result should be expressed in E notation.  Default value is [false](https://www.tradingview.com/pine-script-reference/v6/#const_false). \
// \
// Allows [na](https://www.tradingview.com/pine-script-reference/v6/#var_na) value: `precision`, `groupingSeparator`
// ___
// **Remarks** \
// The Parameters section describes the acceptable values for each argument, including which
// arguments are allowed to be [na](https://www.tradingview.com/pine-script-reference/v6/#var_na). \
// This function will not generate a custom runtime error for invalid arguments, but the behavior is undefined. \
// \
// If E notation is used, any |value| < 1 will begin with a zero ("0.###e-#"), \
// and any |value| >= 1 will begin with a single digit that is not zero ("#.###e+#").
// ___
// **Usage**
// > ```
// > float value = 1000.234
// >
// > // Example: using a minimum tick size
// > value.asDecimal(precision = na, minTick = 0.25)  //"1000.25"
// >
// > // Example: using a specific precision
// > value.asDecimal(precision = 5)  //"1000.23400"
// >
// > // Example: modifying separators
// > value.asDecimal(precision = 1, decimalSeparator = ",", groupingSeparator = ".")  //"1.000,2"
// >
// > // Example: displaying in E notation
// > value.asDecimal(precision = 1, eNotation = true)  //"1.0002e+3"
// > ```
// ___
// @param value              (series float) The value to format.
// @param precision          (series int) Number of decimal places.
// @param minTick            (series float) Minimum tick size.
// @param decimalSeparator   (series string) The decimal separator.
// @param groupingSeparator  (series string) The thousands separator, aka digit group separator.
// @param eNotation          (series bool) Whether the result should use E notation.
// @returns                  String in decimal format.
export method asDecimal(float value, int precision = na, float minTick = syminfo.mintick, string decimalSeparator = ".", string groupingSeparator = "", bool eNotation = false) =>

    // Note: some of these calculations may seem unnecessary at first glance but counting the number of decimal places
    // is crucial for accuracy, otherwise the result might display more decimal places than is actually shown in chart prices
    // (which can be caused by loss of precision when performing operations on floating-point numbers).

    // If `precision` is `na`: round the price to the minimum tick size.
    // Reference: https://www.tradingview.com/pine-script-docs/faq/functions/#how-can-i-round-a-number-to-x-increments
    //
    // If `precision` is not `na`: rounding is not performed at this stage because the `str.tostring()` conversions
    // will automatically round the result to the required number of decimal places.
    float _value = na(precision) ? math.round(value / minTick) * minTick : value

    // If `precision` is `na`, use the tick size to determine the number of decimal places.
    // Otherwise, use the specified precision (which should be between 0 and 16).
    int _precision = if na(precision)
        // Convert the tick size to a string (excluding trailing zeros) to count the number of decimal places.
        // Uses a custom `format` parameter because the default format only includes up to 10 decimal places.
        //
        // Note: some markets (e.g., crypto) may have a minimum tick size with even more than 16 decimal places,
        // therefore the custom `format` value should be large enough to account for those scenarios.
        string minTickString = str.tostring(minTick, "0." + EXTENDED_MAX_DECIMALS_FORMAT)
        int decimalPointPosition = str.pos(minTickString, ".")
        na(decimalPointPosition) ? 0 : str.length(minTickString) - 1 - decimalPointPosition
    else
        // Custom precision assumes a minimum tick size of:  1 / math.pow(10, precision)
        precision < 0 ? 0 : (precision > MAX_PRECISION ? MAX_PRECISION : precision)


    // Build the main format string
    string format = if not eNotation
        // Limit to 16 decimal places, even if the minimum tick size had more decimal places
        int decimals = _precision < MAX_PRECISION ? _precision : MAX_PRECISION
        decimals == 0 ? "#,##0" : "#,##0." + str.substring(MAX_DECIMALS_TRAILING_ZEROS_FORMAT, 0, decimals)
    else
        // Create a string that represents the original value but rounded to the minimum tick size,
        // which will be used to count the maximum number of decimal places that should be included in the E notation
        int decimals = _precision < EXTENDED_MAX_DECIMALS_LENGTH ? _precision : EXTENDED_MAX_DECIMALS_LENGTH
        string valueString = str.tostring(_value, decimals == 0 ? "0" : "0." + str.substring(EXTENDED_MAX_DECIMALS_FORMAT, 0, decimals))

        if str.startswith(valueString, "-")
            valueString := str.substring(valueString, 1)    // Convert to absolute value for easier decision-making

        // The expressions in the following `if` structure use the string representation of the value
        // rather than the value itself because comparison operators round the values to 9 fractional digits,
        // which may give incorrect results for very small numbers (e.g., `1e-10 > 0` evaluates to `false`).
        // Reference: https://www.tradingview.com/pine-script-docs/language/type-system/#float

        // 1) Converted string represents a value of 0.
        //    The result should be the number 0, followed by no decimals at all.
        if valueString == "0"
            "0"

        // 2) Converted string represents a value between 0 and 1 (exclusive).
        //    The result should begin with a '0', followed by some amount of decimals.
        //
        //    Note: any leading zeros are not included in the fractional part of the E notation,
        //    therefore the number of decimal places to use is equal to the total number of decimal places,
        //    minus the number of zeros directly after the decimal point.
        else if str.startswith(valueString, "0.")
            int zerosAfterDecimalPoint = 0
            for i = 2 to str.length(valueString) - 1
                if str.substring(valueString, i, i + 1) != "0"
                    break
                zerosAfterDecimalPoint += 1
            int significantDigits = str.length(valueString) - 2 - zerosAfterDecimalPoint
            "'0'." + str.substring(EXTENDED_MAX_DECIMALS_FORMAT, 0, significantDigits) + "E0"

        // 3) Converted string represents a value greater than or equal to 1.
        //    The result should begin with a non-zero digit (unlike the previous condition),
        //    followed by some amount of decimals (or no decimals at all).
        //
        //    Note: the number of decimal places to use in the E notation is equal to the total number of digits,
        //    minus 1 (excludes the very first digit which will be placed BEFORE the decimal point).
        else
            int significantDigits = str.length(valueString) - (str.contains(valueString, ".") ? 1 : 0)
            significantDigits == 1 ? "0E0" : "0." + str.substring(EXTENDED_MAX_DECIMALS_FORMAT, 0, significantDigits - 1) + "E0"


    string result = str.tostring(_value, format)

    // Post-processing to replace the decimal/grouping separators
    if not eNotation
        // Pseudocode:
        //     result := str.replace_all(result, ".", decimalSeparator)
        //     result := str.replace_all(result, ",", groupingSeparator)
        // Note: actual code is different so it can work in the scenario where `decimalSeparator` is a comma (",")
        array<string> parts = str.split(result, ".")
        parts.set(0, str.replace_all(parts.first(), ",", groupingSeparator))
        parts.join(decimalSeparator)
    else
        // Only need to replace the decimal separator because E notation does not use grouping separators
        result := str.replace(result, ".", decimalSeparator)
        // Exponents are displayed with a lower case 'e' followed by a plus or minus sign
        str.contains(result, "E-") ? str.replace(result, "E", "e") : str.replace(result, "E", "e+")



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// 𝗣𝗜𝗣𝗦 𝗙𝗢𝗥𝗠𝗔𝗧 𝗜𝗡𝗙𝗢
//
// Pips format is a type of decimal format but it allows emphasizing the pip value.
//
// The open/high/low/close prices on forex charts are displayed using decimal format,
// but the pips format can be viewed by opening the symbol's overview page.
//
// Relevant Links:
//
// 1) https://www.tradingview.com/charting-library-docs/latest/connecting_data/Symbology/#how-to-display-pips
//    - Contains an example of displaying pips and details about the 'minMove2' value
//
//
//
// 𝗜𝗡𝗖𝗢𝗥𝗣𝗢𝗥𝗔𝗧𝗜𝗡𝗚 `𝗺𝗶𝗻𝗠𝗼𝘃𝗲𝟮`
//
// Pine does not provide a `syminfo.minmove2` built-in variable.
// Unless specified by the user (which is not always practical), the only other option is to try and infer its value.
//
//
// • What is `minMove2`?
//   This property is used to display pips in forex symbols.
//   There are two decimal forms:
//       1) 0.97839    - display in ticks (`minMove2` is 0)
//       2) 0.9783⁹    - display in pips  (`minMove2` is 10)
//   Reference: https://www.tradingview.com/charting-library-docs/latest/connecting_data/Symbology/#how-to-display-pips
//
//
// • What are the possible values of `minMove2`?
//   `minMove2` = 0 or 10
//
//
// • How is `minMove2` used?
//   The value is set to 10 to make fractional pips (aka pipettes) look smaller than the price digits.
//   E.g., symbol OANDA:EURUSD:
//       - `minMove` = 1, `priceScale` = 100000, `minMove2` = 10
//       - A price of 0.97839 can be displayed as 0.9783⁹
//   Example from: https://www.tradingview.com/charting-library-docs/latest/connecting_data/Symbology/#how-to-display-pips
//
//
// • How can `minMove2` be inferred?
//   ┏━━━━━━━━━━━━━━━━┳━━━━━━━━━━━┳━━━━━━━━━━┳━━━━━━━━━━━━━━┳━━━━━━━━━━━━━━━━━━━━━━┳━━━━━━━━━━━━━━━━━━━┳━━━━━━━━━━━━━━━━━━━━┓
//   ┃ Forex Market   ┃ Tick size ┃ Pip size ┃ Sample Price ┃ `syminfo.pricescale` ┃ `syminfo.minmove` ┃ `syminfo.minmove2` ┃
//   ┡━━━━━━━━━━━━━━━━╇━━━━━━━━━━━╇━━━━━━━━━━╇━━━━━━━━━━━━━━╇━━━━━━━━━━━━━━━━━━━━━━╇━━━━━━━━━━━━━━━━━━━╇━━━━━━━━━━━━━━━━━━━━┩
//   │ OANDA:EURUSD   │ 0.00001   │ 0.0001   │ 0.9783⁹      │ 100000               │ 1                 │ 10                 │  <-- '10' is known by the example above
//   ├────────────────┼───────────┼──────────┼──────────────┼──────────────────────┼───────────────────┼────────────────────┤
//   │ FX_IDC:GBPUSD  │ 0.0001    │ 0.0001   │ 1.2419       │ 10000                │ 1                 │ 0                  │  <-- '0' because the displayed price is unmodified
//   ├────────────────┼───────────┼──────────┼──────────────┼──────────────────────┼───────────────────┼────────────────────┤
//   │ JFX:USDJPY     │ 0.001     │ n/a      │ 150.239      │ 1000                 │ 1                 │ 0                  │  <-- '0' because the displayed price is unmodified
//   └────────────────┴───────────┴──────────┴──────────────┴──────────────────────┴───────────────────┴────────────────────┘
//   Note: Tick size & Pip size can be viewed in Security Info
//
//   The table shows three possible scenarios:
//       1) "Pip size" is different from "Tick size"   (display pips = true)
//       2) "Pip size" is the same as "Tick size".     (display pips = false)
//       3) There is no "Pip size", only "Tick size"   (display pips = false)
//
//   Based on the sample prices, a market can display pips if the "Pip size" is not n/a AND it is different from the "Tick size".
//   The "Tick size" for any market is already known, but the "Pip size" cannot be known since `syminfo.minmove2` does not actually exist.
//
//   The general rule of thumb is that pip size is 0.0001 for most currency pairs, and 0.01 for Japanese Yen (JPY) pairs.
//   But in reality, pip size and/or tick size can vary across different exchanges (even for the same currency pair),
//   so there is no one-size-fits-all method of determining which currency pairs can display pips.
//
//   Instead, pip size must be determined on a broker-by-broker basis,
//   by scanning through all available currency pairs and then determining the combination of
//   base currency (`syminfo.basecurrency`), quote currency (`syminfo.currency`), and decimal places (`syminfo.pricescale`)
//   that would allow the symbol to display prices as pips.
//
//
// • TL;DR:
//   The `minMove2` variable determines how the last digit of a price value is displayed (0 = normal, 10 = superscript).
//   Since there is no `syminfo.*` variable for it, the value must be "guessed" (which is not straightforward since rules vary by exchange).
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////



// @function  Converts a number to a string in decimal format with the last digit replaced by a superscript character.
// ___
// **Parameters**
// ```
// • series int priceScale
// • series int minMove
// • series int minMove2
// • series string decimalSeparator
// • series string groupingSeparator
// ```
// `priceScale` - Price scale.  Should be 10ⁿ (a power of 10) and >= 1.  Default value is [syminfo.pricescale](https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.pricescale). \
// `minMove` - Minimum movement.  Should be a value >= 1.  Default value is [syminfo.minmove](https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.minmove). \
// `minMove2` - Secondary minimum movement.  Should be 0 or 10ⁿ (a power of 10).  Default value is 0. \
// `decimalSeparator` - The character to use for separating the integer and fractional part of the result.  Default value is "." (period). \
// `groupingSeparator` - The character to use for separating groups of digits in the result.  Should be different from the decimal separator.  Default value is "" (empty string). \
// \
// Allows [na](https://www.tradingview.com/pine-script-reference/v6/#var_na) value: `groupingSeparator`
// ___
// **Remarks** \
// The Parameters section describes the acceptable values for each argument, including which
// arguments are allowed to be [na](https://www.tradingview.com/pine-script-reference/v6/#var_na). \
// This function will not generate a custom runtime error for invalid arguments, but the behavior is undefined. \
// \
// Using a `minMove2` value of 0 will not produce a result with any superscript characters, \
// thus resulting in behavior that is equivalent to calling the `asDecimal()` function. \
// \
// TradingView uses a `minMove2` value of 10 to denote the ability to display pips, \
// but this function will additionally accept any other power of 10 (e.g., 100, 1000, etc.) \
// to allow discarding trailing digits for scenarios where a symbol may have superfluous decimal places. \
// For example, USD/ZAR typically has a pip size of 0.0001 in most exchanges (sample price: 17.2042³), \
// but [FX_IDC:USDZAR](https://www.tradingview.com/symbols/USDZAR/?exchange=FX_IDC) has a
// pip size of 0.00001 (sample price: 17.20423⁰) even though the last digit is superfluous because it is always 0. \
// To display [FX_IDC:USDZAR](https://www.tradingview.com/symbols/USDZAR/?exchange=FX_IDC) prices using
// the typical 0.0001 pip size, increase the `minMove2` value from 10 to 100 which will trim an additional decimal place.
// ___
// **Usage**
// > ```
// > float value = 1000.234
// >
// > // Example: display in ticks, not pips
// > value.asPips(priceScale = 1000, minMove = 1, minMove2 = 0)  //"1000.234"
// >
// > // Example: display in pips, not ticks
// > value.asPips(priceScale = 1000, minMove = 1, minMove2 = 10)  //"1000.23⁴"
// >
// > // Example: trim trailing digits
// > value.asPips(priceScale = 1000, minMove = 1, minMove2 = 100)  //"1000.2³"
// >
// > // Example: modifying separators
// > value.asPips(priceScale = 1000, minMove = 1, minMove2 = 10, decimalSeparator = ",", groupingSeparator = ".")  //"1.000,23⁴"
// > ```
// ___
// @param value              (series float) The value to format.
// @param priceScale         (series int) Price scale.
// @param minMove            (series int) Min move.
// @param minMove2           (series int) Min move 2.
// @param decimalSeparator   (series string) The decimal separator.
// @param groupingSeparator  (series string) The thousands separator, aka digit group separator.
// @returns                  String in decimal format with an emphasis on the pip value.
export method asPips(float value, int priceScale = syminfo.pricescale, int minMove = syminfo.minmove, int minMove2 = 0, string decimalSeparator = ".", string groupingSeparator = "") =>
    // Formula:   syminfo.minmove / syminfo.pricescale = syminfo.mintick
    // Reference: https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.pricescale
    float tickSize = minMove / priceScale

    float pipSize = minMove2 / priceScale

    // Divide the pip size by 10 to add one more decimal place which represents a fraction of a pip (pipette).
    // Note: if `minMove2` is '10' or '0', the tick size does not need adjustment since it already has the correct # of decimal places.
    if minMove2 >= 100
        tickSize := pipSize / 10

    const int precision = int(float(na))    // use `na` precision so that the `asDecimal()` function utilizes the `minTick` argument

    string result = value.asDecimal(precision, tickSize, decimalSeparator, groupingSeparator, eNotation = false)

    bool hasDecimalSeparator = str.contains(result, decimalSeparator)   // make sure that only digits after the decimal point are modified

    if minMove2 >= 10 and hasDecimalSeparator
        int lastDigitPosition = str.length(result) - 1
        string lastDigit = str.substring(result, lastDigitPosition)
        string pipette = switch lastDigit
            "0" => "⁰"
            "1" => "¹"
            "2" => "²"
            "3" => "³"
            "4" => "⁴"
            "5" => "⁵"
            "6" => "⁶"
            "7" => "⁷"
            "8" => "⁸"
            "9" => "⁹"
        str.substring(result, 0, lastDigitPosition) + pipette
    else
        result



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// 𝗙𝗥𝗔𝗖𝗧𝗜𝗢𝗡𝗔𝗟 𝗙𝗢𝗥𝗠𝗔𝗧 𝗜𝗡𝗙𝗢
//
// The fractional format is an alternative way of representing a floating point number instead of the decimal format.
//
// Note: the fractional format itself has various ways that it can be represented.
// E.g., a value of 116'27'8 may also be written as 116.278 or 116-278 or 116-27⅞.
// For the sake of simplicity, this library will focus on replicating how TradingView displays fractional formats.
//
// Relevant Links:
//
// 1) https://www.tradingview.com/charting-library-docs/latest/connecting_data/Symbology/#price-format
//    - Contains information about how prices can be displayed in either Decimal format or Fractional format
//
// 2) https://www.tradingview.com/charting-library-docs/latest/api/interfaces/Charting_Library.LibrarySymbolInfo
//    - Contains additional definitions for: 'format', 'fractional', 'minmov', 'minmove2', 'pricescale'
//
// 3) https://www.cmegroup.com/trading/interest-rates/files/treasury-futures-price-rounding-conventions-2020.pdf
//    - Contains details on how Treasury Futures contract prices are represented
//
// 4) https://www.cmegroup.com/markets/interest-rates/us-treasury.html
//    - Contains actual examples of contract prices that use a fractional format
//
//
//
// 𝗜𝗡𝗖𝗢𝗥𝗣𝗢𝗥𝗔𝗧𝗜𝗡𝗚 `𝗺𝗶𝗻𝗠𝗼𝘃𝗲𝟮`
//
// Pine does not provide a `syminfo.minmove2` built-in variable.
// Unless specified by the user (which is not always practical), the only other option is to try and infer its value.
//
//
// • What is `minMove2`?
//   This property is used to display prices in the "fraction of a fraction" format.
//   There are two fractional forms:
//       1) [xx'yy]       - fraction               (`minMove2` should be 0)
//       2) [xx'yy'zz]    - fraction of a fraction (`minMove2` differs from 0)
//   Reference: https://www.tradingview.com/charting-library-docs/latest/connecting_data/Symbology/#fractional-format
//
//
// • What are the possible values of `minMove2`?
//   `minMove2` = 0 or 2ⁿ  (e.g.,  2¹=2,  2²=4,  2³=8,  ...)
//
//
// • How is `minMove2` used?
//   The value represents a fraction of a fraction, which determines the "zz" part of the [xx'yy'zz] format.
//   E.g., symbol ZFM2023:
//       - `minMove` = 1, `priceScale` = 128, `minMove2` = 4
//       - Minimum tick size is 1/4th of a 32nd of one point
//       - The "yy" part of the [xx'yy'zz] format can change in increments of 1/32
//       - Since `minMove2` = 4, each increment of 1/32 can be subdivided into 4 smaller fractions: 0.0/32, 0.25/32, 0.5/32, 0.75/32
//   Example from: https://www.tradingview.com/charting-library-docs/latest/connecting_data/Symbology/#fraction-of-a-fraction-format
//
//
// • How can `minMove2` be inferred?
//   ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┳━━━━━━━━━━━━━━━━━━━━━━━━━━━┳━━━━━━━━━━━━━━┳━━━━━━━━━━━━━━━━━━━━━━┳━━━━━━━━━━━━━━━━━━━┳━━━━━━━━━━━━━━━━━━━━┓
//   ┃ CME Group Futures Market           ┃ Minimum Price Fluctuation ┃ Sample Price ┃ `syminfo.pricescale` ┃ `syminfo.minmove` ┃ `syminfo.minmove2` ┃
//   ┡━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━╇━━━━━━━━━━━━━━━━━━━━━━━━━━━╇━━━━━━━━━━━━━━╇━━━━━━━━━━━━━━━━━━━━━━╇━━━━━━━━━━━━━━━━━━━╇━━━━━━━━━━━━━━━━━━━━┩
//   │ U.S. Treasury Bond Futures (ZB1!)  │ 1/32 of one point         │ 119'31       │ 32                   │ 1                 │ ?                  │
//   ├────────────────────────────────────┼───────────────────────────┼──────────────┼──────────────────────┼───────────────────┼────────────────────┤
//   │ 10-Year T-Note Futures (ZN1!)      │ 1/2 of 1/32 of one point  │ 119'31'5     │ 64                   │ 1                 │ ?                  │
//   ├────────────────────────────────────┼───────────────────────────┼──────────────┼──────────────────────┼───────────────────┼────────────────────┤
//   │ 5-Year T-Note Futures (ZF1!)       │ 1/4 of 1/32 of one point  │ 119'31'7     │ 128                  │ 1                 │ 4                  │  <-- '4' is known by the example above
//   ├────────────────────────────────────┼───────────────────────────┼──────────────┼──────────────────────┼───────────────────┼────────────────────┤
//   │ 2-Year T-Note Futures (ZT1!)       │ 1/8 of 1/32 of one point  │ 119'31'8     │ 256                  │ 1                 │ ?                  │
//   └────────────────────────────────────┴───────────────────────────┴──────────────┴──────────────────────┴───────────────────┴────────────────────┘
//   Reference (for columns 1 & 2): https://www.cmegroup.com/markets/interest-rates/us-treasury.html
//
//   Analysis of the sample markets:
//       1) The 𝗦𝗮𝗺𝗽𝗹𝗲 𝗣𝗿𝗶𝗰𝗲 column shows that the "yy" part of the prices
//          displayed by TradingView are capped at a value out of 32 (i.e., 0/32, 1/32, 2/32, ..., 30/32, 31/32).
//       2) The 𝗠𝗶𝗻𝗶𝗺𝘂𝗺 𝗣𝗿𝗶𝗰𝗲 𝗙𝗹𝘂𝗰𝘁𝘂𝗮𝘁𝗶𝗼𝗻 column shows that the only difference in tick size is the denominator of the subfractions:
//              - 1/𝟭 of 1/32 of one point
//              - 1/𝟮 of 1/32 of one point
//              - 1/𝟰 of 1/32 of one point
//              - 1/𝟴 of 1/32 of one point
//
//   Based on this analysis and the ZFM2023 example, the following can be concluded:
//       syminfo.minmove2 × 32 = syminfo.pricescale
//
//   The formula can be rewritten as:
//       syminfo.minmove2 = syminfo.pricescale ÷ 32
//
//   Given this formula, the `syminfo.minmove2` column can be completed:
//       ┏━━━━━━━━━━━━━━━━━━━━┓
//       ┃ `syminfo.minmove2` ┃
//       ┡━━━━━━━━━━━━━━━━━━━━┩
//       │ = 32 ÷ 32 = 1      │
//       ├────────────────────┤
//       │ = 64 ÷ 32 = 2      │
//       ├────────────────────┤
//       │ 4                  │  <-- '4' was already known from the ZFM2023 example
//       ├────────────────────┤
//       │ = 256 ÷ 32 = 8     │
//       └────────────────────┘
//
//   The first row is actually a special case because its value should be 0, not 1,
//   since a market that uses a fractional format of [xx'yy] instead of [xx'yy'zz] should set `minMove2` as 0
//   according to https://www.tradingview.com/charting-library-docs/latest/connecting_data/Symbology/#fractional-format.
//
//   Therefore, the revised logic to infer `minMove2` is:
//       minMove2 = syminfo.pricescale > 32 ? syminfo.pricescale / 32 : 0
//
//   Note: this method of inference will not be accurate in (rare) cases that
//   a symbol uses a fractional format of [xx'yy] where "yy" is refined
//   further than 1/32 of one point (e.g., 1/64 of one point or 1/128 of one point).
//   Examples of these exceptions include options contracts for the sample markets listed in the table.
//
//
// • TL;DR:
//   The `minMove2` value determines the possible values for the "zz" part of a fractional format in the form: [xx'yy'zz].
//   Since there is no `syminfo.*` variable for it, the value must be "guessed" (best guess is: minMove2 = syminfo.pricescale ÷ 32).
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////



// @function  Converts a number to a string in fractional format.
// ___
// **Parameters**
// ```
// • series int priceScale
// • series int minMove
// • series int minMove2
// • series string fractionalSeparator1
// • series string fractionalSeparator2
// ```
// `priceScale` - Price scale.  Should be 2ⁿ (a power of 2) and > 1.  Default value is [syminfo.pricescale](https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.pricescale). \
// `minMove` - Minimum movement.  Should be a value of 1 or 2.  Default value is [syminfo.minmove](https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.minmove). \
// `minMove2` - Secondary minimum movement.  Should be 0 or 2ⁿ (a power of 2).  Default value is 0. \
// `fractionalSeparator1` - The character to use for separating the integer and fraction part of the result.  Default value is "'" (apostrophe). \
// `fractionalSeparator2` - The character to use for separating the fraction part and the 'fraction of a fraction' part of the result.  Default value is "'" (apostrophe). \
// \
// Allows [na](https://www.tradingview.com/pine-script-reference/v6/#var_na) value: `fractionalSeparator2`
// ___
// **Remarks** \
// The Parameters section describes the acceptable values for each argument, including which
// arguments are allowed to be [na](https://www.tradingview.com/pine-script-reference/v6/#var_na). \
// This function will not generate a custom runtime error for invalid arguments, but the behavior is undefined. \
// \
// Using a `minMove2` value of 0 will produce a result in the [xx'yy] form, \
// while any other positive value will produce a result in the [xx'yy'zz] form. \
// Only `fractionalSeparator1` is used in the first form. \
// Both `fractionalSeparator1` and `fractionalSeparator2` are used in the second form.
// ___
// **Usage**
// > ```
// > float value = 1000.234
// >
// > // Example: [xx'yy] form
// > value.asFractional(priceScale = 32, minMove = 1, minMove2 = 0)  //"1000'07"
// >
// > // Example: [xx'yy'zz] form
// > value.asFractional(priceScale = 128, minMove = 1, minMove2 = 4)  //"1000'07'5"
// >
// > // Example: modifying separators
// > value.asFractional(priceScale = 128, minMove = 1, minMove2 = 4, fractionalSeparator1 = ".", fractionalSeparator2 = "")  //"1000.075"
// > ```
// ___
// @param value                 (series float) The value to format.
// @param priceScale            (series int) Price scale.
// @param minMove               (series int) Min move.
// @param minMove2              (series int) Min move 2.
// @param fractionalSeparator1  (series string) The primary fractional separator.
// @param fractionalSeparator2  (series string) The secondary fractional separator.
// @returns                     String in fractional format.
export method asFractional(float value, int priceScale = syminfo.pricescale, int minMove = syminfo.minmove, int minMove2 = 0, string fractionalSeparator1 = "'", string fractionalSeparator2 = "'") =>

    // This variable determines the possible values for the "zz" part of a fractional format in the form [xx'yy'zz]
    int secondaryFractionalScale = minMove2

    // Determines which fractional format to use.
    // There are two fractional forms:
    //     1) [xx'yy]       - fraction               (`secondaryFractionalScale` == 0)
    //     2) [xx'yy'zz]    - fraction of a fraction (`secondaryFractionalScale` > 0)
    bool fractionOfFractionFormat = secondaryFractionalScale > 0

    // This variable determines the possible values for the "yy" part of a fractional format in either form [xx'yy] or [xx'yy'zz].
    // Examples (assuming `minMove` = 1):
    //     • `priceScale` = 64, `minMove2` = 0:
    //        - value is 64, so the result can be a number from xx'00 to xx'63
    //     • `priceScale` = 64, `minMove2` = 2:
    //        - value is 64 / 2 = 32, so the result can be a number from xx'00'zz to xx'31'zz
    int primaryFractionalScale = fractionOfFractionFormat ? priceScale / secondaryFractionalScale : priceScale

    float absoluteValue = math.abs(value)                   // convert to positive value so that this function also works with negative values

    int wholePoints = int(absoluteValue)                    // this is the "xx" part of the [xx'yy] or [xx'yy'zz] fractional value
    float decimalPortion = absoluteValue - wholePoints      // this is the decimal portion (>= 0.0 and < 1.0) that will be transformed to "yy" or "yy'zz"

    // Number of ticks that one full point can be divided into, which is simply the multiplicative inverse of minimum tick size.
    // https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.pricescale states that `syminfo.minmove / syminfo.pricescale = syminfo.mintick`
    int ticksPerPoint = priceScale / minMove

    // Actual number of ticks which represents the decimal portion of the original value.
    // Using `math.round()` instead of `int()` to match the behavior of TradingView when the decimal portion is not already rounded to the tick size.
    int fractionalTicks = math.round(decimalPortion * ticksPerPoint)

    // Check if the result of rounding brings the number of fractional ticks up to one full point.
    // This condition can be true in a scenario where the raw price data has a value that
    // is close to, but not exactly equal to a whole number.  E.g., 119.9981738 instead of 120.00
    if fractionalTicks == ticksPerPoint
        wholePoints += 1
        fractionalTicks := 0

    // Finalize the "xx" part of the [xx'yy] or [xx'yy'zz] fractional format, undoing the `math.abs()` operation from the beginning.
    // Note: the value can be used directly since TradingView does not add any grouping separator if the value is >= 1000'00.
    string result = str.tostring(wholePoints, value >= 0 ? "0" : "-0") + fractionalSeparator1


    // Rescale the `primaryFractionalScale` to 10 if necessary.
    // This will rescale any value that represents 1/2 or 1/4 of one point:
    //     - x/2 (i.e., 0/2, 1/2) will be rescaled to x/10 (i.e., 0.0/10, 5.0/10).
    //     - x/4 (i.e., 0/4, 1/4, 2/4, 3/4) will be rescaled to x/10 (i.e., 0.0/10, 2.5/10, 5.0/10, 7.5/10).
    // Note: does not rescale if `minMove` > 1 or `minMove2` > 0, to prevent potentially confusing results.
    int rescaled = primaryFractionalScale >= 8 or minMove > 1 or fractionOfFractionFormat ? primaryFractionalScale : 10

    // This is the "yy" part of the [xx'yy] or [xx'yy'zz] fractional price
    float fractions = (fractionalTicks / ticksPerPoint) * rescaled
    // Note: it is rounded DOWN to conform with the fraction representation that is described in
    // https://www.cmegroup.com/trading/interest-rates/files/treasury-futures-price-rounding-conventions-2020.pdf.
    // The PDF shows that a fractional tick is converted into a decimal tick
    // and the integer displayed is the first digit of the decimal tick
    // (e.g., 3/4 = 0.𝟳5 would display xx'𝟳 instead of rounding it to xx'8).
    // This expected behavior can be confirmed by manually changing the chart settings to use a precision of 1/2 or 1/4.
    int wholeFractions = int(fractions)

    // Add leading zeros if necessary.
    // Note: using '>' instead of '>=' because maximum value will be 1 less than the scale (e.g., scale of 10 will only use values 0 to 9).
    if rescaled > 10 and wholeFractions < 10
        result += "0"
    if rescaled > 100 and wholeFractions < 100
        result += "0"

    result += str.tostring(wholeFractions)


    if fractionOfFractionFormat
        decimalPortion := fractions - wholeFractions    // remaining decimal portion (>= 0.0 and < 1.0) that will be transformed to "zz" in [xx'yy'zz]

        // Rescale the `secondaryFractionalScale` to 10 if necessary.
        // Note: unlike the initial rescaling logic for `primaryFractionalScale`,
        // this will also include any value that represents 1/8 of a fraction.
        //     - x/8 (i.e., 0/8, 1/8, 2/8, 3/8, 4/8, 5/8, 6/8, 7/8) will be rescaled to x/10 (i.e., 0.0/10, 1.25/10, 2.5/10, 3.75/10, 5.0/10, 6.25/10, 7.5/10, 8.75/10).
        rescaled := secondaryFractionalScale > 10 ? secondaryFractionalScale : 10

        // This is the "zz" part of the [xx'yy'zz] fractional price
        fractions := decimalPortion * rescaled
        // Note: it is rounded DOWN for the same reasons that "yy" was rounded down
        wholeFractions := int(fractions)

        result += fractionalSeparator2

        // Add leading zeros if necessary.
        // Note: using '>' instead of '>=' because maximum value will be 1 less than the scale (e.g., scale of 10 will only use values 0 to 9).
        if rescaled > 10 and wholeFractions < 10
            result += "0"
        if rescaled > 100 and wholeFractions < 100
            result += "0"

        result += str.tostring(wholeFractions)

    result



// @function  Converts a number to a string in volume format. \
//            \
//            The result will first be rounded to the min tick size,
//            and then the maximum number of decimal places will be limited to the specified precision. \
//            \
//            Trailing zeros are trimmed from the result.
// ___
// **Parameters**
// ```
// • series int precision
// • series float minTick
// • series string decimalSeparator
// • series string groupingSeparator
// • series string spacing
// ```
// `precision` - Maximum number of decimal places to use.  Should be a value >= 0 and <= 16.  Default value is 2. \
// `minTick` - Minimum tick value.  Should be a value > 0.  Default value is [syminfo.mintick](https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.mintick). \
// `decimalSeparator` - The character to use for separating the integer and fractional part of the result.  Default value is "." (period). \
// `groupingSeparator` - The character to use for separating groups of digits in the result.  Should be different from the decimal separator.  Default value is "" (empty string). \
// `spacing` - The whitespace character(s) to use for separating the abbreviated value and the letter suffix.  Default value is " " ([thin space](https://en.wikipedia.org/wiki/Thin_space)). \
// \
// Allows [na](https://www.tradingview.com/pine-script-reference/v6/#var_na) value: `groupingSeparator`, `spacing`
// ___
// **Remarks** \
// The Parameters section describes the acceptable values for each argument, including which
// arguments are allowed to be [na](https://www.tradingview.com/pine-script-reference/v6/#var_na). \
// This function will not generate a custom runtime error for invalid arguments, but the behavior is undefined. \
// \
// Numbers less than 1000 will not have any letter suffix in the result. \
// \
// Numbers greater than or equal to 10^12 will not use any abbreviation larger than a Trillion ("T"). \
// E.g., a value of 1e12 is abbreviated as "1,000 T".
// ___
// **Usage**
// > ```
// > float value = 1000.234
// >
// > // Example: using default precision
// > value.asVolume(precision = 2, minTick = 0.001)  //"1 K"
// >
// > // Example: using custom precision
// > value.asVolume(precision = 5, minTick = 0.001)  //"1.00023 K"
// >
// > // Example: modifying separators
// > value.asVolume(precision = 5, minTick = 0.001, decimalSeparator = ",", spacing = "")  //"1,00023K"
// > ```
// ___
// @param value              (series float) The value to format.
// @param precision          (series int) Maximum number of decimal places.
// @param minTick            (series float) Minimum tick size.
// @param decimalSeparator   (series string) The decimal separator.
// @param groupingSeparator  (series string) The thousands separator, aka digit group separator.
// @param spacing            (series string) The whitespace separator.
// @returns                  String in volume format.
export method asVolume(float value, int precision = 2, float minTick = syminfo.mintick, string decimalSeparator = ".", string groupingSeparator = "", string spacing = " ") =>

    // Round the price to the minimum tick size.
    // Reference: https://www.tradingview.com/pine-script-docs/faq/functions/#how-can-i-round-a-number-to-x-increments
    float _value = math.round(value / minTick) * minTick

    // Ensure that the precision value is between 0 and 16 to avoid a runtime error when using `str.substring()`
    int _precision = na(precision) ? 2 : (precision < 0 ? 0 : (precision > MAX_PRECISION ? MAX_PRECISION : precision))

    string format = _precision == 0 ? "#,##0" : "#,##0." + str.substring(MAX_DECIMALS_FORMAT, 0, _precision)

    int digitsAmount = int(nz(math.log10(math.abs(_value)))) + 1    // use `nz()` because `math.log10()` is `na` if the value is 0

    // Abbreviate the number.
    // Note: logic borrowed from https://www.tradingview.com/pine-script-docs/faq/functions/#how-can-i-abbreviate-large-values
    _value /= switch
        digitsAmount > 12 => 1e12
        digitsAmount > 9  => 1e9
        digitsAmount > 6  => 1e6
        digitsAmount > 3  => 1e3
        => 1

    // Convert the abbreviated value to a string
    string result = str.tostring(_value, format)

    int integerLength = nz(str.pos(result, "."), str.length(result))    // # of digits (and commas) to the left of the decimal point

    // Check if the string conversion caused the abbreviated value to be rounded up to a Thousand.
    // If so, the `digitsAmount` should be incremented to correctly determinine the letter suffix to use.
    //
    // E.g., "999,999,123" has 9 digits and can be rewritten as "999.999123 M".
    // If a maximum precision of '2' is used, the format string will be '#,##0.##'
    // which rounds the abbreviated value up to 1000.00 for a result of "1,000 M".
    // In this scenario, the result should be adjusted to divide the abbreviated value by 1000
    // and change the letter suffix to the next higher magnitude (i.e., "1,000 M" becomes "1 B").
    //
    // Note: this only affects numbers that have a digit count <= 12 (i.e., less than 1 trillion)
    // because the function will not abbreviate anything beyond the letter "T" (trillion).
    if integerLength >= 5 and digitsAmount <= 12
        digitsAmount += 1
        _value /= 1e3
        result := str.tostring(_value, format)

    string suffix = switch
        digitsAmount > 12 => spacing + "T"
        digitsAmount > 9  => spacing + "B"
        digitsAmount > 6  => spacing + "M"
        digitsAmount > 3  => spacing + "K"
        => ""

    // Post-processing to replace the decimal/grouping separators and add the suffix
    array<string> parts = str.split(result, ".")
    parts.set(0, str.replace_all(parts.first(), ",", groupingSeparator))
    parts.join(decimalSeparator) + suffix

//#endregion




//#region ------------------------------ Main Library Functions ------------------------------

// @function  Formats a price value to match how it would be displayed on the user's current chart.
// ___
// **Parameters**
// ```
// • series Precision precision
// • series Language language
// • simple bool allowPips
// ```
// `precision` - Precision enum to force the result into decimal or fractional format.
// Default value is [na](https://www.tradingview.com/pine-script-reference/v6/#var_na) (equivalent to Precision.DEFAULT). \
// `language` - Language enum to determine the decimal/grouping separators.
// Use [na](https://www.tradingview.com/pine-script-reference/v6/#var_na) for a decimal point "." and no grouping separator.
// Default value is [na](https://www.tradingview.com/pine-script-reference/v6/#var_na). \
// `allowPips` - Boolean value to allow displaying prices in pips format (if supported by the current symbol).
// Default value is [false](https://www.tradingview.com/pine-script-reference/v6/#const_false).
// ___
// **Remarks** \
// `precision` is exposed as an optional function argument because there is no Pine built-in variable \
// to know which Precision option the user selected in the TradingView chart settings. \
// \
// `language` is exposed as an optional function argument because there is no Pine built-in variable \
// to know which Language option the user selected in the TradingView user menu. \
// The default value ([na](https://www.tradingview.com/pine-script-reference/v6/#var_na))
// will use neutral decimal/grouping separators (matching the behavior of `str.tostring(value)`). \
// \
// By default, Forex charts display open/high/low/close prices as regular decimal numbers, therefore the default \
// `allowPips` value is set to [false](https://www.tradingview.com/pine-script-reference/v6/#const_false) for consistency.
// ___
// **Usage**
// > ```
// > // Example: default arguments
// > open.formatPrice()
// >
// > // Example: custom arguments
// > precision = input.enum(Precision.DEFAULT)
// > language = input.enum(Language.ENGLISH)
// > open.formatPrice(precision, language, allowPips = true)
// > ```
// ___
// @param price      (series float) The value to format.
// @param precision  (series Precision) A Precision.* enum value.
// @param language   (series Language) A Language.* enum value.
// @param allowPips  (simple bool) Whether to allow decimal numbers to display as pips.
// @returns          Automatically formatted price string.
export method formatPrice(float price, Precision precision = na, Language language = na, simple bool allowPips = false) =>
    var Format format = determinePriceFormat(allowPips)

    // PERCENTAGE format simply adds a percent sign ("%") at the end
    var string suffix = format == Format.PERCENTAGE ? "%" : ""

    [decimalSeparator, groupingSeparator] = switch language
        Language.ENGLISH             => [".", ","]
        Language.SPANISH             => [",", "."]
        Language.INDIAN_ENGLISH      => [".", ","]
        Language.GERMAN              => [",", "."]
        Language.FRENCH              => [",", " "]
        Language.ITALIAN             => [",", "."]
        Language.POLISH              => [",", " "]
        Language.TURKISH             => [",", "."]
        Language.RUSSIAN             => [",", " "]
        Language.PORTUGUESE          => [",", "."]
        Language.INDONESIAN          => [",", "."]
        Language.MALAY               => [".", ","]
        Language.THAI                => [".", ","]
        Language.VIETNAMESE          => [".", ","]
        Language.JAPANESE            => [".", ","]
        Language.KOREAN              => [".", ","]
        Language.CHINESE_SIMPLIFIED  => [".", ","]
        Language.CHINESE_TRADITIONAL => [".", ","]
        Language.ARABIC              => [".", ","]
        Language.HEBREW              => [".", ","]
        => [".", ""]    // default separators as used by `str.tostring(value)`

    // Default Precision: automatically determine how to format the price value
    if na(precision) or precision == Precision.DEFAULT

        if format == Format.PIPS
            // Refer to 𝗣𝗜𝗣𝗦 𝗙𝗢𝗥𝗠𝗔𝗧 𝗜𝗡𝗙𝗢 section above the `asPips()` function declaration for details about `minMove2`
            const int minMove2 = 10
            price.asPips(syminfo.pricescale, syminfo.minmove, minMove2, decimalSeparator, groupingSeparator)

        else if format == Format.FRACTIONAL
            // Refer to 𝗙𝗥𝗔𝗖𝗧𝗜𝗢𝗡𝗔𝗟 𝗙𝗢𝗥𝗠𝗔𝗧 𝗜𝗡𝗙𝗢 section above the `asFractional()` function declaration for details about `minMove2`
            var int minMove2 = syminfo.pricescale > 32 ? syminfo.pricescale / 32 : 0
            price.asFractional(syminfo.pricescale, syminfo.minmove, minMove2)

        else if format == Format.VOLUME
            // Volume prices are displayed with a maximum of 2 decimal places
            price.asVolume(2, syminfo.mintick, decimalSeparator, groupingSeparator)

        else
            // For some markets with a very small tick size (e.g., crypto),
            // TradingView uses E notation to display prices less than 0.0001.
            // Note: using `plot()` with a value greater than or equal to 1e21
            // will display it in E notation, so this library assumes that
            // a chart price >= 1e21 would also be displayed in E notation.
            var bool largePriceScale = syminfo.pricescale >= 1e10
            bool eNotation = (largePriceScale and math.abs(price) < 1e-4) or math.abs(price) >= 1e21

            // Note: in rare cases, this will not use the correct amount of decimals.
            // E.g., if a penny stock has a minimum tick size of 0.0001 (4 decimal places),
            // TradingView might use 2 decimal places (instead of 4) to display prices that are > $1.00
            // but this function will still format the price with 4 decimal places.
            // This is just one example; the specific criteria may not be the same for other symbols.
            // This special behavior is likely due to what is known as "variable tick size".
            // Reference: https://www.tradingview.com/charting-library-docs/latest/connecting_data/Symbology/#variable-tick-size
            price.asDecimal(na, syminfo.mintick, decimalSeparator, groupingSeparator, eNotation) + suffix

    // Non-default Precision: specifically use Decimal or Fractional format
    else
        string title = str.tostring(precision)

        if str.contains(title, "/")
            int fractions = switch precision
                Precision.FRACTIONAL_2   => 2
                Precision.FRACTIONAL_4   => 4
                Precision.FRACTIONAL_8   => 8
                Precision.FRACTIONAL_16  => 16
                Precision.FRACTIONAL_32  => 32
                Precision.FRACTIONAL_64  => 64
                Precision.FRACTIONAL_128 => 128
                Precision.FRACTIONAL_320 => 320
            price.asFractional(priceScale = fractions, minMove = 1, minMove2 = 0) + suffix

        else
            int decimals = switch precision
                Precision.INTEGER    => 0
                Precision.DECIMAL_1  => 1
                Precision.DECIMAL_2  => 2
                Precision.DECIMAL_3  => 3
                Precision.DECIMAL_4  => 4
                Precision.DECIMAL_5  => 5
                Precision.DECIMAL_6  => 6
                Precision.DECIMAL_7  => 7
                Precision.DECIMAL_8  => 8
                Precision.DECIMAL_9  => 9
                Precision.DECIMAL_10 => 10
                Precision.DECIMAL_11 => 11
                Precision.DECIMAL_12 => 12
                Precision.DECIMAL_13 => 13
                Precision.DECIMAL_14 => 14
                Precision.DECIMAL_15 => 15
            price.asDecimal(precision = decimals, decimalSeparator = decimalSeparator, groupingSeparator = groupingSeparator) + suffix



// @function  Measures a change in price in terms of both distance and ticks. \
//            \
//            Returns a tuple:
//            ```
//            [string distance, string ticks]
//            ```
//            • 'distance' is the difference between the start & end price,
//               in the same format that would be returned by a call to `formatPrice()` \
//            • 'ticks' is a verbose representation of the number of ticks
//               or pips (percentage in point) or bps (basis point) between the start & end price \
//            \
//            Note: returned values can be negative if the end price is lower than the start price.
// ___
// **Parameters**
// ```
// • series float startPrice
// • series float endPrice
// • series Precision precision
// • series Language language
// • simple bool allowPips
// ```
// `startPrice` - The starting price.  Required argument. \
// `endPrice` - The ending price.  Required argument. \
// `precision` - Precision enum to force the [distance] return value into decimal or fractional format.
// Default value is [na](https://www.tradingview.com/pine-script-reference/v6/#var_na) (equivalent to Precision.DEFAULT). \
// `language` - Language enum to determine the decimal/grouping separators.
// Use [na](https://www.tradingview.com/pine-script-reference/v6/#var_na) for a decimal point "." and no grouping separator.
// Default value is [na](https://www.tradingview.com/pine-script-reference/v6/#var_na). \
// `allowPips` - Boolean value to allow displaying prices in pips format (if supported by the current symbol).
// Default value is [false](https://www.tradingview.com/pine-script-reference/v6/#const_false).
// ___
// **Remarks** \
// `precision` is exposed as an optional function argument because there is no Pine built-in variable \
// to know which Precision option the user selected in the TradingView chart settings. \
// \
// `language` is exposed as an optional function argument because there is no Pine built-in variable \
// to know which Language option the user selected in the TradingView user menu. \
// The default value ([na](https://www.tradingview.com/pine-script-reference/v6/#var_na))
// will use neutral decimal/grouping separators (matching the behavior of `str.tostring(value)`). \
// \
// By default, Forex charts display open/high/low/close prices as regular decimal numbers, therefore the default \
// `allowPips` value is set to [false](https://www.tradingview.com/pine-script-reference/v6/#const_false) for consistency. \
// \
// 💡Tip: if this function is called alongside `formatPrice()`, it is recommended to use the same optional arguments for both.
// ___
// **Usage**
// > ```
// > float startPrice = 1.16258
// > float endPrice = 1.16109
// >
// > // Example: default arguments
// > [distance, ticks] = measurePriceChange(startPrice, endPrice)  //["-0.00149", "-149 ticks"]
// >
// > // Example: allow pips (on forex symbols)
// > [distance, ticks] = measurePriceChange(startPrice, endPrice, allowPips = true)  //["-0.0014⁹", "-14.9 pips"]
// >
// > // Example: custom arguments
// > precision = Precision.DECIMAL_4
// > language = Language.FRENCH
// > [distance, ticks] = measurePriceChange(startPrice, endPrice, precision, language, true)  //["-0,0015", "-14,9 pips"]
// >
// > // Example: use absolute values (2 ways of removing the negative sign)
// > distance := str.startswith(distance, "-") ? str.substring(distance, 1) : distance  //"0,0015"
// > ticks := str.replace(ticks, "-", "")                                               //"14,9 pips"
// > ```
// ___
// @param startPrice  (series float) The starting price.
// @param endPrice    (series float) The ending price.
// @param precision   (series Precision) A Precision.* enum value.
// @param language    (series Language) A Language.* enum value.
// @param allowPips   (simple bool) Whether to allow decimal numbers to display as pips.
// @returns           A tuple of formatted strings: [string distance, string ticks].
export measurePriceChange(float startPrice, float endPrice, Precision precision = na, Language language = na, simple bool allowPips = false) =>
    var Format format = determinePriceFormat(allowPips)

    var string singularSuffix = format == Format.PIPS ? " pip" : format == Format.PERCENTAGE ? " bp" : " tick"
    var string pluralSuffix = singularSuffix + "s"

    // The price difference will be multiplied by this 'multiplier' to calculate the number of ticks / pips / bps
    var float multiplier = switch format
        // Divide the price scale by 10 to shift one decimal place for counting the pips instead of pipettes
        Format.PIPS => syminfo.pricescale / 10
        // A price movement of 0.01 is equal to 1 basis point (0.01 × 100 = 1 bp)
        Format.PERCENTAGE => 100
        // Uses the multiplicative inverse of minimum tick size (1 / `syminfo.mintick` = # ticks per point)
        // Reference: https://www.tradingview.com/pine-script-reference/v6/#var_syminfo.pricescale
        => syminfo.pricescale / syminfo.minmove

    // Round prices for accuracy.
    // Reference: https://www.tradingview.com/pine-script-docs/faq/functions/#how-can-i-round-a-number-to-x-increments
    float startPriceRounded = math.round(startPrice / syminfo.mintick) * syminfo.mintick
    float endPriceRounded = math.round(endPrice / syminfo.mintick) * syminfo.mintick
    float difference = endPriceRounded - startPriceRounded

    float value = difference * multiplier

    // Force the result to use a maximum of 1 decimal place (or use none for whole numbers).
    // Note: using `formatPrice()`, instead of `asDecimal()`, since it already has the logic to determine the decimal/grouping separators.
    string ticks = math.round(value) == value ? value.formatPrice(Precision.INTEGER, language) : value.formatPrice(Precision.DECIMAL_1, language)

    if format == Format.PERCENTAGE
        ticks := str.substring(ticks, 0, str.length(ticks) - 1) // remove the percent sign added to the end

    // If a non-default `precision` is specified, use the unmodified price values since `formatPrice()` will round the result accordingly
    float price = na(precision) or precision == Precision.DEFAULT ? difference : endPrice - startPrice

    string distance = price.formatPrice(precision, language, allowPips)

    [distance, ticks + (ticks == "1" ? singularSuffix : pluralSuffix)]



// @function  Alternative to the Pine `str.tostring(value, format)` built-in function. \
//            \
//            Uses one of the `format.*` built-in constants as the format string. \
//            The function will generate a custom runtime error for any other invalid format string. \
//            \
//            Summary of formatting behavior: \
//            • [format.inherit](https://www.tradingview.com/pine-script-reference/v6/#const_format.inherit) -
//              match the price format of the user's current chart (equivalent to `formatPrice()` with default args) \
//            • [format.mintick](https://www.tradingview.com/pine-script-reference/v6/#const_format.mintick) -
//              rounds the value to the minimum tick size with trailing zeros included \
//            • [format.volume](https://www.tradingview.com/pine-script-reference/v6/#const_format.volume) -
//              abbreviates any value >= 1000 and adds a letter suffix to denote the magnitude of the value \
//            • [format.price](https://www.tradingview.com/pine-script-reference/v6/#const_format.price) -
//              converts to a regular decimal number, excluding trailing zeros after the decimal point \
//            • [format.percent](https://www.tradingview.com/pine-script-reference/v6/#const_format.percent) -
//              same as `format.price` but with a percent sign "%" added to the end
// ___
// **Parameters**
// ```
// • series float value
// • series string format
// ```
// `value` - The value to convert to a string. \
// `format` - Format string.  Accepts any of the format.* built-in constants.
// ___
// **Remarks** \
// Unlike the [str.tostring(value, format)](https://www.tradingview.com/pine-script-reference/v6/#fun_str.tostring)
// built-in function, this custom function supports
// [format.price](https://www.tradingview.com/pine-script-reference/v6/#const_format.price)
// and [format.inherit](https://www.tradingview.com/pine-script-reference/v6/#const_format.inherit). \
// \
// Note: the [format.mintick](https://www.tradingview.com/pine-script-reference/v6/#const_format.mintick),
// [format.percent](https://www.tradingview.com/pine-script-reference/v6/#const_format.percent),
// and [format.volume](https://www.tradingview.com/pine-script-reference/v6/#const_format.volume)
// formats will behave similar to the \
// [str.tostring(value, format)](https://www.tradingview.com/pine-script-reference/v6/#fun_str.tostring)
// built-in function, but the formatted result may have minor differences.
// ___
// **Usage**
// > ```
// > // Example: call as a method
// > open.tostring(format.inherit)
// >
// > // Example: call as a function
// > tostring(open, format.inherit)
// > ```
// ___
// @param value   (series float) The value to format.
// @param format  (series string) The format string.
// @returns       String in the specified format.
export method tostring(float value, string format) =>
    switch format
        format.inherit => value.formatPrice()
        format.mintick => value.asDecimal()
        format.volume  => value.asVolume(minTick = 1e-2)
        format.price   => str.tostring(value, "0." + EXTENDED_MAX_DECIMALS_FORMAT)
        format.percent => str.tostring(value, "0." + EXTENDED_MAX_DECIMALS_FORMAT) + "%"
        => runtime.error("Invalid `format` argument.  Use one of the built-in `format.*` constants"), ""

//#endregion
````
