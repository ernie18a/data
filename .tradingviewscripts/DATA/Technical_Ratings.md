<!-- tradingview-pine-id: PUB;eNbFyHDmTIdGB7PEd6YGhJno5zhtmJdM -->
<!-- tradingviewscripts-format: 1 -->
# Technical Ratings

Source: https://www.tradingview.com/script/Jdw7wW2g-Technical-Ratings/

## Description

█ OVERVIEW

This indicator calculates TradingView's well-known "Strong Buy", "Buy", "Neutral", "Sell" or "Strong Sell"  states using the aggregate biases of 26 different technical indicators.

█ FEATURES

Differences with the built-in version
 • You can adjust the weight of the Oscillators and MAs components of the rating here.
 • The built-in version produces values matching the states displayed in the "Technicals" ratings gauge; this one does not always, where weighting is used.
 • A strategy version is also available as a built-in; this script is an indicator—not a strategy.
 • This indicator will show a slightly different vertical scale, as it does not use a fixed scale like the built-in.
 • This version allows control over repainting of the signal when you do not use a higher timeframe. Higher timeframe (HTF) information from this version does not repaint.
 • You can configure markers on signal breaches of configurable levels, or on advances declines of the signal.

The indicator's settings allow you to:
 • Choose the timeframe you want calculations to be made on.
 • When not using a HTF, you can select a repainting or non-repainting signal.
 • When using both MAs and Oscillators groups to calculate the rating, you can vary the weight of each group in the calculation. The default is 50/50.
  Because the MAs group uses longer periods for some of its components, its value is not as jumpy as the Oscillators value.
  Increasing the weight of the MAs group will thus have a calming effect on the signal.
 • Alerts can be created on the indicator using the conditions configured to control the display of markers.

Display
The calculated rating is displayed as columns, but you can change the style in the inputs. The color of the signal can be one of three colors: bull, bear, or neutral. You can choose from a few presets, or check one and edit its color. The color is determined from the rating's value. Between 0.1 and -0.1 it is in the neutral color. Above/below 0.1/-0.1 it will appear in the bull/bear color. The intensity of the bull/bear color is determined by cumulative advances/declines in the rating. It is capped to 5, so there are five intensities for each of the bull/bear colors.

The "Strong Buy", "Buy", "Neutral", "Sell" or "Strong Sell" state of the last calculated value is displayed to the right of the last bar for each of the three groups: All, MAs and Oscillators. The first value always reflects your selection in the "Rating uses" field and is the one used to display the signal. A "Strong Buy" or "Strong Sell" state appears when the signal is above/below the 0.5/-0.5 level. A "Buy" or "Sell" state appears when the signal is above/below the 0.1/-0.1 level. The "Neutral" state appears when the signal is between 0.1 and -0.1 inclusively.

Five levels are always displayed: 0.5 and 0.1 in the bull color, zero in the neutral color, and -0.1 and - 0.5 in the bull color.

The levels that can be used to determine the breaches displaying long/short markers will only be visible when their respective long/short markers are turned on in the "Direction" input. The levels appear as a bright dotted line in bull/bear colors. You can control both levels separately through the "Longs Level" and "Shorts Level" inputs.

If you specify a higher timeframe that is not greater than the chart's timeframe, an error message will appear and the indicator's background will turn red, as it doesn't make sense to use a lower timeframe than the chart's.

Markers
Markers are small triangles that appear at the bottom and top of the indicator's pane. The marker settings define the conditions that will trigger an alert when you configure an alert on the indicator. You can:
 • Choose if you want long, short or both long and short markers.
 • Determine the signal level and/or the number of cumulative advances/declines in the signal which must be reached for either a long or short marker to appear.
  Reminder: the number of advances/declines is also what controls the brightness of the plotted signal.
 • Decide if you want to restrict markers to ones that alternate between longs and shorts, if you are displaying both directions.
  This helps to minimize the number of markers, e.g., only the first long marker will be displayed, and then no more long markers will appear until a short comes in, then a long, etc.

Alerts
When you create an alert from this indicator, that alert will trigger whenever your marker conditions are confirmed. Before creating your alert, configure the makers so they reflect the conditions you want your alert to trigger on.

The script uses the [alert()](https://www.tradingview.com/pine-script-reference/v4/#fun_alert) function, which entails that you select the "Any alert() function call" condition from the "Create Alert" dialog box when creating alerts on the script. The alert messages can be configured in the inputs. You can safely disregard the warning popup that appears when you create alerts from this script. Alerts will not repaint. Markers will appear, and thus alerts will trigger, at the opening of the bar following the confirmation of the marker condition. Markers will never disappear from the bar once they appear.

Repainting
This indicator uses a two-pronged approach to control repainting. The repainting of the displayed signal is controlled through the "Repainting" field in the script's inputs. This only applies when you have "Same as chart" selected in the "Timeframe" field, as higher timeframe data never repaints. Regardless of that setting, markers and thus alerts never repaint.

When using the chart's timeframe, choosing a non-repainting signal makes the signal one  bar late, so that it only displays a value once the bar it was calculated has elapsed. When using a higher timeframe, new values are only displayed once the higher timeframe completes.

Because the markers never repaint, their logic adapts to the repainting setting used for the signal. When the signal repaints, markers will only appear at the close of a realtime bar. When the signal does not repaint (or if you use a higher timeframe), alerts will appear at the beginning of the realtime bar, since they are calculated on values that already do not repaint.

█ CALCULATIONS

The indicator calculates the aggregate value of two groups of indicators: moving averages and oscillators.

The "MAs" group is comprised of 15 different components:
 • Six Simple Moving Averages of periods 10, 20, 30, 50, 100 and 200
 • Six Exponential Moving Averages of the same periods
 • A Hull Moving Average of period 9
 • A Volume-weighed Moving Average of period 20
 • Ichimoku‎

The "Oscillators" group includes 11 components:
 • RSI
 • Stochastic
 • CCI
 • ADX
 • Awesome Oscillator
 • Momentum
 • MACD
 • Stochastic RSI
 • Wiliams %R
 • Bull Bear Power
 • Ultimate Oscillator

The state of each group's components is evaluated to a +1/0/-1 value corresponding to its bull/neutral/bear bias. The resulting value for each of the two groups are then averaged to produce the overall value for the indicator, which oscillates between +1 and -1. The complete conditions used in the calculations are documented in the [Help Center](https://www.tradingview.com/?solution=43000614331).

█ NOTES

Accuracy
When comparing values to the other versions of the Rating, make sure you are comparing similar timeframes, as the "Technicals" gauge in the chart's right pane, for example, uses a 1D timeframe by default.

For coders
We use a handy characteristic of [array.avg()](https://www.tradingview.com/pine-script-reference/v4/#fun_array{dot}avg) which, contrary to [avg()](https://www.tradingview.com/pine-script-reference/v4/#fun_avg), does not return [na](https://www.tradingview.com/pine-script-reference/v4/#var_na) when one  of the averaged values is [na](https://www.tradingview.com/pine-script-reference/v4/#var_na). It will average only the array elements which are not [na](https://www.tradingview.com/pine-script-reference/v4/#var_na). This is useful in the context where the functions used to calculate the bull/neutral/bear bias for each component used in the rating include special checks to return [na](https://www.tradingview.com/pine-script-reference/v4/#var_na) whenever the dataset does not yet contain enough data to provide reliable values. This way, components gradually kick in the calculations as the script calculates on more and more historical data.

We also use the new `group` and `tooltip` parameters to [input()](https://www.tradingview.com/pine-script-reference/v4/#fun_input), as well as dynamic color generation of different transparencies from the bull/bear/neutral colors selected by the user.

Our script was written using the [PineCoders Coding Conventions for Pine](http://www.pinecoders.com/coding_conventions/).
The description was formatted using the techniques explained in the [How We Write and Format Script Descriptions](https://www.tradingview.com/chart/SSP/aOYEvBxw-How-We-Write-and-Format-Script-Descriptions/) PineCoders publication.
Bits and pieces were lifted from the PineCoders' [MTF Selection Framework](https://www.tradingview.com/script/90mqACUV-MTF-Selection-Framework-PineCoders-FAQ/).

[Look first. Then leap.](https://www.tradingview.com/athletes/)

---

## Source Code

````pine
// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © TradingView

//@version=6
indicator("Technical Ratings", "Ratings", precision = 2)

// Technical Ratings
// v4, 2026.04.14

// This code's style is based on the recommendations from the Pine Script User Manual's Style guide:
//    https://www.tradingview.com/pine-script-docs/writing/style-guide/



import TradingView/TechnicalRating/3 as TVtr



//#region ———————————————————— Constants and inputs


// Colors
color BLUE    = #013bca
color GOLD    = #cccc00
color GRAY    = #787b86
color LIME    = #00ff00
color PINK    = #ff0080
color VIOLET  = #aa00ff
color WHITE   = #ffffff
color NEUTRAL = #808080
color SILVER  = #b2b5be

// Threshold levels for strong and weak "Buy/Sell" ratings
float LEVEL_STRONG = 0.5
float LEVEL_WEAK   = 0.1

// Formatting strings
string FORMAT1 = "  {0}{1}"
string FORMAT2 = "      {0}"

// Input options
string RT1 = "MAs and Oscillators"
string RT2 = "MAs"
string RT3 = "Oscillators"

string ON  = "On"
string OFF = "Off"

string TD0 = "None"
string TD1 = "Longs"
string TD2 = "Shorts"
string TD3 = "Longs and Shorts"

string PS1 = "Columns"
string PS2 = "Histogram"
string PS3 = "Area"
string PS4 = "Line"

// Tooltips
string B_TT  = "The selected preset determines the base color for 'Buy' and 'Strong Buy' ratings."
string S_TT  = "The selected preset determines the base color for 'Sell' and 'Strong Sell' ratings."
string TF_TT = "Select a timeframe that is higher than or equal to the chart's timeframe."
string RP_TT = (
    "If 'On', plotted ratings update during an open realtime bar on the specified timeframe, "
    + "and alerts occur as soon as possible. If the timeframe is higher than the chart's timeframe, " 
    + "new values that appear before an HTF bar closes become unavailable after the indicator reloads.\n\n"
    + "If 'Off', plotted HTF ratings update and alerts occur only after a realtime bar closes.\n\n"
)
string MA_TT = (
    "Specifies the percentage weight of the MA rating for the total rating. The percentage weight of the oscillator " 
    + "rating is 100 - MA weight. For example, if the value is 50, the MA and oscillator ratings have a weight of 50%. " 
    + "If the value is 60, the MA rating has 60% weight, and the oscillator rating's weight is 40% (100% - 60%)."
)
string WD_TT = "The style and width of the plot. Width selection does not apply to the 'Columns' style."
string DR_TT = (
    "The alert direction. Visual markers for the alert conditions appear on the chart if the selection is not 'None'."
)
string AT_TT = (
    "If 'On' and the direction is 'Longs and Shorts', the indicator does not allow consecutive alerts for the same "
    + "direction. A short alert must occur after a long alert before another long alert can occur. Likewise, a long "
    + "alert must occur after a short alert before another short alert can occur. This input does not affect the "
    + "other direction options."
)
string LU_TT = (
    "The level that the rating value must cross to trigger a long alert. The default is the value for a 'Strong Buy' "
    + "state. Use a value of 0 to not trigger long alerts on level crosses."
)
string LD_TT = (
    "The level that the rating value must cross to trigger a short alert. The default is the value for a 'Strong Sell' "
    + "state. Use a value of 0 to not trigger short alerts on level crosses."
)

string GD_TT = (
    "The cumulative advances or declines that must occur in the latest positive or negative ratings before an alert " 
    + "occurs. If the current rating has a 'Buy'/'Strong Buy' or 'Sell'/'Strong Sell' state, the counter "
    + "increments by one if the value is less than 5 when the absolute rating rises, and decements by one when the "
    + "absolute value falls. The counter value resets to 0 when the current rating has a 'Neutral' state. " 
    + "The maximum counter value of 5 corresponds to the most opaque colors in the plot's gradients. Use a value of 0 "
    + "to not trigger alerts after successive advances or declines."
)

// Inputs
string  GRP1            = "Calculations"
string  tfInput         = input.timeframe("",        "Timeframe",                group = GRP1, tooltip = TF_TT)
bool    repaintInput    = input.string(ON,           "Repainting",               group = GRP1, tooltip = RP_TT, options = [ON, OFF]) == ON
string  calcsInput      = input.string(RT1,          "Rating uses",              group = GRP1,                  options = [RT2, RT3, RT1])
bool    isWeighted      = calcsInput == RT1
float   maInput         = input.int(50,              "Weight of MAs (%)",        group = GRP1, tooltip = MA_TT, minval  = 0, maxval = 100, step = 10) / 100

string  GRP2            = "Ratings display"
string  styleInput      = input.string(PS1,          "Plot style",               group = GRP2, inline  = "01",  options = [PS1, PS2, PS3, PS4])
int     widthInput      = input.int(1,               "",                         group = GRP2, inline  = "01",  minval  = 1, maxval = 50, tooltip = WD_TT, active = styleInput != PS1)
string  presetInput1    = input.string("1",          "Bull color",               group = GRP2, inline  = "02",  options = ["1", "2", "3"], tooltip = B_TT)
bool    showColorInput1 = presetInput1 == "1"
bool    showColorInput2 = presetInput1 == "2"
bool    showColorInput3 = presetInput1 == "3"
color   colorInput1     = input.color(LIME,          "",                         group = GRP2, inline  = "02", active = showColorInput1)
color   colorInput2     = input.color(GOLD,          "",                         group = GRP2, inline  = "02", active = showColorInput2)
color   colorInput3     = input.color(WHITE,         "",                         group = GRP2, inline  = "02", active = showColorInput3)
string  presetInput2    = input.string("1",          "Bear color",               group = GRP2, inline  = "03",  options = ["1", "2", "3"], tooltip = S_TT)
bool    showColorInput4 = presetInput2 == "1"
bool    showColorInput5 = presetInput2 == "2"
bool    showColorInput6 = presetInput2 == "3"
color   colorInput4     = input.color(PINK,          "",                         group = GRP2, inline  = "03", active = showColorInput4)
color   colorInput5     = input.color(VIOLET,        "",                         group = GRP2, inline  = "03", active = showColorInput5)
color   colorInput6     = input.color(BLUE,          "",                         group = GRP2, inline  = "03", active = showColorInput6)
color   neutColorInput  = input.color(NEUTRAL,       "Neutral color",            group = GRP2, inline  = "04")

string  GRP3            = "Alerts and markers"
string  dirInput        = input.string(TD0,          "Direction",                group = GRP3, tooltip = DR_TT, options = [TD0, TD1, TD2, TD3]) 
bool    alertsActive    = dirInput != TD0
bool    isBiDirectional = dirInput == TD3
bool    longsActive     = dirInput == TD1
bool    shortsActive    = dirInput == TD2
bool    altInput        = input.string(ON,           "Alternate longs & shorts", group = GRP3, tooltip = AT_TT, options = [ON, OFF], active = isBiDirectional) == ON and dirInput == TD3
float   levelUpInput    = input.float( LEVEL_STRONG, "Longs level",              group = GRP3, tooltip = LU_TT, minval  =  0, maxval = 1, step = 0.05, active = longsActive  or isBiDirectional) 
float   levelDnInput    = input.float(-LEVEL_STRONG, "Shorts level",             group = GRP3, tooltip = LD_TT, minval  = -1, maxval = 0, step = 0.05, active = shortsActive or isBiDirectional) 
float   gradInput       = input.float(0,             "Cumulative adv./decl.",    group = GRP3, tooltip = GD_TT, minval  =  0, maxval = 5, step = 1,    active = alertsActive) 
string  alertUpInput    = input.text_area("Long",    "Alert message: Long",      group = GRP3, active  = longsActive  or isBiDirectional)
string  alertDnInput    = input.text_area("Short",   "Alert message: Short",     group = GRP3, active  = shortsActive or isBiDirectional)
//#endregion



//#region ———————————————————— Functions 


// @function            Calculates transparency gradient with three base colors based on a value from -5 to +5.
// @param gradient      (series int) The value for which to calculate the color.
// @param bullColor     (series color) The base color for positive `gradient` values.
// @param bearColor     (series color) The base color for negative `gradient` values.
// @param neutralColor  (series color) The base color for neutral `gradient` values.
// @returns             (color) One of the three base colors, with increasing transparency for smaller values.
signalColor(int gradient, color bullColor, color bearColor, color neutralColor) =>
    color col  = gradient > 0 ? bullColor : gradient < 0 ? bearColor : neutralColor
    float transp = 100 - (math.abs(gradient) * 20)
    transp :=  transp == 80 ? 75 : transp
    color result = col == neutralColor ? color.new(neutralColor, 75) : color.new(col, transp)


// @function            Returns a defined color with modified transparency based on a rating value.
// @param rating        (series float) The rating value from which to derive a color.
// @param bullColor     (series color) The base color for positive `rating` values.
// @param bearColor     (series color) The base color for negative `rating` values.
// @param neutralColor  (series color) The base color for neutral `rating` values.
// @returns             (color) A color derived from the `bullColor`, `bearColor`, or `neutralColor` value, 
//                      corresponding to the directional state of the `rating` value. 
colorFromRating(float rating, color bullColor, color bearColor, color neutralColor) =>
    color result = switch 
        rating >  LEVEL_STRONG => color.new(bullColor, 20)
        rating >  LEVEL_WEAK   => color.new(bullColor, 40)
        rating < -LEVEL_STRONG => color.new(bearColor, 20)
        rating < -LEVEL_WEAK   => color.new(bearColor, 40)
        =>                        color.new(neutralColor, 20)


// @function            Creates a string representing the named state of a rating value.
// @param rating        (series float) The rating value from which to derive the named state.
// @returns             (string) One of the following strings: `"Strong Buy"`, `"Buy"`, `"Neutral"`, 
//                      `"Sell"`, or `"Strong Sell"`.
textFromRating(float rating) =>
    string result = switch 
        rating >  LEVEL_STRONG => "Strong Buy"
        rating >  LEVEL_WEAK   => "Buy"
        rating < -LEVEL_STRONG => "Strong Sell"
        rating < -LEVEL_WEAK   => "Sell"
        =>                        "Neutral"


// @function            Draws and updates a label on the last bar to print a line of rating information.
// @param txt           (series string) A string containing the label's text.
// @param lineNo        (series int) A "line number" for overlapping labels. Controls the number of `\n` characters
//                      the label uses to vertically position its text.
// @param txtColor      (series color) The color of the displayed text.
// @returns             (void) The function does not return usable data.
print(series string txt, series int lineNo, series color txtColor) =>
    var label lbl = label.new(
        na, 0.0, "", xloc.bar_index, yloc.price, #00000000, label.style_label_left, color.white, 
        textalign = text.align_left
    )
    var string labelStr = str.repeat("\n", math.max(0, lineNo - 1)) + "{0}" + str.repeat("\n", math.max(0, 4 - lineNo))
    if barstate.islast
    	label.set_x(lbl, bar_index)
        label.set_text(lbl, str.format(labelStr, txt))
    	label.set_textcolor(lbl, txtColor)
//#endregion



//#region ———————————————————— Calculations 


// Get input color choices.
var color bullColor = 
     showColorInput1 ? colorInput1 : showColorInput2 ? colorInput2 : showColorInput3 ? colorInput3 : #00000000
var color bearColor = 
     showColorInput4 ? colorInput4 : showColorInput5 ? colorInput5 : showColorInput6 ? colorInput6 : #00000000

// Get input direction choices.
var bool doLongs  = longsActive  or isBiDirectional
var bool doShorts = shortsActive or isBiDirectional

// Get input style choice.
var style = switch styleInput 
    PS4 => plot.style_line
    PS3 => plot.style_area  
    PS2 => plot.style_histogram
    =>     plot.style_columns 

// Get label titles based on the chosen rating type.
var string title1 = calcsInput == RT2 ? "MAs" : calcsInput == RT3 ? "Osc" : "All"
var string title2 = calcsInput == RT2 ? "Osc" : "MAs"
var string title3 = calcsInput == RT1 ? "Osc" : "All"

// Get values for the data request.
var bool htfUsed = timeframe.in_seconds(tfInput) > timeframe.in_seconds(timeframe.main_period) 
var int  offset  = htfUsed and not repaintInput ? 1 : 0
var lookahead    = htfUsed and not repaintInput ? barmerge.lookahead_on : barmerge.lookahead_off

// Calculate the oscillator and MA ratings.
[_, ratingOscChart, ratingMasChart] = TVtr.calcRatingAll()
// @variable A weighted sum of the MA and oscillator ratings.
float ratingTotChart = nz(ratingMasChart * maInput) + nz(ratingOscChart * (1.0 - maInput))

// Raise an error if the input timeframe is lower than the chart's timeframe.
if barstate.isfirst and timeframe.in_seconds(timeframe.main_period) > timeframe.in_seconds(tfInput) 
    string errorStr = "The requested timeframe cannot be lower than the chart''s timeframe (''{0}'')."
    runtime.error(str.format(errorStr, timeframe.main_period))

// Retreive the oscillator, MA, and weighted total ratings for the specified timeframe.
[ratingTot, ratingOsc, ratingMas] = request.security(
    syminfo.tickerid, tfInput, [ratingTotChart[offset], ratingOscChart[offset], ratingMasChart[offset]], 
    lookahead = lookahead
)

// Calculate rating colors.
color totColor = colorFromRating(ratingTot, bullColor, bearColor, neutColorInput)
color masColor = colorFromRating(ratingMas, bullColor, bearColor, neutColorInput)
color oscColor = colorFromRating(ratingOsc, bullColor, bearColor, neutColorInput)

// Assign the ratings and colors to variables for ordered display based on the `calcsInput` value.
[userRating, rating2, rating3, urColor, r2Color, r3Color] = switch calcsInput
    RT2 => [ratingMas, ratingOsc, ratingTot, masColor, oscColor, totColor]
    RT3 => [ratingOsc, ratingMas, ratingTot, oscColor, masColor, totColor]
    =>     [ratingTot, ratingMas, ratingOsc, totColor, masColor, oscColor]

// Calculate additional values for signal display and alerts.
bool  condBuy      = userRating >  LEVEL_WEAK
bool  condSell     = userRating < -LEVEL_WEAK
float valsBuy      = condBuy  ? userRating : 0
float valsSell     = condSell ? userRating : 0
int   risingBuys   = TVtr.countRising(valsBuy) 
int   fallingSells = TVtr.countRising(valsSell)
int   gradientLvl  = condBuy ? risingBuys : condSell ? -fallingSells : 0
color signalColor  = signalColor(gradientLvl, bullColor, bearColor, neutColorInput)
//#endregion



//#region ———————————————————— Main plots 


// Plot the user-selected rating.
plot(userRating, "Rating", signalColor, widthInput, style)

// Plot levels for rating states.
hline( LEVEL_STRONG, "Strong Buy level",  color.new(bullColor,  50), hline.style_dashed)
hline( LEVEL_WEAK,   "Buy level",         color.new(bullColor,  65), hline.style_dashed)
hline( 0.0,          "Zero level",        color.new(GRAY,       50), hline.style_dashed)
hline(-LEVEL_WEAK,   "Sell level",        color.new(bearColor,  75), hline.style_dashed)
hline(-LEVEL_STRONG, "Strong Sell level", color.new(bearColor,  50), hline.style_dashed)

// Plot all ratings in the data window.
plot(ratingTot, "All",         totColor, display = display.data_window, editable = false)
plot(ratingMas, "MAs",         masColor, display = display.data_window, editable = false)
plot(ratingOsc, "Oscillators", oscColor, display = display.data_window, editable = false)
plot(na,        "═══════",     totColor, display = display.data_window, editable = false)

// Draw labels to display categorized rating states in order based on the `calcsInput` value.
print(str.format(FORMAT1, title1, ":"), 1, SILVER)
print(str.format(FORMAT1, title2, ":"), 2, GRAY)
print(str.format(FORMAT1, title3, ":"), 3, GRAY)
print(str.format(FORMAT2, textFromRating(userRating)), 1, urColor)
print(str.format(FORMAT2, textFromRating(rating2)),    2, r2Color)
print(str.format(FORMAT2, textFromRating(rating3)),    3, r3Color)
//#endregion



//#region ———————————————————— Alerts and markers 


// Get alert conditions based on user selections.
var int lastDir = 0
bool xUp = ta.crossover( userRating, levelUpInput) and levelUpInput != 0 
bool xDn = ta.crossunder(userRating, levelDnInput) and levelDnInput != 0 
bool gUp = gradInput != 0 and gradientLvl ==  gradInput and gradientLvl[1] <  gradInput
bool gDn = gradInput != 0 and gradientLvl == -gradInput and gradientLvl[1] > -gradInput
bool allowLong  = not altInput or lastDir == 0 or lastDir == -1
bool allowShort = not altInput or lastDir == 0 or lastDir ==  1
bool allowAlert = repaintInput or (htfUsed and barstate.isnew) or (not htfUsed and barstate.isconfirmed)
bool triggerLong  = allowAlert and ((xUp or gUp) and allowLong  and doLongs )
bool triggerShort = allowAlert and ((xDn or gDn) and allowShort and doShorts)

// Create alert triggers and update logic.
if triggerLong
    alert(alertUpInput, alert.freq_once_per_bar)
    lastDir := 1
else if triggerShort
    alert(alertDnInput, alert.freq_once_per_bar)
    lastDir := -1

// Display long and short cross alert levels.
hline(doLongs  ? levelUpInput : na, "Long level",  levelUpInput == 0 ? na : bullColor, hline.style_dotted)
hline(doShorts ? levelDnInput : na, "Short level", levelDnInput == 0 ? na : bearColor, hline.style_dotted)
// Plot advances/declines used for alerts in the data window.
plot(gradientLvl, "Advances/Declines", signalColor, display = display.data_window, editable = false)
// Plot alert markers.
plotchar(triggerLong,  "Long marker",  "▲", location.bottom, color.new(bullColor, 00), size = size.tiny)
plotchar(triggerShort, "Short marker", "▼", location.top,    color.new(bearColor, 00), size = size.tiny)
//#endregion
````
