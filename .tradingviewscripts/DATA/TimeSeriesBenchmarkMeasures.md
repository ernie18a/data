<!-- tradingview-pine-id: PUB;5bcdf31174794207bae5403e60501c8e -->
<!-- tradingviewscripts-format: 1 -->
# TimeSeriesBenchmarkMeasures

Source: https://www.tradingview.com/script/IiPJ0Kjz-TimeSeriesBenchmarkMeasures/

## Description

Library  "TimeSeriesBenchmarkMeasures"
Time Series Benchmark Metrics. \
Provides a comprehensive set of functions for benchmarking time series data, allowing you to evaluate the accuracy, stability, and risk characteristics of various models or strategies. The functions cover a wide range of statistical measures, including accuracy metrics (MAE, MSE, RMSE, NRMSE, MAPE, SMAPE), autocorrelation analysis (ACF, ADF), and risk measures (Theils Inequality, Sharpness, Resolution, Coverage, and Pinball).

___
Reference:
- https://github.com/PYFTS/pyFTS/blob/master/pyFTS/benchmarks/Measures.py .
- https://medium.com/analytics-vidhya/assessment-of-accuracy-metrics-for-time-series-forecasting-bc115b655705 .
- https://www.salesforce.com/blog/gift-eval-time-series-benchmark/ .
- https://towardsdatascience.com/an-overview-of-forecasting-performance-metrics-ef548dad0134/ .
- https://github.com/PYFTS/pyFTS/blob/master/pyFTS/benchmarks/Measures.py .

mae(actual, forecasts)
  In statistics, mean absolute error (MAE) is a measure of errors between paired observations expressing the same phenomenon. Examples of Y versus X include comparisons of predicted versus observed, subsequent time versus initial time, and one technique of measurement versus an alternative technique of measurement.
  Parameters:
    actual (array<float>): List of actual values.
    forecasts (array<float>): List of forecasts values.
  Returns: - Mean Absolute Error (MAE).

___
Reference:
- https://en.wikipedia.org/wiki/Mean_absolute_error .
- The Orange Book of Machine Learning - Carl McBride Ellis .

mse(actual, forecasts)
  The Mean Squared Error (MSE) is a measure of the quality of an estimator. As it is derived from the square of Euclidean distance, it is always a positive value that decreases as the error approaches zero.
  Parameters:
    actual (array<float>): List of actual values.
    forecasts (array<float>): List of forecasts values.
  Returns: - Mean Squared Error (MSE).

___
Reference:
- https://en.wikipedia.org/wiki/Mean_squared_error .

rmse(targets, forecasts, order, offset)
  Calculates the Root Mean Squared Error (RMSE) between target observations and forecasts. RMSE is a standard measure of the differences between values predicted by a model and the values actually observed.
  Parameters:
    targets (array<float>): List of target observations.
    forecasts (array<float>): List of forecasts.
    order (int): Model order parameter that determines the starting position in the targets array, `default=0`.
    offset (int): Forecast offset related to target, `default=0`.
  Returns: - RMSE value.

nmrse(targets, forecasts, order, offset)
  Normalised Root Mean Squared Error.
  Parameters:
    targets (array<float>): List of target observations.
    forecasts (array<float>): List of forecasts.
    order (int): Model order parameter that determines the starting position in the targets array, `default=0`.
    offset (int): Forecast offset related to target, `default=0`.
  Returns: - NRMSE value.

rmse_interval(targets, forecasts)
  Root Mean Squared Error for a set of interval windows. Computes RMSE by converting interval forecasts (with min/max bounds) into point forecasts using the mean of the interval bounds, then compares against actual target values.
  Parameters:
    targets (array<float>): List of target observations.
    forecasts (matrix<float>): The forecasted values in matrix format with at least 2 columns (min, max).
  Returns: - RMSE value for the combined interval list.

mape(targets, forecasts)
  Mean Average Percentual Error.
  Parameters:
    targets (array<float>): List of target observations.
    forecasts (array<float>): List of forecasts.
  Returns: - MAPE value.

smape(targets, forecasts, mode)
  Symmetric Mean Average Percentual Error. Calculates the Mean Absolute Percentage Error (MAPE) between actual targets and forecasts. MAPE is a common metric for evaluating forecast accuracy, expressed as a percentage, lower values indicate a better forecast accuracy.
  Parameters:
    targets (array<float>): List of target observations.
    forecasts (array<float>): List of forecasts.
    mode (int): Type of method: default=0:`sum(abs(Fi-Ti)) / sum(Fi+Ti)` , 1:`mean(abs(Fi-Ti) / ((Fi + Ti) / 2))` , 2:`mean(abs(Fi-Ti) / (abs(Fi) + abs(Ti))) * 100`
  Returns: - SMAPE value.

mape_interval(targets, forecasts)
  Mean Average Percentual Error for a set of interval windows.
  Parameters:
    targets (array<float>): List of target observations.
    forecasts (matrix<float>): The forecasted values in matrix format with at least 2 columns (min, max).
  Returns: - MAPE value for the combined interval list.

acf(data, k)
  Autocorrelation Function (ACF) for a time series at a specified lag.
  Parameters:
    data (array<float>): Sample data of the observations.
    k (int): The lag period for which to calculate the autocorrelation. Must be a non-negative integer.
  Returns: - The autocorrelation value at the specified lag, ranging from -1 to 1.

___
The autocorrelation function measures the linear dependence between observations in a time series
at different time lags. It quantifies how well the series correlates with itself at different
time intervals, which is useful for identifying patterns, seasonality, and the appropriate
lag structure for time series models.

ACF values close to 1 indicate strong positive correlation, values close to -1 indicate
strong negative correlation, and values near 0 indicate no linear correlation.

___
Reference:
- https://statisticsbyjim.com/time-series/autocorrelation-partial-autocorrelation/

acf_multiple(data, k)
  Autocorrelation function (ACF) for a time series at a set of specified lags.
  Parameters:
    data (array<float>): Sample data of the observations.
    k (array<int>): List of lag periods for which to calculate the autocorrelation. Must be a non-negative integer.
  Returns: - List of ACF values for provided lags.

___
The autocorrelation function measures the linear dependence between observations in a time series
at different time lags. It quantifies how well the series correlates with itself at different
time intervals, which is useful for identifying patterns, seasonality, and the appropriate
lag structure for time series models.

ACF values close to 1 indicate strong positive correlation, values close to -1 indicate
strong negative correlation, and values near 0 indicate no linear correlation.

___
Reference:
- https://statisticsbyjim.com/time-series/autocorrelation-partial-autocorrelation/

adfuller(data, n_lag, conf)
  : Augmented Dickey-Fuller test for stationarity.
  Parameters:
    data (array<float>): Data series.
    n_lag (int): Maximum lag.
    conf (string): Confidence Probability level used to test for critical value, (`90%`, `95%`, `99%`).
  Returns: - `adf`	The test statistic.
- `crit`	Critical value for the test statistic at the 10 % levels.
- `nobs`	Number of observations used for the ADF regression and calculation of the critical values.

___
The Augmented Dickey-Fuller test is used to determine whether a time series is stationary
or contains a unit root (non-stationary). The null hypothesis is that the series has a unit root
(is non-stationary), while the alternative hypothesis is that the series is stationary.

A stationary time series has statistical properties that do not change over time, making it
suitable for many time series forecasting models. If the test statistic is less than the
critical value, we reject the null hypothesis and conclude the series is stationary.

___
Reference:
- https://www.jstor.org/stable/2286348
- https://en.wikipedia.org/wiki/Augmented_Dickey%E2%80%93Fuller_test

theils_inequality(targets, forecasts)
  Calculates Theil's Inequality Coefficient, a measure of forecast accuracy that quantifies the relative difference between actual and predicted values.
  Parameters:
    targets (array<float>): List of target observations.
    forecasts (array<float>): Matrix with list of forecasts, ordered column wise.
  Returns: - Theil's Inequality Coefficient value, value closer to 0 is better.

___
Theil's Inequality Coefficient is calculated as: `sqrt(Sum((y_i - f_i)^2)) / (sqrt(Sum(y_i^2)) + sqrt(Sum(f_i^2)))`
where `y_i` represents actual values and `f_i` represents forecast values.
This metric ranges from 0 to infinity, with 0 indicating perfect forecast accuracy.

___
Reference:
- https://en.wikipedia.org/wiki/Theil_index

sharpness(forecasts)
  The average width of the forecast intervals across all observations, representing the sharpness or precision of the predictive intervals.
  Parameters:
    forecasts (matrix<float>): The forecasted values in matrix format with at least 2 columns (min, max).
  Returns: - Sharpness The sharpness level, which is the average width of all prediction intervals across the forecast horizon.

___
Sharpness is an important metric for evaluating forecast quality. It measures how narrow or wide the
prediction intervals are. Higher sharpness (narrower intervals) indicates greater precision in the
forecast intervals, while lower sharpness (wider intervals) suggests less precision.

The sharpness metric is calculated as the mean of the interval widths across all observations, where
each interval width is the difference between the upper and lower bounds of the prediction interval.

Note: This function assumes that the forecasts matrix has at least 2 columns, with the first column
representing the lower bounds and the second column representing the upper bounds of prediction intervals.

___
Reference:
- Hyndman, R. J., & Athanasopoulos, G. (2018). Forecasting: principles and practice. OTexts. https://otexts.com/fpp2/

resolution(forecasts)
  Calculates the resolution of forecast intervals, measuring the average absolute difference between individual forecast interval widths and the overall sharpness measure.
  Parameters:
    forecasts (matrix<float>): The forecasted values in matrix format with at least 2 columns (min, max).
  Returns: - The average absolute difference between individual forecast interval widths and the overall sharpness measure, representing the resolution of the forecasts.

___
Resolution is a key metric for evaluating forecast quality that measures the consistency of prediction
interval widths. It quantifies how much the individual forecast intervals vary from the average interval
width (sharpness). High resolution indicates that the forecast intervals are relatively consistent
across observations, while low resolution suggests significant variation in interval widths.

The resolution is calculated as the mean absolute deviation of individual interval widths from the
overall sharpness value. This provides insight into the uniformity of the forecast uncertainty
estimates across the forecast horizon.

Note: This function requires the forecasts matrix to have at least 2 columns (min, max) representing
the lower and upper bounds of prediction intervals.

___
Reference:
- [Gneiting, T., & Raftery, A. E. (2007). Strictly proper scoring rules, prediction, and estimation.](https://sites.stat.washington.edu/raftery/Research/PDF/Gneiting2007jasa.pdf)
- [Journal of the American statistical Association, 102(477), 359-378.](https://www.jstor.org/stable/i27639812)

coverage(targets, forecasts)
  Calculates the coverage probability, which is the percentage of target values that fall within the corresponding forecasted prediction intervals.
  Parameters:
    targets (array<float>): List of target values.
    forecasts (matrix<float>): The forecasted values in matrix format with at least 2 columns (min, max).
  Returns: - Percent of target values that fall within their corresponding forecast intervals, expressed as a decimal value between 0 and 1 (or 0% and 100%).

___
Coverage probability is a crucial metric for evaluating the reliability of prediction intervals.
It measures how well the forecast intervals capture the actual observed values. An ideal forecast
should have a coverage probability close to the nominal confidence level (e.g., 90%, 95%, or 99%).

For example, if a 95% prediction interval is used, we expect approximately 95% of the actual
target values to fall within those intervals. If the coverage is significantly lower than the
nominal level, the intervals may be too narrow; if it's significantly higher, the intervals may
be too wide.

Note: This function requires the targets array and forecasts matrix to have the same number of
observations, and the forecasts matrix must have at least 2 columns (min, max) representing
the lower and upper bounds of prediction intervals.

___
Reference:
- [Diebold, F. X., & Mariano, R. S. (1995). Comparing predictive accuracy. Journal of business & economic statistics, 13(3), 253-263.](https://www.jstor.org/stable/1392185)

pinball(tau, target, forecast)
  Pinball loss function, measures the asymmetric loss for quantile forecasts.
  Parameters:
    tau (float): The quantile level (between 0 and 1), where 0.5 represents the median.
    target (float): The actual observed value to compare against.
    forecast (float): The forecasted value.
  Returns: - The Pinball loss value, which quantifies the distance between the forecast and target relative to the specified quantile level.

___
The Pinball loss function is specifically designed for evaluating quantile forecasts. It is
asymmetric, meaning it penalizes underestimates and overestimates differently depending on the
quantile level being evaluated.

For a given quantile τ, the loss function is defined as:
- If target >= forecast: (target - forecast) * τ
- If target < forecast: (forecast - target) * (1 - τ)

This loss function is commonly used in quantile regression and probabilistic forecasting
to evaluate how well forecasts capture specific quantiles of the target distribution.

___
Reference:
- [Forecasting: Principles and Practice (3rd Edition). Chapter 5.9](https://www.otexts.com/fpp3/distaccuracy.html)

pinball_mean(tau, targets, forecasts)
  Calculates the mean pinball loss for quantile regression.
  Parameters:
    tau (float): The quantile level (between 0 and 1), where 0.5 represents the median.
    targets (array<float>): The actual observed values to compare against.
    forecasts (matrix<float>): The forecasted values in matrix format with at least 2 columns (min, max).
  Returns: - The mean pinball loss value across all observations.

___
The pinball_mean() function computes the average Pinball loss across multiple observations,
making it suitable for evaluating overall forecast performance in quantile regression tasks.

This function leverages the asymmetric Pinball loss function to evaluate how well forecasts
capture specific quantiles of the target distribution. The choice of which column from the
forecasts matrix to use depends on the quantile level:
- For τ ≤ 0.5: Uses the first column (min) of forecasts
- For τ > 0.5: Uses the second column (max) of forecasts

This loss function is commonly used in quantile regression and probabilistic forecasting
to evaluate how well forecasts capture specific quantiles of the target distribution.

___
Reference:
- [Forecasting: Principles and Practice (3rd Edition). Chapter 5.9](https://www.otexts.com/fpp3/distaccuracy.html)

---

## Source Code

````pine
// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RicardoSantos

//@version=6

// @description Time Series Benchmark Metrics. \
// Provides a comprehensive set of functions for benchmarking time series data, allowing you to evaluate the accuracy, stability, and risk characteristics of various models or strategies. The functions cover a wide range of statistical measures, including accuracy metrics (MAE, MSE, RMSE, NRMSE, MAPE, SMAPE), autocorrelation analysis (ACF, ADF), and risk measures (Theils Inequality, Sharpness, Resolution, Coverage, and Pinball).
// 
// ___
// Reference:
// - https://github.com/PYFTS/pyFTS/blob/master/pyFTS/benchmarks/Measures.py .
// - https://medium.com/analytics-vidhya/assessment-of-accuracy-metrics-for-time-series-forecasting-bc115b655705 .
// - https://www.salesforce.com/blog/gift-eval-time-series-benchmark/ .
// - https://towardsdatascience.com/an-overview-of-forecasting-performance-metrics-ef548dad0134/ .
// - https://github.com/PYFTS/pyFTS/blob/master/pyFTS/benchmarks/Measures.py .
library("TimeSeriesBenchmarkMeasures")

import RicardoSantos/SimilarityMeasures/1 as SM
import RicardoSantos/FunctionADF/1 as ADF
import RicardoSantos/MLLossFunctions/1 as ML

//#region	Mean Absolute Error (MAE).

// @function In statistics, mean absolute error (MAE) is a measure of errors between paired observations expressing the same phenomenon. Examples of Y versus X include comparisons of predicted versus observed, subsequent time versus initial time, and one technique of measurement versus an alternative technique of measurement.
// @param actual	List of actual values.
// @param forecasts	List of forecasts values.
// @returns - Mean Absolute Error (MAE).
// 
// ___
// Reference:
// - https://en.wikipedia.org/wiki/Mean_absolute_error .
// - The Orange Book of Machine Learning - Carl McBride Ellis .
export mae (float[] actual, float[] forecasts) =>
	//
	int _size = actual.size()
	switch
		_size != forecasts.size() => runtime.error("`actual`, `forecasts` arrays must have a compatible size!")
	//
	float _sum = 0.0
	for _i = 0 to _size - 1
		_sum += math.abs(actual.get(_i) - forecasts.get(_i))
	_sum / _size

//#endregion
//#region	Mean Squared Error (MSE).

// @function The Mean Squared Error (MSE) is a measure of the quality of an estimator. As it is derived from the square of Euclidean distance, it is always a positive value that decreases as the error approaches zero. 
// @param actual	List of actual values.
// @param forecasts	List of forecasts values.
// @returns - Mean Squared Error (MSE).
// 
// ___
// Reference:
// - https://en.wikipedia.org/wiki/Mean_squared_error .
export mse (float[] actual, float[] forecasts) =>
	ML.mse(actual, forecasts)

//#endregion
//#region	Root Mean Squared Error (RMSE).

// @function Calculates the Root Mean Squared Error (RMSE) between target observations and forecasts. RMSE is a standard measure of the differences between values predicted by a model and the values actually observed.
// @param	targets		List of target observations.
// @param	forecasts	List of forecasts.
// @param	order		Model order parameter that determines the starting position in the targets array, `default=0`.
// @param	offset		Forecast offset related to target, `default=0`.
// @returns - RMSE value.
export rmse (float[] targets, float[] forecasts, int order=0, int offset=0) =>
	// 
	int _ntargets = targets.size()
	int _nfrcasts = forecasts.size()
	switch
		(_ntargets - order) != _nfrcasts => runtime.error("'targets' and 'forecasts' arrays must have a compatible size!")
		(order + offset) >= _nfrcasts => runtime.error("The sum of 'order' and 'offset' values must be lower than 'forecasts' array size!")
	// 
	if offset == 0
		float _ssd = 0.0
		for _i = 0 to _nfrcasts - 1
			_ssd += math.pow(targets.get(order + _i) - forecasts.get(_i), 2.0)
		math.sqrt(_ssd / _nfrcasts)
	else
		float _ssd = 0.0
		for _i = 0 to _nfrcasts - (order + offset + 1)
			_ssd += math.pow(targets.get(order + offset + _i) - forecasts.get(_i), 2.0)
		math.sqrt(_ssd / _nfrcasts)

// TEST 28022024
// int window = 20
// int order = input.int(2)
// int offset = input.int(2)
// var float[] data0 = array.new<float>(window + order, open)	, data0.unshift(open)	, data0.pop()
// var float[] data1 = array.new<float>(window, close)			, data1.unshift(close)	, data1.pop()
// rmses = rmse(data0, data1, order, offset)
// plot(rmses)

//#endregion
//#region	Normalized Root Mean Squared Error (NRMSE).

// @function Normalised Root Mean Squared Error.
// @param	targets		List of target observations.
// @param	forecasts	List of forecasts.
// @param	order		Model order parameter that determines the starting position in the targets array, `default=0`.
// @param	offset		Forecast offset related to target, `default=0`.
// @returns - NRMSE value.
export nmrse (float[] targets, float[] forecasts, int order=0, int offset=0) =>
	// normalizing in targets because on forecasts might explode to inf (when model predict a line)
	rmse(targets, forecasts, order, offset) / targets.range()

//#endregion
//#region	RMSE interval.

// @function Root Mean Squared Error for a set of interval windows. Computes RMSE by converting interval forecasts (with min/max bounds) into point forecasts using the mean of the interval bounds, then compares against actual target values.
// @param	targets		List of target observations.
// @param	forecasts	The forecasted values in matrix format with at least 2 columns (min, max).
// @returns - RMSE value for the combined interval list.
export rmse_interval (float[] targets, matrix<float> forecasts) =>
	int _nfrcast = forecasts.rows()
	float[] _fmean = array.new<float>(_nfrcast, 0.0)
	for _i = 0 to _nfrcast - 1
		_fmean.set(_i, forecasts.row(_i).avg())
	rmse(_fmean, targets)

// TEST 28022024
// float src0 = close
// float src1 = ta.sma(src0, 2)
// float src2 = ta.sma(src0, 3)
// float src3 = ta.sma(src0, 4)
// float src4 = ta.sma(src0, 5)
// int window = 20
// var float[] data0 = array.new<float>(window, src0)	, data0.unshift(src0)	, data0.pop()
// var matrix<float> mat = matrix.new<float>(window, 4, src0)
// mat.add_row(0, array.from(src1, src2, src3, src4))
// mat.remove_row(window)
// rmses = rmse_interval(data0, mat)
// plot(rmses)

//#endregion
//#region	Mean Average Percentual Error.

// @function Mean Average Percentual Error.
// @param	targets		List of target observations.
// @param	forecasts	List of forecasts.
// @returns - MAPE value.
export mape (float[] targets, float[] forecasts) =>
	int _n = forecasts.size()
	float _s = 0.0
	for _i = 0 to _n - 1
		float _ti = targets.get(_i)
		float _fi = forecasts.get(_i)
		_s += math.abs((_ti - _fi) / _ti)
	(_s / _n) * 100.0

// TEST 28022024
// int window = 20
// var float[] data0 = array.new<float>(window, open)		, data0.unshift(open)	, data0.pop()
// var float[] data1 = array.new<float>(window, close)		, data1.unshift(close)	, data1.pop()
// float m = mape(data0, data1)
// plot(m)

// @function Symmetric Mean Average Percentual Error. Calculates the Mean Absolute Percentage Error (MAPE) between actual targets and forecasts. MAPE is a common metric for evaluating forecast accuracy, expressed as a percentage, lower values indicate a better forecast accuracy.
// @param	targets		List of target observations.
// @param	forecasts	List of forecasts.
// @param	mode		Type of method: default=0:`sum(abs(Fi-Ti)) / sum(Fi+Ti)` , 1:`mean(abs(Fi-Ti) / ((Fi + Ti) / 2))` , 2:`mean(abs(Fi-Ti) / (abs(Fi) + abs(Ti))) * 100`
// @returns - SMAPE value.
export smape (float[] targets, float[] forecasts, int mode=0) =>
	int _n = forecasts.size()
	switch mode
		1 =>
			float _s = 0.0
			for _i = 0 to _n - 1
				float _ti = targets.get(_i)
				float _fi = forecasts.get(_i)
				_s += math.abs((_ti - _fi) / ((_ti + _fi) / 2.0))
			float(_s / _n)
		2 =>
			float _s = 0.0
			for _i = 0 to _n - 1
				float _ti = targets.get(_i)
				float _fi = forecasts.get(_i)
				_s += math.abs(_ti - _fi) / (math.abs(_ti) + math.abs(_fi))
			float((_s / _n) * 100.0)
		=>
			float _s0 = 0.0
			float _s1 = 0.0
			for _i = 0 to _n - 1
				float _ti = targets.get(_i)
				float _fi = forecasts.get(_i)
				_s0 += math.abs(_ti - _fi)
				_s1 += _ti + _fi
			float(_s0 / _s1)

// TEST 28022024
// int window = 20
// var float[] data0 = array.new<float>(window, open)		, data0.unshift(open)	, data0.pop()
// var float[] data1 = array.new<float>(window, close)		, data1.unshift(close)	, data1.pop()
// float m = smape(data0, data1, input(0))
// plot(m)

//#endregion
//#region	MAPE interval.

// @function Mean Average Percentual Error for a set of interval windows.
// @param	targets		List of target observations.
// @param	forecasts	The forecasted values in matrix format with at least 2 columns (min, max).
// @returns - MAPE value for the combined interval list.
export mape_interval (float[] targets, matrix<float> forecasts) =>
	int _nfrcast = forecasts.rows()
	float[] _fmean = array.new<float>(_nfrcast, 0.0)
	for _i = 0 to _nfrcast - 1
		_fmean.set(_i, forecasts.row(_i).avg())
	mape(_fmean, targets)

//#endregion
//#region	Auto Correlation Function test (ACF).

// @function Autocorrelation Function (ACF) for a time series at a specified lag.
// @param	data	Sample data of the observations.
// @param	k		The lag period for which to calculate the autocorrelation. Must be a non-negative integer.
// @returns - The autocorrelation value at the specified lag, ranging from -1 to 1.
//
// ___
// The autocorrelation function measures the linear dependence between observations in a time series 
// at different time lags. It quantifies how well the series correlates with itself at different 
// time intervals, which is useful for identifying patterns, seasonality, and the appropriate 
// lag structure for time series models.
//
// ACF values close to 1 indicate strong positive correlation, values close to -1 indicate 
// strong negative correlation, and values near 0 indicate no linear correlation.
// 
// ___
// Reference:
// - https://statisticsbyjim.com/time-series/autocorrelation-partial-autocorrelation/
export acf (float[] data, int k) =>
	int _n = data.size() - 1
	// Handle edge cases.
	if _n == 0 or k < 0 or k >= _n
		float(na)
	else
		float _mu = data.avg()
		float _sigma = data.variance()
		float _s = 0.0
		for _t = 0 to _n - k
			_s += (data.get(_t) - _mu) * (data.get(_t + k) - _mu)
		1.0 / ((_n - k) * _sigma) * _s

// @function Autocorrelation function (ACF) for a time series at a set of specified lags.
// @param	data	Sample data of the observations.
// @param	k		List of lag periods for which to calculate the autocorrelation. Must be a non-negative integer.
// @returns - List of ACF values for provided lags.
//
// ___
// The autocorrelation function measures the linear dependence between observations in a time series 
// at different time lags. It quantifies how well the series correlates with itself at different 
// time intervals, which is useful for identifying patterns, seasonality, and the appropriate 
// lag structure for time series models.
//
// ACF values close to 1 indicate strong positive correlation, values close to -1 indicate 
// strong negative correlation, and values near 0 indicate no linear correlation.
// 
// ___
// Reference:
// - https://statisticsbyjim.com/time-series/autocorrelation-partial-autocorrelation/
export acf_multiple (float[] data, int[] k) =>
	float[] _s = array.new<float>(k.size())
	for [_i, _k] in k
		_s.set(_i, acf(data, _k))
	_s

// TEST ACF 26022024
// var float[] data = array.new<float>(20, close)
// data.unshift(close), data.pop()
// lags = array.from(2, 4, 6, 8, 12)
// acfs = acf_multiple(data, lags)
// plot(acfs.get(0))
// plot(acfs.get(1))
// plot(acfs.get(2))
// plot(acfs.get(3))
// plot(acfs.get(4))
// plot(acfs.avg())

//#endregion
//#region	Augmented Dickey Fuller test.

// @function: Augmented Dickey-Fuller test for stationarity.
// @param	data	Data series.
// @param	n_lag	Maximum lag.
// @param	conf	Confidence Probability level used to test for critical value, (`90%`, `95%`, `99%`).
// @returns
// - `adf`	The test statistic.
// - `crit`	Critical value for the test statistic at the 10 % levels.
// - `nobs`	Number of observations used for the ADF regression and calculation of the critical values.
// 
// ___
// The Augmented Dickey-Fuller test is used to determine whether a time series is stationary
// or contains a unit root (non-stationary). The null hypothesis is that the series has a unit root
// (is non-stationary), while the alternative hypothesis is that the series is stationary.
//
// A stationary time series has statistical properties that do not change over time, making it
// suitable for many time series forecasting models. If the test statistic is less than the
// critical value, we reject the null hypothesis and conclude the series is stationary.
// 
// ___
// Reference:
// - https://www.jstor.org/stable/2286348
// - https://en.wikipedia.org/wiki/Augmented_Dickey%E2%80%93Fuller_test
export adfuller (float[] data, int n_lag, string conf) =>
	ADF.adftest(data, n_lag, conf)

//#endregion
//#region	Theil's Inequality.

// @function Calculates Theil's Inequality Coefficient, a measure of forecast accuracy that quantifies the relative difference between actual and predicted values.
// @param	targets		List of target observations.
// @param	forecasts	Matrix with list of forecasts, ordered column wise.
// @returns - Theil's Inequality Coefficient value, value closer to 0 is better.
//
// ___
// Theil's Inequality Coefficient is calculated as: `sqrt(Sum((y_i - f_i)^2)) / (sqrt(Sum(y_i^2)) + sqrt(Sum(f_i^2)))`
// where `y_i` represents actual values and `f_i` represents forecast values.
// This metric ranges from 0 to infinity, with 0 indicating perfect forecast accuracy.
// 
// ___
// Reference:
// - https://en.wikipedia.org/wiki/Theil_index
export theils_inequality (float[] targets, float[] forecasts) =>
	int _nfrcast = forecasts.size()
	float _u = 0.0
	float _y = 0.0
	float _f = 0.0
	for _i = 0 to _nfrcast - 1
		float _fi = forecasts.get(_i)
		float _ti = targets.get(_i)
		_u += math.pow(_ti - _fi, 2.0)
		_y += math.pow(_ti, 2.0)
		_f += math.pow(_fi, 2.0)
	math.sqrt(_u) / math.sqrt(_y + _f)

// TEST 20250811
// int window = 20
// int order = input.int(2)
// int offset = input.int(2)
// var float[] data0 = array.new<float>(window + order, open)	, data0.unshift(open)	, data0.pop()
// var float[] data1 = array.new<float>(window, close)			, data1.unshift(close)	, data1.pop()
// float ine = theils_inequality(data0, data1)
// plot(ine)
// plot(theils_inequality(array.from(1000.0, 2000, 3000, 4000, 5000), array.from(1000.0, 2000, 3000, 4000, 5000)), 'low inequality', color.silver)
// plot(theils_inequality(array.from(100.0, 200, 300, 400, 500), array.from(1000.0, 2000, 3000, 4000, 5000)), 'high inequality', color.silver)

//#endregion
//#region	Sharpness.

// @function The average width of the forecast intervals across all observations, representing the sharpness or precision of the predictive intervals.
// @param	forecasts	The forecasted values in matrix format with at least 2 columns (min, max).
// @returns - Sharpness The sharpness level, which is the average width of all prediction intervals across the forecast horizon.
//
// ___
// Sharpness is an important metric for evaluating forecast quality. It measures how narrow or wide the 
// prediction intervals are. Higher sharpness (narrower intervals) indicates greater precision in the 
// forecast intervals, while lower sharpness (wider intervals) suggests less precision.
//
// The sharpness metric is calculated as the mean of the interval widths across all observations, where
// each interval width is the difference between the upper and lower bounds of the prediction interval.
//
// Note: This function assumes that the forecasts matrix has at least 2 columns, with the first column 
// representing the lower bounds and the second column representing the upper bounds of prediction intervals.
//
// ___
// Reference:
// - Hyndman, R. J., & Athanasopoulos, G. (2018). Forecasting: principles and practice. OTexts. https://otexts.com/fpp2/
export sharpness (matrix<float> forecasts) =>
	int _frows = forecasts.rows()
	int _fcols = forecasts.columns()
	switch
		_fcols < 2 => runtime.error('Forecasts matrix requires 2 collumn elements at a minimum (min, max).')
	//
	float _s = 0.0
	for _i = 0 to _frows - 1
		_s += forecasts.get(_i, 1) - forecasts.get(_i, 0)
	_s / _frows

// TEST 20250707
// float src0 = (close/close[1]) - 1.0
// float src1 = ta.sma(math.sum(src0, 2), 2)
// float src2 = ta.sma(math.sum(src0, 3), 3)
// int window = 20
// var float[] data0 = array.new<float>(window, src0)	, data0.unshift(src0)	, data0.pop()
// var matrix<float> mat = matrix.new<float>(window, 2, src0)
// mat.add_row(0, array.from(src1, src2))
// mat.remove_row(window)
// float sharpness_test = sharpness(mat)
// float sharp_max = 0.0, sharp_max := math.max(nz(sharp_max[1]), sharpness_test)
// float sharp_min = 0.0, sharp_min := math.min(nz(sharp_min[1]), sharpness_test)
// color sharpness_gradient = color.from_gradient(sharpness_test, sharp_min, sharp_max, color.red, color.blue)
// plot(sharpness_test, 'Sharpness Test', sharpness_gradient)
// plot(sharp_max, 'Sharp Max', color.gray)
// plot(sharp_min, 'Sharp Min', color.gray)
// barcolor(sharpness_gradient, title='Sharpness Gradient Overlay')

//#endregion
//#region	Resolution.

// @function Calculates the resolution of forecast intervals, measuring the average absolute difference between individual forecast interval widths and the overall sharpness measure.
// @param	forecasts	The forecasted values in matrix format with at least 2 columns (min, max).
// @returns - The average absolute difference between individual forecast interval widths and the overall sharpness measure, representing the resolution of the forecasts.
//
// ___
// Resolution is a key metric for evaluating forecast quality that measures the consistency of prediction 
// interval widths. It quantifies how much the individual forecast intervals vary from the average interval 
// width (sharpness). High resolution indicates that the forecast intervals are relatively consistent 
// across observations, while low resolution suggests significant variation in interval widths.
//
// The resolution is calculated as the mean absolute deviation of individual interval widths from the 
// overall sharpness value. This provides insight into the uniformity of the forecast uncertainty 
// estimates across the forecast horizon.
//
// Note: This function requires the forecasts matrix to have at least 2 columns (min, max) representing 
// the lower and upper bounds of prediction intervals.
//
// ___
// Reference:
// - [Gneiting, T., & Raftery, A. E. (2007). Strictly proper scoring rules, prediction, and estimation.](https://sites.stat.washington.edu/raftery/Research/PDF/Gneiting2007jasa.pdf)
// - [Journal of the American statistical Association, 102(477), 359-378.](https://www.jstor.org/stable/i27639812)
export resolution (matrix<float> forecasts) =>
	int _frows = forecasts.rows()
	int _fcols = forecasts.columns()
	switch
		_fcols < 2 => runtime.error('Forecasts matrix requires 2 collumn elements at a minimum (min, max).')
	//
	float _shrp = sharpness(forecasts)
	float[] _res = array.new<float>(_frows, na)
	for _i = 0 to _frows - 1
		float _frange = forecasts.get(_i, 1) - forecasts.get(_i, 0)
		_res.set(_i, math.abs(_frange - _shrp))
	_res.avg()

// TEST 20250707
// float src0 = (close/close[1]) - 1.0
// float src1 = ta.sma(math.sum(src0, 2), 2)
// float src2 = ta.sma(math.sum(src0, 10), 10)
// int window = 20
// var float[] data0 = array.new<float>(window, src0)	, data0.unshift(src0)	, data0.pop()
// var matrix<float> mat = matrix.new<float>(window, 2, src0)
// mat.add_row(0, array.from(src1, src2))
// mat.remove_row(window)
// float resolution_test = resolution(mat)
// float reso_max = 0.0, reso_max := math.max(nz(reso_max[1]), resolution_test)
// float reso_min = 0.0, reso_min := math.min(nz(reso_min[1]), resolution_test)
// color reso_gradient = color.from_gradient(resolution_test, reso_min, reso_max, color.red, color.blue)
// plot(resolution_test, 'Resolution Test', reso_gradient)
// plot(reso_max, 'Reso Max', color.gray)
// plot(reso_min, 'Reso Min', color.gray)
// barcolor(reso_gradient, title='Resolution Gradient Overlay')


//#endregion
//#region	Coverage.

// @function Calculates the coverage probability, which is the percentage of target values that fall within the corresponding forecasted prediction intervals.
// @param	targets		List of target values.
// @param	forecasts	The forecasted values in matrix format with at least 2 columns (min, max).
// @returns - Percent of target values that fall within their corresponding forecast intervals, expressed as a decimal value between 0 and 1 (or 0% and 100%).
// 
// ___
// Coverage probability is a crucial metric for evaluating the reliability of prediction intervals. 
// It measures how well the forecast intervals capture the actual observed values. An ideal forecast 
// should have a coverage probability close to the nominal confidence level (e.g., 90%, 95%, or 99%).
//
// For example, if a 95% prediction interval is used, we expect approximately 95% of the actual 
// target values to fall within those intervals. If the coverage is significantly lower than the 
// nominal level, the intervals may be too narrow; if it's significantly higher, the intervals may 
// be too wide.
//
// Note: This function requires the targets array and forecasts matrix to have the same number of 
// observations, and the forecasts matrix must have at least 2 columns (min, max) representing 
// the lower and upper bounds of prediction intervals.
//
// ___
// Reference:
// - [Diebold, F. X., & Mariano, R. S. (1995). Comparing predictive accuracy. Journal of business & economic statistics, 13(3), 253-263.](https://www.jstor.org/stable/1392185)
export coverage (float[] targets, matrix<float> forecasts) =>
	int _nobs = targets.size()
	switch
		_nobs != forecasts.rows() => runtime.error('Size of `targets` array must match `forecasts` rows!')
		forecasts.columns() < 2 => runtime.error('Forecasts matrix requires 2 collumn elements at a minimum (min, max).')
	//
	float[] _cov = array.new<float>(_nobs, 0.0)
	for _i = 0 to _nobs - 1
		float _ti = targets.get(_i)
		float _fi0 = forecasts.get(_i, 0)
		float _fi1 = forecasts.get(_i, 1)
		if _ti >= _fi0 and _ti <= _fi1
			_cov.set(_i, 1.0)
	_cov.avg()

// TEST 20250707
// float src0 = (close / close[1]) - 1.0
// float src1 = ta.sma(math.sum(src0, 2), 2)
// float src2 = ta.sma(math.sum(src0, 10), 10)
// int window = 20
// var float[] data0 = array.new<float>(window, src0)	, data0.unshift(src0)	, data0.pop()
// var matrix<float> mat = matrix.new<float>(window, 2, src0)
// mat.add_row(0, array.from(src1, src2))
// mat.remove_row(window)
// float coverage_test = coverage(data0, mat)
// float cove_max = 0.0, cove_max := math.max(nz(cove_max[1]), coverage_test)
// float cove_min = 0.0, cove_min := math.min(nz(cove_min[1]), coverage_test)
// color cove_gradient = color.from_gradient(coverage_test, cove_min, cove_max, color.red, color.blue)
// plot(coverage_test, 'Coverage Test', cove_gradient)
// plot(cove_max, 'Reso Max', color.gray)
// plot(cove_min, 'Reso Min', color.gray)
// barcolor(cove_gradient, title='Coverage Gradient Overlay')

//#endregion
//#region	Pinball.
// Forecasting: Principles and Practice. 5.9: https://www.otexts.com/fpp3/distaccuracy.html

// @function Pinball loss function, measures the asymmetric loss for quantile forecasts.
// @param	tau			The quantile level (between 0 and 1), where 0.5 represents the median.
// @param	target		The actual observed value to compare against.
// @param	forecast	The forecasted value.
// @returns - The Pinball loss value, which quantifies the distance between the forecast and target relative to the specified quantile level.
//
// ___
// The Pinball loss function is specifically designed for evaluating quantile forecasts. It is 
// asymmetric, meaning it penalizes underestimates and overestimates differently depending on the 
// quantile level being evaluated. 
//
// For a given quantile τ, the loss function is defined as:
// - If target >= forecast: (target - forecast) * τ
// - If target < forecast: (forecast - target) * (1 - τ)
//
// This loss function is commonly used in quantile regression and probabilistic forecasting 
// to evaluate how well forecasts capture specific quantiles of the target distribution.
//
// ___
// Reference:
// - [Forecasting: Principles and Practice (3rd Edition). Chapter 5.9](https://www.otexts.com/fpp3/distaccuracy.html)
export pinball (float tau, float target, float forecast) =>
	if target >= forecast
		(target - forecast) * tau
	else
		(forecast - target) * (1.0 - tau)

// @function Calculates the mean pinball loss for quantile regression.
// @param	tau			The quantile level (between 0 and 1), where 0.5 represents the median.
// @param	targets		The actual observed values to compare against.
// @param	forecasts	The forecasted values in matrix format with at least 2 columns (min, max).
// @returns - The mean pinball loss value across all observations.
// 
// ___
// The pinball_mean() function computes the average Pinball loss across multiple observations,
// making it suitable for evaluating overall forecast performance in quantile regression tasks.
// 
// This function leverages the asymmetric Pinball loss function to evaluate how well forecasts
// capture specific quantiles of the target distribution. The choice of which column from the
// forecasts matrix to use depends on the quantile level:
// - For τ ≤ 0.5: Uses the first column (min) of forecasts
// - For τ > 0.5: Uses the second column (max) of forecasts
// 
// This loss function is commonly used in quantile regression and probabilistic forecasting 
// to evaluate how well forecasts capture specific quantiles of the target distribution.
// 
// ___
// Reference:
// - [Forecasting: Principles and Practice (3rd Edition). Chapter 5.9](https://www.otexts.com/fpp3/distaccuracy.html)
export pinball_mean (float tau, float[] targets, matrix<float> forecasts) =>
	int _frows = forecasts.rows()
	int _fcols = forecasts.columns()
	switch
		_fcols < 2 => runtime.error('Forecasts matrix requires 2 collumn elements at a minimum (min, max).')
	//
	float[] _preds = array.new<float>(_frows, na)
	if tau <= 0.5
		for _i = 0 to _frows - 1
			_preds.set(_i, pinball(tau, targets.get(_i), forecasts.get(_i, 0)))
	else
		for _i = 0 to _frows - 1
			_preds.set(_i, pinball(tau, targets.get(_i), forecasts.get(_i, 1)))
	_preds.avg()

//#endregion
````
