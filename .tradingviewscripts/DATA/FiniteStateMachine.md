<!-- tradingview-pine-id: PUB;d7c6070996dc46b0bfa0b6a1261b2eb9 -->
<!-- tradingviewscripts-format: 1 -->
# FiniteStateMachine

Source: https://www.tradingview.com/script/uYMS0UUJ-FiniteStateMachine/

## Description

🟩 OVERVIEW

A flexible framework for creating, testing and implementing a Finite State Machine (FSM) in your script. FSMs use rules to control how states change in response to events. 

This is the first Finite State Machine library on TradingView and it's quite a different way to think about your script's logic. Advantages of using this vs hardcoding all your logic include: 
 • Explicit logic: You can see all rules easily side-by-side.
 • Validation: Tables show your rules and validation results right on the chart.
 • Dual approach: Simple matrix for straightforward transitions; map implementation for concurrent scenarios. You can combine them for complex needs.
 • Type safety: Shows how to use enums for robustness while maintaining string compatibility.
 • Real-world examples: Includes both conceptual (traffic lights) and practical (trading strategy) demonstrations.
 • Priority control: Explicit control over which rules take precedence when multiple conditions are met.
 • Wildcard system: Flexible pattern matching for states and events.

The library seems complex, but it's not really. Your conditions, events, and their potential interactions are complex. The FSM makes them all explicit, which is some work. However, like all "good" pain in life, this is front-loaded, and *saves* pain later, in the form of unintended interactions and bugs that are very hard to find and fix.

🟩 SIMPLE FSM (MATRIX-BASED)

The simple FSM uses a matrix to define transition rules with the structure: state > event > state. We look up the current state, check if the event in that row matches, and if it does, output the resulting state.
Each row in the matrix defines one rule, and the first matching row, counting from the top down, is applied.
A limitation of this method is that you can supply only ONE event.
You can design layered rules using widlcards. Use an empty string "" or the special string "ANY" for any state or event wildcard.

The matrix FSM is foruse where you have clear, sequential state transitions triggered by single events. Think traffic lights, or any logic where only one thing can happen at a time.
The demo for this FSM is of traffic lights.

🟩 CONCURRENT FSM (MAP-BASED)

The map FSM uses a more complex structure where each state is a key in the map, and its value is an array of event rules. Each rule maps a named condition to an output (event or next state).
This FSM can handle multiple conditions simultaneously. Rules added first have higher priority.
Adding more rules to existing states combines the entries in the map (if you use the supplied helper function) rather than overwriting them.

This FSM is for more complex scenarios where multiple conditions can be true simultaneously, and you need to control which takes precedence. Like trading strategies, or any system with concurrent conditions.
The demo for this FSM is a trading strategy.

🟩 HOW TO USE

Pine Script libraries contain reusable code for importing into indicators. You do not need to copy any code out of here. Just import the library and call the function you want.
For example, for version 1 of this library, import it like this:
[pine]
import SimpleCryptoLife/FiniteStateMachine/1
[/pine]
See the EXAMPLE USAGE sections within the library for examples of calling the functions.
For more information on libraries and incorporating them into your scripts, see the [Libraries ](https://www.tradingview.com/pine-script-docs/v6/concepts/libraries/)section of the Pine Script User Manual. 

🟩 TECHNICAL IMPLEMENTATION

Both FSM implementations support wildcards using blank strings "" or the special string "ANY". Wildcards match in this priority order:
 • Exact state + exact event match
 • Exact state + empty event (event wildcard)  
 • Empty state + exact event (state wildcard)
 • Empty state + empty event (full wildcard)

When multiple rules match the same state + event combination, the FIRST rule encountered takes priority. In the matrix FSM, this means row order determines priority. In the map FSM, it's the order you add rules to each state.

The library uses user-defined types for the map FSM:
 • o_eventRule: Maps a condition name to an output
 • o_eventRuleWrapper: Wraps an array of rules (since maps can't contain arrays directly)

Everything uses strings for maximum library compatibility, though the examples show how to use enums for type safety by converting them to strings.

Unlike normal maps where adding a duplicate key overwrites the value, this library's `m_addRuleToEventMap()` method *combines* rules, making it intuitive to build rule sets without breaking them.

🟩 VALIDATION & ERROR HANDLING

The library includes comprehensive validation functions that catch common FSM design errors:

Error detection:
 • Empty next states
 • Invalid states not in the states array  
 • Duplicate rules
 • Conflicting transitions
 • Unreachable states (no entry/exit rules)

Warning detection:
 • Redundant wildcards
 • Empty states/events (potential unintended wildcards)
 • Duplicate conditions within states

You can display validation results in tables on the chart, with tooltips providing detailed explanations. The helper functions to display the tables are exported so you can call them from your own script.

🟩 PRACTICAL EXAMPLES

The library includes four comprehensive demos:

Traffic Light Demo (Simple FSM): Uses the matrix FSM to cycle through traffic light states (red → red+amber → green → amber → red) with timer events. Includes pseudo-random "break" events and repair logic to demonstrate wildcards and priority handling.

Trading Strategy Demo (Concurrent FSM): Implements a realistic long-only trading strategy using BOTH FSM types:
 • Map FSM converts multiple technical conditions (EMA crosses, gaps, fractals, RSI) into prioritised events
 • Matrix FSM handles state transitions (idle → setup → entry → position → exit → re-entry)
 • Includes position management, stop losses, and re-entry logic

Error Demonstrations: Both FSM types include error demos with intentionally malformed rules to showcase the validation system's capabilities.

🟩 BRING ON THE FUNCTIONS

f_printFSMMatrix(_mat_rules, _a_states, _tablePosition)
  Prints a table of states and rules to the specified position on the chart. Works only with the matrix-based FSM.
  Parameters:
    _mat_rules (matrix<string>)
    _a_states (array<string>)
    _tablePosition (simple string)
  Returns: The table of states and rules.

method m_loadMatrixRulesFromText(_mat_rules, _rulesText)
  Loads rules into a rules matrix from a multiline string where each line is of the form "current state | event | next state" (ignores empty lines and trims whitespace).
This is the most human-readable way to define rules because it's a visually aligned, table-like format.
  Namespace types: matrix<string>
  Parameters:
    _mat_rules (matrix<string>)
    _rulesText (string)
  Returns: No explicit return. The matrix is modified as a side-effect.

method m_addRuleToMatrix(_mat_rules, _currentState, _event, _nextState)
  Adds a single rule to the rules matrix. This can also be quite readble if you use short variable names and careful spacing.
  Namespace types: matrix<string>
  Parameters:
    _mat_rules (matrix<string>)
    _currentState (string)
    _event (string)
    _nextState (string)
  Returns: No explicit return. The matrix is modified as a side-effect.

method m_validateRulesMatrix(_mat_rules, _a_states, _showTable, _tablePosition)
  Validates a rules matrix and a states array to check that they are well formed. Works only with the matrix-based FSM.
Checks: matrix has exactly 3 columns; no empty next states; all states defined in array; no duplicate states; no duplicate rules; all states have entry/exit rules; no conflicting transitions; no redundant wildcards. To avoid slowing down the script unnecessarily, call this method once (perhaps using `barstate.isfirst`), when the rules and states are ready.
  Namespace types: matrix<string>
  Parameters:
    _mat_rules (matrix<string>)
    _a_states (array<string>)
    _showTable (bool)
    _tablePosition (simple string)
  Returns: `true` if the rules and states are valid; `false` if errors or warnings exist.

method m_getStateFromMatrix(_mat_rules, _currentState, _event, _strictInput, _strictTransitions)
  Returns the next state based on the current state and event, or `na` if no matching transition is found. Empty (not na) entries are treated as wildcards if `strictInput` is false.
Priority: exact match > event wildcard > state wildcard > full wildcard.
  Namespace types: matrix<string>
  Parameters:
    _mat_rules (matrix<string>)
    _currentState (string)
    _event (string)
    _strictInput (bool)
    _strictTransitions (bool)
  Returns: The next state or `na`.

method m_addRuleToEventMap(_map_eventRules, _state, _condName, _output)
  Adds a single event rule to the event rules map. If the state key already exists, appends the new rule to the existing array (if different). If the state key doesn't exist, creates a new entry.
  Namespace types: map<string, o_eventRuleWrapper>
  Parameters:
    _map_eventRules (map<string, o_eventRuleWrapper>)
    _state (string)
    _condName (string)
    _output (string)
  Returns: No explicit return. The map is modified as a side-effect.

method m_addEventRulesToMapFromText(_map_eventRules, _configText)
  Loads event rules from a multiline text string into a map structure.
Format: "state | condName > output | condName > output | ..." . Pairs are ordered by priority. You can have multiple rules on the same line for one state.
Supports wildcards: Use an empty string ("") or the special string "ANY" for state or condName to create wildcard rules.
Examples: " | condName > output" (state wildcard), "state |  > output" (condition wildcard), " |  > output" (full wildcard).
Splits lines by \n, extracts state as key, creates/appends to array<o_eventRule> with new o_eventRule(condName, output).
Call once, e.g., on barstate.isfirst for best performance.
  Namespace types: map<string, o_eventRuleWrapper>
  Parameters:
    _map_eventRules (map<string, o_eventRuleWrapper>)
    _configText (string)
  Returns: No explicit return. The map is modified as a side-effect.

f_printFSMMap(_map_eventRules, _a_states, _tablePosition)
  Prints a table of map-based event rules to the specified position on the chart.
  Parameters:
    _map_eventRules (map<string, o_eventRuleWrapper>)
    _a_states (array<string>)
    _tablePosition (simple string)
  Returns: The table of map-based event rules.

method m_validateEventRulesMap(_map_eventRules, _a_states, _a_validEvents, _showTable, _tablePosition)
  Validates an event rules map to check that it's well formed.
Checks: map is not empty; wrappers contain non-empty arrays; no duplicate condition names per state; no empty fields in o_eventRule objects; optionally validates outputs against matrix events.
NOTE: Both "" and "ANY" are treated identically as wildcards for both states and conditions.
To avoid slowing down the script unnecessarily, call this method once (perhaps using `barstate.isfirst`), when the map is ready.
  Namespace types: map<string, o_eventRuleWrapper>
  Parameters:
    _map_eventRules (map<string, o_eventRuleWrapper>)
    _a_states (array<string>)
    _a_validEvents (array<string>)
    _showTable (bool)
    _tablePosition (simple string)
  Returns: `true` if the event rules map is valid; `false` if errors or warnings exist.

method m_getEventFromConditionsMap(_currentState, _a_activeConditions, _map_eventRules)
  Returns a single event or state string based on the current state and active conditions.
Uses a map of event rules where rules are pre-sorted by implicit priority via load order.
Supports wildcards using empty string ("") or "ANY" for flexible rule matching.
Priority: exact match > condition wildcard > state wildcard > full wildcard.
  Namespace types: series string, simple string, input string, const string
  Parameters:
    _currentState (string)
    _a_activeConditions (array<string>)
    _map_eventRules (map<string, o_eventRuleWrapper>)
  Returns: The output string (event or state) for the first matching condition, or na if no match found.

o_eventRule
  o_eventRule defines a condition-to-output mapping for the concurrent FSM.
  Fields:
    condName (series string): The name of the condition to check.
    output (series string): The output (event or state) when the condition is true.

o_eventRuleWrapper
  o_eventRuleWrapper wraps an array of o_eventRule for use as map values (maps cannot contain collections directly).
  Fields:
    a_rules (array<o_eventRule>): Array of o_eventRule objects for a specific state.

---

## Source Code

````pine
// @version=6
//@description A flexible framework for creating and testing a Finite State Machine (FSM). Finite State Machines use rules to control how states change in response to events. Using a FSM gives you more visibility of your logic, wheras  hardcoding everything can result in hard-to-find bugs. This library gives two separate implementations of FSMs: one using a matrix and a more complex one using a map. It uses strings for states and events for maximum compatibility. Includes comprehensive validation to catch common errors such as invalid states, conflicting transitions, duplicate rules, and unreachable states. Exported helper functions help to add rules in a simple way. Provides working demonstrations and error examples.
library("FiniteStateMachine", overlay = true)
const string SCRIPT_NAME = "FiniteStateMachine Library"  // We use script and function name constants for error reporting


//#region 🟩🟩🟩🟩🟩🟩  SIMPLE FSM: GET STATE FROM SINGLE EVENT  🟩🟩🟩🟩🟩🟩

// This FSM uses a matrix to define the transition rules. We look up the current state and then check whether the event in that row matches. If it does, we output the resulting state.
// If we don't match the state + event, we continue looking for another row with a matching state and repeat.
// A string is defined as a state or event simply by the position (column) of the strings in the matrix, which go state > event > state.
// A limitation of this design is that you must supply only ONE event. Your script must itself handle any concurrent events and output only one to pass to the FSM. If you want to handle concurrent events, use the map implementation. Search for the MAP-BASED FSM (MORE COMPLEX) section.
// When multiple rules match the same state + event combination, the FIRST rule encountered takes priority (row order determines priority).
// You can use a blank string "" or the special string "ANY" as a wildcard. Wilcards match in this priority order:
// Priority 1: Exact state + exact event match
// Priority 2: Exact state + empty event (event wildcard)
// Priority 3: Empty state + exact event (state wildcard)
// Priority 4: Empty state + empty event (full wildcard)


//#region SETUP

// FSM Display Colour Constants (used by both matrix and map tables)
const  color colour_headerBg         = color.maroon
simple color colour_headerText       = chart.fg_color  // Can't be of type "const" because the chart properties are known only at run time.
const  color colour_statesHeaderBg   = color.yellow
const  color colour_statesHeaderText = color.black
const  color colour_statesCellBg     = color.white
const  color colour_statesCellText   = color.black
const  color colour_rulesHeaderBg    = color.gray
const  color colour_rulesHeaderText  = color.black
const  color colour_rulesCellBg      = color.white
const  color colour_rulesCellText    = color.black

//#endregion SETUP

//#region 🟩🟩🟩  HELPER FUNCTIONS  🟩🟩🟩

//@function Prints a table of states and rules to the specified position on the chart. Works only with the matrix-based FSM.
//@param    matrix<string>  _mat_rules      The matrix of transition rules.
//@param    array<string>   _a_states       The array of all allowable states.
//@param    simple string   _tablePosition  The position where the table should be displayed on the chart.
//@returns  The table of states and rules.
export f_printFSMMatrix(matrix<string> _mat_rules, array<string> _a_states, simple string _tablePosition = position.top_center) =>
    // Create table to display states and rules
    int   _numStates = _a_states.size()
    int   _numRules  = _mat_rules.rows()
    int   _totalRows = _numRules + 5 // Plus five rows for the states and the various headers (main, states header, rules header, rules column headers).
    table _t_fsm     = table.new(_tablePosition, 3, _totalRows, border_width=1)
    
    int _rowIndex = 0  // We use this to keep track of how many rows we've printed.
    
    // Main header
    _t_fsm.cell(0, _rowIndex, "FiniteStateMachine Library\nStates and Rules", text_color=colour_headerText, text_size=size.large, bgcolor=colour_headerBg)
    _t_fsm.merge_cells(0, _rowIndex, 2, _rowIndex)
    _rowIndex += 1  // We printed a row so each time we add one to this variable
    
    // States header
    _t_fsm.cell(0, _rowIndex, "States", text_color=colour_statesHeaderText, text_size=size.normal, bgcolor=colour_statesHeaderBg)
    _t_fsm.merge_cells(0, _rowIndex, 2, _rowIndex)
    _rowIndex += 1
    
    // Display all states in a single cell as comma-separated string
    string _statesString = ""
    for _i = 0 to _numStates - 1
        string _state = _a_states.get(_i)
        string _displayState = _state == "" ? "<empty>" : _state
        _statesString += (_i > 0 ? ", " : "") + _displayState  // Only add a comma between states not at the beginning
    _t_fsm.cell(0, _rowIndex, _statesString, text_color=colour_statesCellText, text_size=size.normal, bgcolor=colour_statesCellBg)
    _t_fsm.merge_cells(0, _rowIndex, 2, _rowIndex)
    _rowIndex += 1
    
    // Rules header
    _t_fsm.cell(0, _rowIndex, "Rules", text_color=colour_rulesHeaderText, text_size=size.normal, text_formatting=text.format_bold, bgcolor=colour_rulesHeaderBg)
    _t_fsm.merge_cells(0, _rowIndex, 2, _rowIndex)
    _rowIndex += 1
    
    // Rules subheading
    _t_fsm.cell(0, _rowIndex, "Current State", text_color=colour_rulesHeaderText, text_size=size.normal, bgcolor=colour_rulesHeaderBg)
    _t_fsm.cell(1, _rowIndex, "Event", text_color=colour_rulesHeaderText, text_size=size.normal, bgcolor=colour_rulesHeaderBg)
    _t_fsm.cell(2, _rowIndex, "Next State", text_color=colour_rulesHeaderText, text_size=size.normal, bgcolor=colour_rulesHeaderBg)
    _rowIndex += 1
    
    // Display rules
    for _r = 0 to _numRules - 1
        // Get the current state, event, and next state from the matrix
        string _currentState = _mat_rules.get(_r, 0)
        string _event = _mat_rules.get(_r, 1)
        string _nextState = _mat_rules.get(_r, 2)
        // Display the current state, event, and next state in the table
        _t_fsm.cell(0, _rowIndex, _currentState == "" ? "<empty>" : _currentState, text_color=colour_rulesCellText, text_size=size.normal, bgcolor=colour_rulesCellBg)
        _t_fsm.cell(1, _rowIndex, _event == "" ? "<empty>" : _event, text_color=colour_rulesCellText, text_size=size.normal, bgcolor=colour_rulesCellBg)
        _t_fsm.cell(2, _rowIndex, _nextState == "" ? "<empty>" : _nextState, text_color=colour_rulesCellText, text_size=size.normal, bgcolor=colour_rulesCellBg)
        _rowIndex += 1

    _t_fsm  // Return the table. This allows the consuming script to assign the table ID to a variable.


// @function Loads rules into a rules matrix from a multiline string where each line is of the form "current state | event | next state" (ignores empty lines and trims whitespace).
//           This is the most human-readable way to define rules because it's a visually aligned, table-like format.
// @param    matrix<string>  _mat_rules  The matrix of transition rules.
// @param    string          _rulesText  The multiline string of rules.
// @returns  No explicit return. The matrix is modified as a side-effect.
export method m_loadMatrixRulesFromText(matrix<string> _mat_rules, string _rulesText) =>
    array<string> _lines = str.split(_rulesText, "\n")
    for _line in _lines
        string _trimmed = str.trim(_line)
        if _trimmed != ""
            array<string> _parts = str.split(_trimmed, "|")
            if _parts.size() != 3
                runtime.error("Invalid rule line: " + _trimmed + ". Expected 'current | event | next'.")
            _mat_rules.add_row(_mat_rules.rows(), array.from(str.trim(_parts.get(0)), str.trim(_parts.get(1)), str.trim(_parts.get(2))))


// @function Adds a single rule to the rules matrix. This can also be quite readble if you use short variable names and careful spacing.
// @param    matrix<string>  _mat_rules     The matrix of transition rules.
// @param    string          _currentState  The current state.
// @param    string          _event         The triggering event.
// @param    string          _nextState     The resulting state.
// @returns  No explicit return. The matrix is modified as a side-effect.
export method m_addRuleToMatrix(matrix<string> _mat_rules, string _currentState, string _event, string _nextState) =>
    _mat_rules.add_row(_mat_rules.rows(), array.from(_currentState, _event, _nextState))


//#endregion HELPER FUNCTIONS


//#region 🟩🟩🟩  MAIN FUNCTIONS  🟩🟩🟩

// This validation function prints a table describing all problems and potential problems with a given rules matrix plus states array. The table caells have tooltips giving extra information.

// @function Validates a rules matrix and a states array to check that they are well formed. Works only with the matrix-based FSM.
//           Checks: matrix has exactly 3 columns; no empty next states; all states defined in array; no duplicate states; no duplicate rules; all states have entry/exit rules; no conflicting transitions; no redundant wildcards. To avoid slowing down the script unnecessarily, call this method once (perhaps using `barstate.isfirst`), when the rules and states are ready.
// @param    matrix<string>  _mat_rules      The matrix of transition rules with columns: current state, event, next state.
// @param    array<string>   _a_states       The array of all allowable states.
// @param    bool            _showTable      If `true`, displays the results of the validation in a table on the chart. This feature is to help you to develop and test your rules. It should be `false` in production or just don't call this method at all.
// @param    simple string   _tablePosition  The position where the validation table is displayed on the chart.
// @returns  `true` if the rules and states are valid; `false` if errors or warnings exist.
export method m_validateRulesMatrix(matrix<string> _mat_rules, array<string> _a_states, bool _showTable = false, simple string _tablePosition = position.top_right) =>
    array<string> _errors = array.new<string>()    // Store errors for display in the validation table
    array<string> _warnings = array.new<string>()  // Store warnings for display in the validation table

    if _mat_rules.columns() != 3  // This is an error because the arrays or other operations used to create the matrix are malformed.
        _errors.push("Rules matrix must have exactly 3 columns.")
    int _numRows = _mat_rules.rows()  // Get the number of rows in the matrix

    // Check for duplicate states in the states array first
    map<string, bool> _mapStateSeen = map.new<string, bool>()  // Track states we've seen in the array
    for _state in _a_states  // Loop through every state in the *array*
        if _mapStateSeen.contains(_state)  // If a state is duplicated in the array it's probably carelessness.
            _errors.push("Duplicate state '" + _state + "' found in states array.\nThis is not checked by m_getStateFromMatrix().")
        _mapStateSeen.put(_state, true)  // Add the state to the map

    // Declare maps for consolidated row processing
    map<string, bool>   _map_rowSeen       = map.new<string, bool>()   // Track duplicate rows
    map<string, string> _map_transition    = map.new<string, string>() // Track conflicting transitions  
    map<string, bool>   _map_entries       = map.new<string, bool>()   // Track states with entry rules
    map<string, bool>   _map_exits         = map.new<string, bool>()   // Track states with exit rules
    map<string, bool>   _map_eventWildcard = map.new<string, bool>()   // Track redundant event wildcards
    map<string, bool>   _map_stateWildcard = map.new<string, bool>()   // Track redundant state wildcards  
    map<string, bool>   _map_fullWildcard  = map.new<string, bool>()   // Track redundant full wildcards

    // Process all row-level validations in one pass to avoid excessive loops (number one cause of performance bottlenecks)
    for _rowIndex = 0 to _numRows - 1  // Loop through every row to check the contents of the rules.
        string _currentState = _mat_rules.get(_rowIndex, 0)
        string _event        = _mat_rules.get(_rowIndex, 1)
        string _nextState    = _mat_rules.get(_rowIndex, 2)

        if _currentState == ""  // Empty states may or may not be intentional (wildcards) so we warn the user.
            _warnings.push("Empty current state detected in rules matrix at row " + str.tostring(_rowIndex) + "\nTreated as state wildcard in m_getStateFromMatrix() when `_strictTransitions=false`; otherwise causes runtime error.")  

        if _event == ""  // Empty events may or may not be intentional (wildcards) so we warn the user.
            _warnings.push("Empty event detected in rules matrix at row " + str.tostring(_rowIndex) + "\nTreated as event wildcard in m_getStateFromMatrix() when `_strictTransitions=false`; otherwise causes runtime error.")  

        if _nextState == ""  // If there's no next state the rule doesn't make any sense. The m_getStateFromMatrix() method returns `na` in this case.
            _errors.push("Empty next state detected in rules matrix at row " + str.tostring(_rowIndex) + "\nm_getStateFromMatrix() always returns `na` for an empty next state")

        if not _a_states.includes(_currentState) and _currentState != ""  // If the current state is not in the list of allowed states then it's likely to be an error. 
            _errors.push("Invalid current state '" + _currentState + "' not defined in states array.\nThis causes a runtime error in m_getStateFromMatrix() when `_strictTransitions=true`.")

        if not _a_states.includes(_nextState) and _nextState != ""  // If the next state is not in the list of allowed states then it's likely to be an error. 
            _errors.push("Invalid next state '" + _nextState + "' not defined in states array.\nThis causes a runtime error in m_getStateFromMatrix() when `_strictTransitions=true`.")

        // Check for duplicate rows
        string _rowKey = _currentState + "|" + _event + "|" + _nextState  // Create a key for the row by munging together the current state, event, and next state. This key should be unique.
        if _map_rowSeen.contains(_rowKey)  // If the key is not unique, then we have a duplicate rule.
            _errors.push("Exact duplicate rule found in rules matrix: [" + _currentState + " → " + _event + " → " + _nextState + "]\nThis is not checked by m_getStateFromMatrix().")
        _map_rowSeen.put(_rowKey, true)

        // Check for conflicting transitions
        string _transKey = _currentState + "|" + _event  // Create a key for the transition by munging together the current state and event. This key should be unique.
        if _map_transition.contains(_transKey)  // If the key is not unique, then we have a conflicting transition.
            if _map_transition.get(_transKey) != _nextState  // If the next state is different, then we have a conflicting transition. This is always an error.
                _errors.push("Conflicting next states for transition '" + _transKey + "'.\nThe m_getStateFromMatrix() method returns the first matching transition.")
        else
            _map_transition.put(_transKey, _nextState)

        // Track entry and exit states
        if _nextState != ""  // Record states that have entries (transitions into them)
            _map_entries.put(_nextState, true)
        if _currentState != ""  // Record states that have exits (transitions out of them)
            _map_exits.put(_currentState, true)

        // Check for redundant wildcards
        if _currentState != "" and _event == ""  // Check for redundant event wildcards (same state, empty event)
            string _eventWildcardKey = _currentState + "|eventWildcard|" + _nextState
            if _map_eventWildcard.contains(_eventWildcardKey)
                _warnings.push("Redundant event wildcard for state '" + _currentState + "' with next state '" + _nextState + "' at row " + str.tostring(_rowIndex) + ".\nThe m_getStateFromMatrix() method uses the first matching wildcard.")
            else
                _map_eventWildcard.put(_eventWildcardKey, true)
        
        if _currentState == "" and _event != ""  // Check for redundant state wildcards (empty state, same event)
            string _stateWildcardKey = _event + "|stateWildcard|" + _nextState
            if _map_stateWildcard.contains(_stateWildcardKey)
                _warnings.push("Redundant state wildcard for event '" + _event + "' with next state '" + _nextState + "' at row " + str.tostring(_rowIndex) + ".\nThe m_getStateFromMatrix() method uses the first matching wildcard.")
            else
                _map_stateWildcard.put(_stateWildcardKey, true)
        
        if _currentState == "" and _event == ""  // Check for redundant full wildcards (empty state, empty event)
            string _fullWildcardKey = "fullWildcard|" + _nextState
            if _map_fullWildcard.contains(_fullWildcardKey)
                _warnings.push("Redundant full wildcard with next state '" + _nextState + "' at row " + str.tostring(_rowIndex) + ".\nThe m_getStateFromMatrix() method uses the first matching wildcard.")
            else
                _map_fullWildcard.put(_fullWildcardKey, true)

    // Validate entries/exits using maps
    for _state in _a_states  // Check for states with no entry (no rule transitions into them)
        if not _map_entries.contains(_state)  // A valid state with no means of getting to it probably means the rules are a work in progress; there's no legitimate reason to omit a state from the rules, so we throw an *error*.
            _errors.push("There is no rule that enables the '" + _state + "' state.\nThis is not checked by m_getStateFromMatrix().")

    for _state in _a_states  // Check for states with no exit (no rule transitions out of them)
        if not _map_exits.contains(_state)  // A valid state with no means of getting out of it is almost certainly an *error*.
            _errors.push("There is no rule that exits from the '" + _state + "' state.\nThis is not checked by m_getStateFromMatrix().")
    
    // Display all warnings and errors in a table if `_showTable` is true
    int _totalMessages = _errors.size() + _warnings.size()  // We put this at this scope level so we can reference it for the final returned bool.
    if _showTable and _totalMessages > 0  // If there are any warnings or errors, display them
        // Split the error and warning messages into message and tooltip text
        array<string> _a_errorDisplay = array.new<string>()
        array<string> _a_errorTooltip = array.new<string>()
        array<string> _a_warningDisplay = array.new<string>()
        array<string> _a_warningTooltip = array.new<string>()

        if array.size(_errors) > 0  // It's a good idea to include checks for zero-size arrays whenever you use array functions, to prevent errors or needless processing
            for _i = 0 to _errors.size() - 1
                string _errorMsg = _errors.get(_i)
                array<string> _errorParts = str.split(_errorMsg, "\n")
                string _displayText = array.size(_errorParts) > 0 ? array.get(_errorParts, 0) : ""
                string _tooltipText = array.size(_errorParts) > 1 ? array.get(_errorParts, 1) : ""
                _a_errorDisplay.push(_displayText)
                _a_errorTooltip.push(_tooltipText)
        
        if array.size(_warnings) > 0
            for _i = 0 to _warnings.size() - 1
                string _warningMsg = _warnings.get(_i)
                array<string> _warningParts = str.split(_warningMsg, "\n")
                string _displayText = array.size(_warningParts) > 0 ? array.get(_warningParts, 0) : ""
                string _tooltipText = array.size(_warningParts) > 1 ? array.get(_warningParts, 1) : ""
                _a_warningDisplay.push(_displayText)
                _a_warningTooltip.push(_tooltipText)

        // Create table to display validation results (dynamically size based on messages)
        int _tableRows = 4 + _errors.size() + _warnings.size()  // Header + all messages + buffer
        table _t_validation = table.new(_tablePosition, 2, _tableRows, border_width=1)
        // Create the header
        _t_validation.cell(0, 0, "FiniteStateMachine Library\nMatrix Validation Results", text_color=chart.fg_color, text_size=size.large, bgcolor=color.maroon)
        _t_validation.merge_cells(0, 0, 1, 0)
        
        int _rowIndex = 1
        // Display errors first (red background)
        if _errors.size() > 0
            for _i = 0 to _errors.size() - 1
                string _displayText = _a_errorDisplay.get(_i)
                string _tooltipText = _a_errorTooltip.get(_i)
                
                _t_validation.cell(0, _rowIndex, "Error",     text_color=color.black, text_size=size.normal, bgcolor=color.red)
                _t_validation.cell(1, _rowIndex, _displayText, text_color=color.black, text_size=size.normal, bgcolor=color.red)
                if _tooltipText != ""
                    _t_validation.cell_set_tooltip(1, _rowIndex, _tooltipText)
                _rowIndex += 1
        
        // Display warnings (yellow background)
        if _warnings.size() > 0
            for _i = 0 to _warnings.size() - 1
                string _displayText = _a_warningDisplay.get(_i)
                string _tooltipText = _a_warningTooltip.get(_i)
                
                _t_validation.cell(0, _rowIndex, "Warning",    text_color=color.black, text_size=size.normal, bgcolor=color.yellow)
                _t_validation.cell(1, _rowIndex, _displayText, text_color=color.black, text_size=size.normal, bgcolor=color.yellow)
                if _tooltipText != ""
                    _t_validation.cell_set_tooltip(1, _rowIndex, _tooltipText)
                _rowIndex += 1
        
    else if _showTable  // If there are no warnings or errors, show a success message
        table _t_validation = table.new(_tablePosition, 2, 2, border_width=1)
        _t_validation.cell(0, 0, "FiniteStateMachine Library\nMatrix Validation Results", text_color=chart.fg_color, text_size=size.large, bgcolor=color.maroon)
        _t_validation.merge_cells(0, 0, 1, 0)
        _t_validation.cell(0, 1, "Success", text_color=color.black, text_size=size.normal, bgcolor=color.green)
        _t_validation.cell(1, 1, "Rules and states are valid.", text_color=color.black, text_size=size.normal, bgcolor=color.green)
    // Return boolean result that is `true` if there are no errors or warnings; false otherwise.
    _totalMessages == 0


// This function basically IS the FSM itself. It returns the allowed next state based on the rules matrix.
// It contains some minimal error-checking, most of which is optional, but really you should validate using the validation function during development.

// @function Returns the next state based on the current state and event, or `na` if no matching transition is found. Empty (not na) entries are treated as wildcards if `strictInput` is false.
// Priority: exact match > event wildcard > state wildcard > full wildcard.
// @param    matrix<string> _mat_rules            The matrix of transition rules.
// @param    string          _currentState        The current state.
// @param    string          _event               The triggering event.
// @param    bool            _strictInput         If `true`, validates inputs and enforces strict transition checking (no na values, no empty strings) - throws a runtime error if any of these conditions are not met.
// @param    bool            _strictTransitions   If `true`, validates that states and events exist in the matrix (current state and event must exist in matrix) - throws a runtime error if any of these conditions are not met.
// @returns  The next state or `na`.
export method m_getStateFromMatrix(matrix<string> _mat_rules, string _currentState, string _event, bool _strictInput = true, bool _strictTransitions = false) =>
    const string FUNCTION_NAME = "m_getStateFromMatrix" // Used for runtime errors.
    
    // Validate matrix structure first
    if na(_mat_rules)
        runtime.error(SCRIPT_NAME + "." + FUNCTION_NAME + "(): rules matrix cannot be na")
    if _mat_rules.columns() != 3
        runtime.error(SCRIPT_NAME + "." + FUNCTION_NAME + "(): rules matrix must have exactly 3 columns (currentState, event, nextState), found " + str.tostring(_mat_rules.columns()))
    
    // Input validation under _strictInput flag
    if _strictInput
        if na(_currentState)
            runtime.error(SCRIPT_NAME + "." + FUNCTION_NAME + "(): currentState cannot be na")
        if na(_event)
            runtime.error(SCRIPT_NAME + "." + FUNCTION_NAME + "(): event cannot be na")
        if _currentState == ""
            runtime.error(SCRIPT_NAME + "." + FUNCTION_NAME + "(): currentState cannot be empty")
        if _event == ""
            runtime.error(SCRIPT_NAME + "." + FUNCTION_NAME + "(): event cannot be empty when strictInput=true")
    else
        // Basic safety check even when not strict
        if na(_currentState) or na(_event)
            na
   
    // Declare variables used in errors and for wildcards
    string _resultState = na // This default value of `na` gets returned by the method if we don't find any matches.
    string _eventWildcard = na
    string _stateWildcard = na
    string _fullWildcard = na
   
    for _rowIndex = 0 to _mat_rules.rows() - 1 // Loop through the matrix
        string _matrixState = _mat_rules.get(_rowIndex, 0)
        string _matrixEvent = _mat_rules.get(_rowIndex, 1)
        string _matrixNext = _mat_rules.get(_rowIndex, 2)
       
        // Priority 1: Exact match - break immediately
        if _matrixState == _currentState and _matrixEvent == _event
            _resultState := _matrixNext == "" ? na : _matrixNext
            break
       
        // Priority 2: Event wildcard - store but continue looking for exact match
        if _matrixState == _currentState and _matrixEvent == "" and na(_eventWildcard)
            _eventWildcard := _matrixNext == "" ? na : _matrixNext
       
        // Priority 3: State wildcard - store but continue looking for higher priority
        if _matrixState == "" and _matrixEvent == _event and na(_stateWildcard)
            _stateWildcard := _matrixNext == "" ? na : _matrixNext
       
        // Priority 4: Full wildcard - store but continue looking for higher priority
        if _matrixState == "" and _matrixEvent == "" and na(_fullWildcard)
            _fullWildcard := _matrixNext == "" ? na : _matrixNext
   
    // Select best match found (exact match already set, or fall back to wildcards)
    if na(_resultState)
        _resultState := not na(_eventWildcard) ? _eventWildcard :
                      not na(_stateWildcard) ? _stateWildcard :
                      _fullWildcard
   
    // Strict validation after loop
    if _strictTransitions and na(_resultState)
        runtime.error(SCRIPT_NAME + "." + FUNCTION_NAME + "(): No transition rule found for currentState '" + _currentState + "' and event '" + _event + "' in transition matrix")
   
    _resultState // Return the next state, or `na`.

//#endregion MAIN FUNCTIONS

//#endregion SIMPLE FSM


//#region 🟦🟦🟦🟦🟦🟦  MAP-BASED FSM (MORE COMPLEX): GET EVENT FROM CONCURRENT CONDITIONS  🟦🟦🟦🟦🟦🟦

// This FSM implementation uses a map to define transition rules, allowing for concurrent event handling and more complex logic than the matrix-based FSM.
// Each state in the FSM is a (string) key in the map, and its value is an array of event rules, each of which maps a named condition to an output (event or next state).
// This design supports multiple conditions per state, prioritised by their order in the array. When evaluating transitions, the FSM checks each condition in priority order and triggers the first matching rule.
// Wildcards are supported: you can use an empty string "" or the special string "ANY" for the state or condition name to create wildcard rules that match any state or condition.
// Use this FSM implementation if you have multiple events or conditions that can be true at the same time, and you want to control which takes precedence.


//#region 🟦🟦🟦  SETUP  🟦🟦🟦

// We use a user-defined type called o_eventRule to define the actual rule mapping a condition to an output. And because we want to pass in one or multiple rules at once, we put them in an array.
// Since you cannot have maps of arrays, we wrap the array in another UDT. So we have an o_eventWrapper object which contains only an array of rules, where each rule is an object that contains two strings.
// If this sounds needlessly complex, it's only because we use strings in the end. Programmers can use this template but rewrite it with custom enum types for complete type safety.
// Here, we're constrained to use strings so the library works for everyone. However, in our examples we do use enums and convert them to strings as an added type-safety layer.

//@type o_eventRule defines a condition-to-output mapping for the concurrent FSM.
//@field condName The name of the condition to check.
//@field output   The output (event or state) when the condition is true.
export type o_eventRule
    string condName
    string output

//@type o_eventRuleWrapper wraps an array of o_eventRule for use as map values (maps cannot contain collections directly).
//@field a_rules Array of o_eventRule objects for a specific state.
export type o_eventRuleWrapper
    array<o_eventRule> a_rules

//#endregion SETUP


//#region 🟦🟦🟦  HELPER FUNCTIONS  🟦🟦🟦  

// This method adds rules to the map. Now , normally if you add a second entry into a map with the same key, it *overwrites* the first entry. Here, it is *combined*.
// This makes it much simpler and more intuitive to add rules. The first rule for a given state is added first and subsequent rule objects are added at later indexes.
// The first matching rule from the array is applied. Therefore, the order in which you add rules for a state is the priority order.

//@function Adds a single event rule to the event rules map. If the state key already exists, appends the new rule to the existing array (if different). If the state key doesn't exist, creates a new entry.
//@param    map<string, o_eventRuleWrapper> _map_eventRules  The target map to add the rule to.
//@param    string                        _state             The state key for the rule.
//@param    string                        _condName          The condition name for the rule.
//@param    string                        _output            The output for the rule.
//@returns  No explicit return. The map is modified as a side-effect.
export method m_addRuleToEventMap(map<string, o_eventRuleWrapper> _map_eventRules, string _state, string _condName, string _output) =>
    o_eventRule _newRule = o_eventRule.new(_condName, _output)  // Create the new o_eventRule
    
    // Check if the state key already exists in the map
    if _map_eventRules.contains(_state)
        // Get existing wrapper and check if this rule already exists (avoid duplicates)
        o_eventRuleWrapper _existingWrapper = _map_eventRules.get(_state)
        bool _ruleExists = false
        
        // Check if this exact rule already exists in the array
        for _existingRule in _existingWrapper.a_rules
            if _existingRule.condName == _condName and _existingRule.output == _output
                _ruleExists := true
                break
        
        // Only add if it's a different rule
        if not _ruleExists
            _existingWrapper.a_rules.push(_newRule)
    else
        _map_eventRules.put(_state, o_eventRuleWrapper.new(array.from(_newRule)))  // Create new o_eventRuleWrapper with the rule and add to map
    string(na)  // A hack to avoid `if` branches returning different types

        
//@function Loads event rules from a multiline text string into a map structure.
//          Format: "state | condName > output | condName > output | ..." . Pairs are ordered by priority. You can have multiple rules on the same line for one state.
//          Supports wildcards: Use an empty string ("") or the special string "ANY" for state or condName to create wildcard rules.
//          Examples: " | condName > output" (state wildcard), "state |  > output" (condition wildcard), " |  > output" (full wildcard).
//          Splits lines by \n, extracts state as key, creates/appends to array<o_eventRule> with new o_eventRule(condName, output).
//          Call once, e.g., on barstate.isfirst for best performance.
//@param    map<string, o_eventRuleWrapper> _map_eventRules  The target map to populate with event rules.
//@param    string                        _configText        The multiline text  that contains the rules.
//@returns  No explicit return. The map is modified as a side-effect.
export method m_addEventRulesToMapFromText(map<string, o_eventRuleWrapper> _map_eventRules, string _configText) =>
    array<string> _a_lines = str.split(_configText, "\n")
    for _line in _a_lines
        string _trimmed = str.trim(_line)
        if _trimmed != ""
            array<string> _a_stateParts = str.split(_trimmed, "|")
            if _a_stateParts.size() < 2
                runtime.error("Invalid rule line: " + _trimmed + ". Expected 'state | condName > output | condName > output | ...'.")
            
            string _state = str.trim(_a_stateParts.get(0))
            
            // Get or create o_eventRuleWrapper for this state
            o_eventRuleWrapper _wrapper = na
            if _map_eventRules.contains(_state)
                _wrapper := _map_eventRules.get(_state)
            else
                _wrapper := o_eventRuleWrapper.new(array.new<o_eventRule>())
                _map_eventRules.put(_state, _wrapper)
            
            // Process condition-output pairs for this state
            for _i = 1 to _a_stateParts.size() - 1
                string _conditionPart = str.trim(_a_stateParts.get(_i))
                array<string> _a_condOutput = str.split(_conditionPart, ">")
                if _a_condOutput.size() != 2
                    runtime.error("Invalid condition-output pair: " + _conditionPart + ". Expected 'condName > output'.")
                
                string _condName = str.trim(_a_condOutput.get(0))
                string _output = str.trim(_a_condOutput.get(1))
                
                _wrapper.a_rules.push(o_eventRule.new(_condName, _output))


// We need a specialised function to print the contents of a map, because unlike an aray we can't just `str.tostring()` a map.

//@function Prints a table of map-based event rules to the specified position on the chart.
//@param    map<string, o_eventRuleWrapper> _map_eventRules  The map of event rules.
//@param    array<string>                 _a_states          The array of all allowable states.
//@param    simple string                 _tablePosition     The position where the table should be displayed on the chart.
//@returns  The table of map-based event rules.
export f_printFSMMap(map<string, o_eventRuleWrapper> _map_eventRules, array<string> _a_states = na, simple string _tablePosition = position.top_center) =>
    // Count total rules for table sizing
    int _totalRules = 0
    for _state in _map_eventRules.keys()
        o_eventRuleWrapper _wrapper = _map_eventRules.get(_state)
        _totalRules += _wrapper.a_rules.size()
    
    // Create table to display map-based FSM rules
    int _tableRows = 5 + _totalRules // Headers + all rules
    table _t_fsmMap = table.new(_tablePosition, 3, _tableRows, border_width=1)
    
    int _rowIndex = 0
    
    // Main header
    _t_fsmMap.cell(0, _rowIndex, "FiniteStateMachine Library\nMap-Based Event Rules", text_color=colour_headerText, text_size=size.large, bgcolor=colour_headerBg)
    _t_fsmMap.merge_cells(0, _rowIndex, 2, _rowIndex)
    _rowIndex += 1
    
    // States header
    _t_fsmMap.cell(0, _rowIndex, "States", text_color=colour_statesHeaderText, text_size=size.normal, bgcolor=colour_statesHeaderBg)
    _t_fsmMap.merge_cells(0, _rowIndex, 2, _rowIndex)
    _rowIndex += 1
    
    // Display all states in a single cell as comma-separated string
    string _statesString = ""
    if not na(_a_states)
        for _i = 0 to _a_states.size() - 1
            string _state = _a_states.get(_i)
            string _displayState = _state == "" ? "<empty>" : _state
            _statesString += (_i > 0 ? ", " : "") + _displayState
        _t_fsmMap.cell(0, _rowIndex, _statesString, text_color=colour_statesCellText, text_size=size.normal, bgcolor=colour_statesCellBg)
        _t_fsmMap.merge_cells(0, _rowIndex, 2, _rowIndex)
        _rowIndex += 1
    
    // Rules header
    _t_fsmMap.cell(0, _rowIndex, "Event Rules", text_color=colour_rulesHeaderText, text_size=size.normal, text_formatting=text.format_bold, bgcolor=colour_rulesHeaderBg)
    _t_fsmMap.merge_cells(0, _rowIndex, 2, _rowIndex)
    _rowIndex += 1
    
    // Column headers
    _t_fsmMap.cell(0, _rowIndex, "State", text_color=colour_rulesHeaderText, text_size=size.normal, bgcolor=colour_rulesHeaderBg)
    _t_fsmMap.cell(1, _rowIndex, "Condition", text_color=colour_rulesHeaderText, text_size=size.normal, bgcolor=colour_rulesHeaderBg)
    _t_fsmMap.cell(2, _rowIndex, "Output", text_color=colour_rulesHeaderText, text_size=size.normal, bgcolor=colour_rulesHeaderBg)
    _rowIndex += 1
    
    // Display all rules from the map
    for _state in _map_eventRules.keys()
        o_eventRuleWrapper _wrapper = _map_eventRules.get(_state)
        for _rule in _wrapper.a_rules
            string _displayState = _state == "" ? "<empty>" : (_state == "ANY" ? "ANY" : _state)
            string _displayCondition = _rule.condName == "" ? "<empty>" : (_rule.condName == "ANY" ? "ANY" : _rule.condName)
            string _displayOutput = _rule.output == "" ? "<empty>" : _rule.output
            
            _t_fsmMap.cell(0, _rowIndex, _displayState, text_color=colour_rulesCellText, text_size=size.normal, bgcolor=colour_rulesCellBg)
            _t_fsmMap.cell(1, _rowIndex, _displayCondition, text_color=colour_rulesCellText, text_size=size.normal, bgcolor=colour_rulesCellBg)
            _t_fsmMap.cell(2, _rowIndex, _displayOutput, text_color=colour_rulesCellText, text_size=size.normal, bgcolor=colour_rulesCellBg)
            _rowIndex += 1

    _t_fsmMap  // Return the table.

//#endregion HELPER FUNCTIONS


//#region 🟦🟦🟦  MAIN FUNCTIONS  🟦🟦🟦

// Validate the event rules map. This function works very much like m_validateRulesMatrix.

//@function Validates an event rules map to check that it's well formed.
//          Checks: map is not empty; wrappers contain non-empty arrays; no duplicate condition names per state; no empty fields in o_eventRule objects; optionally validates outputs against matrix events.
//          NOTE: Both "" and "ANY" are treated identically as wildcards for both states and conditions.
//          To avoid slowing down the script unnecessarily, call this method once (perhaps using `barstate.isfirst`), when the map is ready.
//@param    map<string, o_eventRuleWrapper> _map_eventRules    The event rules map to validate.
//@param    array<string>                 _a_states            The array of all allowable states (optional, pass `na` to skip state validation).
//@param    array<string>                 _a_validEvents       Array of valid events from matrix FSM (optional, pass `na` to skip event validation).
//@param    bool                          _showTable           If `true`, displays the results of the validation in a table on the chart.
//@param    simple string                 _tablePosition       The position where the validation table should be displayed on the chart.
//@returns  `true` if the event rules map is valid; `false` if errors or warnings exist.
export method m_validateEventRulesMap(map<string, o_eventRuleWrapper> _map_eventRules, array<string> _a_states = na, array<string> _a_validEvents = na, bool _showTable = false, simple string _tablePosition = position.top_right) =>
    array<string> _errors = array.new<string>()    // Store errors for display in the validation table
    array<string> _warnings = array.new<string>()  // Store warnings for display in the validation table

    // Check if map is empty
    if _map_eventRules.size() == 0
        _errors.push("Event rules map is empty.\nNo states or rules defined for concurrent FSM.")

    // Declare maps for wildcard tracking (similar to matrix validation)
    map<string, bool> _map_conditionWildcard = map.new<string, bool>()  // Track redundant condition wildcards
    map<string, bool> _map_stateWildcard     = map.new<string, bool>()  // Track redundant state wildcards  
    map<string, bool> _map_fullWildcard      = map.new<string, bool>()  // Track redundant full wildcards

    // Process all states in the map
    for _state in _map_eventRules.keys()
        if (_state == "" or _state == "ANY")
            _warnings.push("Wildcard state key '" + (_state == "" ? "<empty>" : _state) + "' found in event rules map.\nThis creates state wildcard rules that match any state.")
        
        // Validate against states array if provided (skip wildcard states)
        if not na(_a_states) and not (_state == "" or _state == "ANY")
            if not _a_states.includes(_state)
                _errors.push("Invalid state '" + _state + "' not defined in states array.\nAll map states should exist in the states array.")

        // Get wrapper and validate
        o_eventRuleWrapper _wrapper = _map_eventRules.get(_state)
        if na(_wrapper)
            _errors.push("o_eventRuleWrapper is na for state '" + _state + "'.\nThis is kind of a big deal.")
            continue

        if _wrapper.a_rules.size() == 0
            _warnings.push("State '" + _state + "' has no event rules.\nThis state's never going to generate events.")
            continue

        // Check for duplicate condition names within this state
        map<string, bool> _conditionsSeen = map.new<string, bool>()
        for _rule in _wrapper.a_rules  // No alternative but to loop within a loop. Hopefully the loop is very small.
            if na(_rule)
                _errors.push("EventRule is na in state '" + _state + "'.\nSomeone done messed up.")
                continue

            if (_rule.condName == "" or _rule.condName == "ANY")
                _warnings.push("Wildcard condition '" + (_rule.condName == "" ? "<empty>" : _rule.condName) + "' found in state '" + _state + "'.\nThis creates a condition wildcard that matches any active condition.")

            if _rule.output == ""
                _warnings.push("Empty output found in state '" + _state + "' for condition '" + _rule.condName + "'.\nThis rule's never going to do anything.")

            // Check for duplicate conditions
            if _conditionsSeen.contains(_rule.condName)
                _warnings.push("Duplicate condition '" + _rule.condName + "' in state '" + _state + "'.\nOnly the first rule's going to be used due to priority ordering.")
            else
                _conditionsSeen.put(_rule.condName, true)

            // Check for redundant wildcards (similar to matrix validation logic)
            if not (_state == "" or _state == "ANY") and (_rule.condName == "" or _rule.condName == "ANY")  // Condition wildcard (exact state, any condition)
                string _conditionWildcardKey = _state + "|conditionWildcard|" + _rule.output
                if _map_conditionWildcard.contains(_conditionWildcardKey)
                    _warnings.push("Redundant condition wildcard for state '" + _state + "' with output '" + _rule.output + "'.\nThe m_getEventFromConditionsMap() method uses the first matching wildcard.")
                else
                    _map_conditionWildcard.put(_conditionWildcardKey, true)
            
            if (_state == "" or _state == "ANY") and not (_rule.condName == "" or _rule.condName == "ANY")  // State wildcard (any state, exact condition)
                string _stateWildcardKey = _rule.condName + "|stateWildcard|" + _rule.output
                if _map_stateWildcard.contains(_stateWildcardKey)
                    _warnings.push("Redundant state wildcard for condition '" + _rule.condName + "' with output '" + _rule.output + "'.\nThe m_getEventFromConditionsMap() method uses the first matching wildcard.")
                else
                    _map_stateWildcard.put(_stateWildcardKey, true)
            
            if (_state == "" or _state == "ANY") and (_rule.condName == "" or _rule.condName == "ANY")  // Full wildcard (any state, any condition)
                string _fullWildcardKey = "fullWildcard|" + _rule.output
                if _map_fullWildcard.contains(_fullWildcardKey)
                    _warnings.push("Redundant full wildcard with output '" + _rule.output + "'.\nThe m_getEventFromConditionsMap() method uses the first matching wildcard.")
                else
                    _map_fullWildcard.put(_fullWildcardKey, true)

            // Validate outputs against valid events if provided
            if not na(_a_validEvents) and _rule.output != ""
                if not _a_validEvents.includes(_rule.output)
                    _errors.push("Output '" + _rule.output + "' in state '" + _state + "' not found in valid events array.\nThis output's never going to be processed by the matrix FSM.")

    // Display all warnings and errors in a table if `_showTable` is true
    int _totalMessages = _errors.size() + _warnings.size()
    if _showTable and _totalMessages > 0
        // Split the error and warning messages into message and tooltip text
        array<string> _a_errorDisplay = array.new<string>()
        array<string> _a_errorTooltip = array.new<string>()
        array<string> _a_warningDisplay = array.new<string>()
        array<string> _a_warningTooltip = array.new<string>()

        if array.size(_errors) > 0
            for _i = 0 to _errors.size() - 1
                string _errorMsg = _errors.get(_i)
                array<string> _errorParts = str.split(_errorMsg, "\n")
                string _displayText = array.size(_errorParts) > 0 ? array.get(_errorParts, 0) : ""
                string _tooltipText = array.size(_errorParts) > 1 ? array.get(_errorParts, 1) : ""
                _a_errorDisplay.push(_displayText)
                _a_errorTooltip.push(_tooltipText)
        
        if array.size(_warnings) > 0
            for _i = 0 to _warnings.size() - 1
                string _warningMsg = _warnings.get(_i)
                array<string> _warningParts = str.split(_warningMsg, "\n")
                string _displayText = array.size(_warningParts) > 0 ? array.get(_warningParts, 0) : ""
                string _tooltipText = array.size(_warningParts) > 1 ? array.get(_warningParts, 1) : ""
                _a_warningDisplay.push(_displayText)
                _a_warningTooltip.push(_tooltipText)

        // Create table to display validation results
        int _tableRows = 4 + _errors.size() + _warnings.size()
        table _t_validation = table.new(_tablePosition, 2, _tableRows, border_width=1)
        // Create the header
        _t_validation.cell(0, 0, "FiniteStateMachine Library\nMap Validation Results", text_color=chart.fg_color, text_size=size.large, bgcolor=color.red)
        _t_validation.merge_cells(0, 0, 1, 0)
        
        int _rowIndex = 1
        // Display errors first (red background)
        if _errors.size() > 0
            for _i = 0 to _errors.size() - 1
                string _displayText = _a_errorDisplay.get(_i)
                string _tooltipText = _a_errorTooltip.get(_i)
                
                _t_validation.cell(0, _rowIndex, "Error",     text_color=color.black, text_size=size.normal, bgcolor=color.red)
                _t_validation.cell(1, _rowIndex, _displayText, text_color=color.black, text_size=size.normal, bgcolor=color.red)
                if _tooltipText != ""
                    _t_validation.cell_set_tooltip(1, _rowIndex, _tooltipText)
                _rowIndex += 1
        
        // Display warnings (yellow background)
        if _warnings.size() > 0
            for _i = 0 to _warnings.size() - 1
                string _displayText = _a_warningDisplay.get(_i)
                string _tooltipText = _a_warningTooltip.get(_i)
                
                _t_validation.cell(0, _rowIndex, "Warning",    text_color=color.black, text_size=size.normal, bgcolor=color.yellow)
                _t_validation.cell(1, _rowIndex, _displayText, text_color=color.black, text_size=size.normal, bgcolor=color.yellow)
                if _tooltipText != ""
                    _t_validation.cell_set_tooltip(1, _rowIndex, _tooltipText)
                _rowIndex += 1
        
    else if _showTable
        // If there are no warnings or errors, show a success message
        table _t_validation = table.new(_tablePosition, 2, 2, border_width=1)
        _t_validation.cell(0, 0, "FiniteStateMachine Library\nMap Validation Results", text_color=chart.fg_color, text_size=size.large, bgcolor=color.green)
        _t_validation.merge_cells(0, 0, 1, 0)
        _t_validation.cell(0, 1, "Success", text_color=color.black, text_size=size.normal, bgcolor=color.green)
        _t_validation.cell(1, 1, "Event rules map is valid.", text_color=color.black, text_size=size.normal, bgcolor=color.green)
    
    // Return boolean result
    _totalMessages == 0


// The actual FSM bit. We get the next allowed state based on the input state and the events.
// Note that because we use strings, whether we call the things events, states, or conditions depends entirely on how you end up using it.

//@function Returns a single event or state string based on the current state and active conditions.
//          Uses a map of event rules where rules are pre-sorted by implicit priority via load order.
//          Supports wildcards using empty string ("") or "ANY" for flexible rule matching.
//          Priority: exact match > condition wildcard > state wildcard > full wildcard.
//@param    string                        _currentState        The current state to look up in the map.
//@param    array<string>                 _a_activeConditions  Array of condition names that are currently true.
//@param    map<string, o_eventRuleWrapper> _map_eventRules    Map of state to o_eventRuleWrapper containing condition-output rules.
//@returns  The output string (event or state) for the first matching condition, or na if no match found.
export method m_getEventFromConditionsMap(string _currentState, array<string> _a_activeConditions, map<string, o_eventRuleWrapper> _map_eventRules) =>
    const string FUNCTION_NAME = "m_getEventFromConditionsMap"
    string _result = na
    
    // A lil input validation to prevent runtime errors
    if na(_currentState)
        _result := na
    else if na(_map_eventRules)
        _result := na
    else if na(_a_activeConditions)
        // Soft early return when there are no active conditions to evaluate
        _result := na
    else
        // Check for exact matches and wildcard matches. Hopefully we get an exact match most of the time and thus only need to run one loop.

        // Priority 1: Exact state match with exact condition match
        if na(_result) and _map_eventRules.contains(_currentState)
            o_eventRuleWrapper _wrapper = _map_eventRules.get(_currentState)
            for _rule in _wrapper.a_rules
                if not (_rule.condName == "" or _rule.condName == "ANY") and _a_activeConditions.includes(_rule.condName)
                    _result := _rule.output
                    break
        
        // Priority 2: Exact state match with condition wildcard (any condition)
        if na(_result) and _map_eventRules.contains(_currentState)
            o_eventRuleWrapper _wrapper = _map_eventRules.get(_currentState)
            for _rule in _wrapper.a_rules
                if (_rule.condName == "" or _rule.condName == "ANY")
                    _result := _rule.output
                    break
        
        // Priority 3: State wildcard with exact condition match (any state)
        if na(_result)
            // Check for empty string wildcard state
            if _map_eventRules.contains("")
                o_eventRuleWrapper _wrapper = _map_eventRules.get("")
                for _rule in _wrapper.a_rules
                    if not (_rule.condName == "" or _rule.condName == "ANY") and _a_activeConditions.includes(_rule.condName)
                        _result := _rule.output
                        break
            
            // Check for "ANY" wildcard state if not found with empty string
            if na(_result) and _map_eventRules.contains("ANY")
                o_eventRuleWrapper _wrapper = _map_eventRules.get("ANY")
                for _rule in _wrapper.a_rules
                    if not (_rule.condName == "" or _rule.condName == "ANY") and _a_activeConditions.includes(_rule.condName)
                        _result := _rule.output
                        break
        
        // Priority 4: Full wildcard (any state, any condition)
        if na(_result)
            // Check for empty string wildcard state with wildcard condition
            if _map_eventRules.contains("")
                o_eventRuleWrapper _wrapper = _map_eventRules.get("")
                for _rule in _wrapper.a_rules
                    if (_rule.condName == "" or _rule.condName == "ANY")
                        _result := _rule.output
                        break
            
            // Check for "ANY" wildcard state with wildcard condition if not found
            if na(_result) and _map_eventRules.contains("ANY")
                o_eventRuleWrapper _wrapper = _map_eventRules.get("ANY")
                for _rule in _wrapper.a_rules
                    if (_rule.condName == "" or _rule.condName == "ANY")
                        _result := _rule.output
                        break
    
    _result

//#endregion MAIN FUNCTIONS

//#endregion MAP-BASED FSM


//#region 🔵🔵🔵🔵🔵🔵  EXAMPLE USAGE  🔵🔵🔵🔵🔵🔵

// The example usage section gives two separate examples: one for the matrix FSM and one for the map FSM.
// We have 4 demos: a working one for each FSM and an error demo for each FSM. Use the script inputs to select the FSM and working/error type.


//#region COMMON SETUP

// Here we define the inputs used to control what gets run and displayed. We also use enums to define the options for the inputs.

//@enum Used to choose which FSM implementation to demonstrate.
//@field simpleMatrix Uses the simple matrix-based FSM (m_getStateFromMatrix).
//@field concurrentMap Uses the concurrent map-based FSM (m_getEventFromConditionsMap).
enum FSMType
    simpleMatrix = "Simple (Matrix)"
    concurrentMap = "Concurrent (Map)"

//@enum Used to choose the type of demonstration.
//@field workingDemo Shows a properly functioning FSM with valid rules.
//@field errorDemo Shows validation errors with intentionally malformed rules.
enum DemoType
    workingDemo = "Working"
    errorDemo = "Error"

// Input to choose FSM type
FSMType in_fsmType = input.enum(defval=FSMType.simpleMatrix, title="FSM Type", 
  tooltip="Choose which FSM implementation to demonstrate:\n• Simple (Matrix): Uses matrix-based state transitions with single events\n• Concurrent (Map): Uses map-based condition evaluation with multiple concurrent conditions")

// Input to choose demo type
DemoType in_demoType = input.enum(defval=DemoType.workingDemo, title="Demo Type",
  tooltip="Choose the type of demonstration:\n• Working Demo: Shows a properly functioning FSM with valid rules\n• Error Demo: Shows validation errors with intentionally malformed rules")

//@enum Used for the input to choose the rule loading method.
//@field individualRules Uses the `m_addRuleToMatrix()` method to add rules one by one.
//@field textFormat Uses the `m_loadMatrixRulesFromText()` method to load rules from a formatted text string.
enum RuleLoadingMethod
    individualRules = "Individual Rules"
    textFormat = "Multiline Text"

// Input to choose rule loading method. Enums are probably overkill here but I kind of got locked in.
RuleLoadingMethod in_ruleLoadingMethod = input.enum(defval=RuleLoadingMethod.individualRules, title="Rule Loading Method",  tooltip="Choose how to load FSM rules:\n• Individual Rules: Add rules one by one using m_addRuleToMatrix()\n• Text Format: Load rules from a formatted text string using m_loadMatrixRulesFromText()")

// Inputs for table display options
bool in_showStatesRulesTable = input.bool(defval=true, title="Show States and Rules Table", tooltip="Display the FSM states and rules in a table on the chart. This is useful for debugging and understanding the FSM rules but should not be enabled for production.")

bool in_showValidationTable = input.bool(defval=true, title="Show Validation Results", tooltip="Display the FSM validation results in a table on the chart. This is useful for debugging and understanding the FSM rules but should not be enabled for production.")

var color colour_background = na

// CONSTANTS
// We use these just to reduce clutter.
const string above = location.abovebar
const string below = location.belowbar

// STATE AND EVENT ENUMS (We must declare them here upfront for both examples so that we can use the s() function freely)
// We define enums for states as best practice for robustness. We convert them to string variables so that any mistakes show up immediately as compilation errors instead of causing bugs later.

// SIMPLE DMEO ENUMS

//@enum The set of allowed states. Here we use a simple traffic light analogy.
//@field stateRed         Stop at the red lights.
//@field stateRedAndAmber Red and amber means get ready to go.
//@field stateGreen       Hey buddy it doesn't get any greener.
//@field stateAmber       Amber means prepare to stop.
//@field stateBroken      The lights are broken and not working.
//@field stateFixed       The lights have just been fixed (temporary state).
enum State
    stateRed
    stateRedAndAmber
    stateGreen
    stateAmber
    stateBroken
    stateFixed

//@enum The allowed set of events. Event priority: eventLightsBreak > eventLightsGetFixed > eventTimerExpired.
//@field eventTimerExpired   The event that triggers a transition to the next state.
//@field eventLightsBreak    The event that breaks the lights (can happen from any state).
//@field eventLightsGetFixed The event that fixes the lights after being broken.
//@field eventEvenBar        The event that fires on an even bar.
enum Event
    eventTimerExpired
    eventLightsBreak
    eventLightsGetFixed
    eventEvenBar


//@enum Error demo states for validation testing for the simple demo.
//@field waiting        Initial state
//@field processing     Working state  
//@field completed      Final state
//@field orphaned       State with no entry rules (error)
//@field trapped        State with no exit rules (error)
//@field invalidState   Invalid state not in states array (error)
//@field invalidNext    Invalid next state not in states array (error)
enum ErrorState
    waiting
    processing
    completed
    orphaned
    trapped
    invalidState
    invalidNext


// CONCURRENT DEMO ENUMS

//@enum Trading events generated by Map FSM
//@field startSetup        Initial bullish signal
//@field confirmGap        Gap requirement met
//@field confirmPullback   Valid pullback occurred
//@field enterTrade        All entry conditions met
//@field monitorExit       Start looking for exit
//@field exitTrade         Exit signal confirmed
//@field cancelSetup       Setup invalidated
//@field resumePosition    RSI cooled off
//@field lookForReentry    Start looking for re-entry after exit below 21 EMA
//@field reenterTrade      Re-enter position above 8 EMA
//@field raiseStop         Raise stop to trailing level below recent low
//@field noEvent           No action needed
enum TradingEvent
    startSetup
    confirmGap
    confirmPullback
    enterTrade
    monitorExit
    exitTrade
    cancelSetup
    resumePosition
    lookForReentry
    reenterTrade
    raiseStop
    noEvent


//@enum Trading states for the Matrix FSM for the concurrent demo
//@field idle               Waiting for signal
//@field awaitingGap        Cross detected, waiting for gap confirmation
//@field awaitingPullback   Gap confirmed, waiting for pullback
//@field awaitingEntry      Pullback occurred, waiting for fractal
//@field inPosition         Trade active
//@field monitoringExit     RSI overbought, watching for exit
//@field exited             Closing position completed
//@field lookingForReentry  After exit below 21 EMA, waiting for re-entry above 8 EMA
enum TradingState
    idle
    awaitingGap
    awaitingPullback
    awaitingEntry
    inPosition
    monitoringExit
    exited
    lookingForReentry


//@enum Error demo events for validation testing for the simple and concurrent demos.
//@field start         Begin processing
//@field finish        Complete processing
//@field reset         Return to waiting
//@field cancel        Cancel operation
//@field event         Generic event
//@field timeout       Timer expired
//@field error         Error occurred
//@field trap          Enter trapped state
//@field emergency     Emergency condition
//@field panic         Panic condition
//@field trigger       Trigger condition
//@field invalidEvent  Invalid event not in valid events array (error)
enum ErrorEvent
    start
    finish
    reset
    cancel
    event
    timeout
    error
    trap
    emergency
    panic
    trigger
    invalidEvent

//@function Converts variables to string format. Saves space and makes the code more readable. This works for all types including enums (for now, you need to declare the enum before the function but this will probly be fixed in the future).
//@param    _thing  The variable to convert to string format.
//@returns  The variable in string format.
s(_thing) => str.tostring(_thing)

//#endregion COMMON SETUP


//#region 🟢🟢🟢  SIMPLE FSM DEMO  🟢🟢🟢

// This demo uses the simple matrix-based FSM to colour the chart background like a set of traffic lights. We choose traffic lights because everyone is familiar with how they work.
// States cycle through red → red+amber → green → amber → red on a countdown timer event. The timer is 10 bars for most transitions, and 5 bars for amber and amber+red.
// A pseudorandom event breaks this cycle: Using a state wildcard, lights can break from any state via a "break" event. Breaks are marked with the ⚡ character.
// States also affect other changes in the script. When the state changes to stateBroken, a separate bar timer starts. After 12 bars, an event is created to fix the lightsm arked with 🔧 on the chart.
// Using an event wildcard, the state returns to red on *any* event. We send an event every two bars so that the Fixed state always lasts one or two bars. Because this even-bar event has no rule configured, it does nothing except in this wildcard case.
// Because this simple FSM can only handle one event at a time, and our events can potentially happen at the same time, we hardcode their priority: break > fix > timer > even.
// For concurrent events, see the map-based FSM demo.

// Variable to store current event for global scope plotting
var string eventForPlotting = na

// Convert enums to strings because a matrix must be of only ONE type, so we can't mix state enums and event enums.
// We assign the converted enums to variables and, for the option to load rules one at a time, use only variables to create the array and matrix. Any mistakes or typos thus show up as compilation errors and don't cause silent bugs.
string stateRed         = s(State.stateRed)
string stateRedAndAmber = s(State.stateRedAndAmber)
string stateGreen       = s(State.stateGreen)
string stateAmber       = s(State.stateAmber)
string stateBroken      = s(State.stateBroken)
string stateFixed       = s(State.stateFixed)

string eventTimerExpired   = s(Event.eventTimerExpired)
string eventLightsBreak    = s(Event.eventLightsBreak)
string eventLightsGetFixed = s(Event.eventLightsGetFixed)
string eventEvenBar        = s(Event.eventEvenBar)

// Build the states array. If you load the rules as a pure multiline string you could use strings instead of variable names here and do away with the state and event enums entirely.
var array<string> a_simpleStates = array.new<string>()
if barstate.isfirst
    a_simpleStates.push(stateRed)
    a_simpleStates.push(stateRedAndAmber)
    a_simpleStates.push(stateGreen)
    a_simpleStates.push(stateAmber)
    a_simpleStates.push(stateBroken)
    a_simpleStates.push(stateFixed)

// Declare the rules in a string upfront. Used for the option to load rules via multiline text. 
// This way of defining rules does NOT use variable names and so is less robust - we will not get compilation errors to warn us if we misspell any names. The benefit is that it has maximum readability.
// Because it does not use variable names, state and event enums are not needed for this method.
// Format: "current state | event | next state" (one rule per line, ignores empty lines, trims whitespace)
// REMEMBER: Add \n for a newline character at the end of each line.
const string rulesText = "
  stateRed         | eventTimerExpired   | stateRedAndAmber \n
  stateRedAndAmber | eventTimerExpired   | stateGreen \n
  stateGreen       | eventTimerExpired   | stateAmber \n
  stateAmber       | eventTimerExpired   | stateRed \n
  stateBroken      | eventLightsGetFixed | stateFixed \n
                   | eventLightsBreak    | stateBroken \n
  stateFixed       |                     | stateRed \n"

// Build the rules matrix using the selected method.
var matrix<string> mat_simpleRules = matrix.new<string>(0, 3, "")
if barstate.isfirst
    switch in_ruleLoadingMethod
        RuleLoadingMethod.individualRules =>
            // Method 1: Add rules individually using m_addRuleToMatrix() and variable names.
            mat_simpleRules.m_addRuleToMatrix(stateRed,         eventTimerExpired,   stateRedAndAmber)  // Normal traffic light cycle
            mat_simpleRules.m_addRuleToMatrix(stateRedAndAmber, eventTimerExpired,   stateGreen)
            mat_simpleRules.m_addRuleToMatrix(stateGreen,       eventTimerExpired,   stateAmber)
            mat_simpleRules.m_addRuleToMatrix(stateAmber,       eventTimerExpired,   stateRed)
            mat_simpleRules.m_addRuleToMatrix(stateBroken,      eventLightsGetFixed, stateFixed)   // Breakdown and repair logic
            mat_simpleRules.m_addRuleToMatrix("",               eventLightsBreak,    stateBroken)  // State wildcard: any state can break
            mat_simpleRules.m_addRuleToMatrix(stateFixed,       "",                  stateRed)     // Event wildcard: fixed state goes to red on any event
        RuleLoadingMethod.textFormat =>
            // Method 2: Load rules from formatted text using m_loadMatrixRulesFromText()     
            mat_simpleRules.m_loadMatrixRulesFromText(rulesText)

// Print the FSM table to the chart 
if in_fsmType == FSMType.simpleMatrix and in_demoType == DemoType.workingDemo and in_showStatesRulesTable and barstate.isfirst
    f_printFSMMatrix(mat_simpleRules, a_simpleStates, position.top_center)

// Validate the FSM setup once at the start
if in_fsmType == FSMType.simpleMatrix and in_demoType == DemoType.workingDemo and barstate.isfirst
    bool isSimpleValid = m_validateRulesMatrix(
      _mat_rules=mat_simpleRules,
      _a_states=a_simpleStates,
      _showTable=in_showValidationTable,
      _tablePosition=position.top_right)

// Run the core FSM logic
if in_fsmType == FSMType.simpleMatrix and in_demoType == DemoType.workingDemo

    // Define semi-random timer for light breakdown (40 + 1-20 bars using math.random())
    var int nextBreakBar = na
    if na(nextBreakBar)  // Initialize on first bar
        nextBreakBar := 40 + math.floor(math.random() * 20) + 1  // 40 + (1-20)
    
    var int brokenAtBar = na  // Define fixed timer for light repair (12 bars after breakdown)
    
    // Define countdown timer that counts down from initial value and triggers when reaching zero
    var int    countdownTimer = 10  // Initialize to 10 bars
    var string previousState = na  // Track state changes to reset timer
    
    // Define conditions for events with hardcoded priority: eventLightsBreak > eventLightsGetFixed > eventTimerExpired
    bool isTimerExpired   = countdownTimer <= 0  // Timer triggers when countdown reaches zero
    bool isLightsBreak    = bar_index == nextBreakBar
    bool isLightsGetFixed = not na(brokenAtBar) and bar_index == brokenAtBar + 12
    
    string simpleEvent = na  // Determine the event based on priority and current state (resets each bar)
    if isLightsBreak  // Highest priority
        simpleEvent := eventLightsBreak
        brokenAtBar := bar_index  // Record when lights broke
        nextBreakBar := bar_index + 30 + math.floor(math.random() * 20) + 1  // Schedule next break
    else if isLightsGetFixed  // Second priority
        simpleEvent := eventLightsGetFixed
        brokenAtBar := na  // Reset broken timer
    else if isTimerExpired  // Lowest priority
        simpleEvent := eventTimerExpired
    else if bar_index % 2 == 0  // If no events, every other bar we raise an event eventEvenBar. This event is valid but has no corresponding rule in the matrix.
        simpleEvent := eventEvenBar

    // Get next state; stay in current state if no transition.
    var string currentSimpleState = stateRed  // Start at red; defined only once.
    if not na(simpleEvent)  // Only call the rules when we have an event.
        string nextSimpleState = m_getStateFromMatrix(
          _mat_rules=mat_simpleRules,
          _currentState=currentSimpleState,
          _event=simpleEvent,
          _strictInput=true,
          _strictTransitions=false)  // NOTE: If we used strictTransitions=true, the event eventEvenBar would cause an error. We would have to create an explicit rule for it.
        
        // Check for state change and reset countdown timer accordingly
        if not na(nextSimpleState) and nextSimpleState != currentSimpleState
            // Reset timer based on the new state - check if it contains 'amber'
            if str.contains(nextSimpleState, "Amber")
                countdownTimer := 5  // Shorter duration for amber states
            else
                countdownTimer := 10  // Default duration for other states
            previousState := currentSimpleState  // Store previous state
            
        currentSimpleState := na(nextSimpleState) ? currentSimpleState : nextSimpleState  // Only reassign the current state if the returned state is not `na`.

    // Assign background color and character display based on state using a switch.
    colour_background := switch currentSimpleState
        stateRed         => color.new(color.red, 90)
        stateRedAndAmber => color.new(color.orange, 90)
        stateGreen       => color.new(color.green, 90)
        stateAmber       => color.new(color.yellow, 90)
        stateBroken      => color.new(color.maroon, 85)   // Dark red for broken
        stateFixed       => color.new(color.aqua, 85)     // Light blue for fixed
        => na
    
    // Store event for plotting later in the global scope
    if not na(simpleEvent)
        eventForPlotting := simpleEvent
    else
        eventForPlotting := na
    
    // Countdown timer decrement each bar (but don't go below 0)
    if countdownTimer > 0
        countdownTimer -= 1

// Plot character displays for special events
plotchar(series=eventForPlotting == s(Event.eventLightsBreak), char="⚡", location=above, color=color.red, size=size.normal, title="Lights Break")
plotchar(series=eventForPlotting == s(Event.eventLightsGetFixed), char="🔧", location=below, color=color.blue, size=size.normal, title="Lights Fixed")

//#endregion SIMPLE FSM DEMO


//#region 🟦🟦🟦  CONCURRENT FSM DEMO  🟦🟦🟦

// This demo implements an actual realistic trading strategy using two FSMs.
// We first use the map FSM to turn multiple technical conditions (EMA crossovers, price gaps, fractals, RSI levels, etc.) into events. We pass ALL conditions at once to the FSM in an array. We defined implicit priority of the rules by the order in which we load the rules into the map (translates into the order of rule objects in the array).
// Once we have generated one single event per bar, we then ALSO use the simpler, matrix-based FSM to turn the events (from a given state) into the next state. We can do this because we know there's only one event at a time.
// The conditions are technical signals or states. The events are interpreted actions that we need to take. The states represent the result on our trade. 
// We display characters on the chart for events, and colour the background for states. You can see the conditions by inspecting price, the plotted EMAs, and RSI using the built-in Relative Strength Index indicator.
// We also plot the entry, stop, and exit prices.
// The trading strategy itself has multiple phases, each of which is represented using a state, including setup, entry, position management, exit, and re-entry. 
// The strategy is well documented in the map rules table itself, which you can display by selecting the "Show states and rules table" input. Briefly:
//      The strategy is long-only. It wants an 8,21 EMA crossover followed by price extending clear of the 8 EMA followed by a pullback and then pivot low for an entry.
//      The setup is cancelled if the trend breaks.
//      We set a stop loss on entry and exit the position if it's hit.
//      If RSI gets overbought, we raise our stop and look for an exit, which is confirmed by a high pivot.
//      If we exit but the EMAs stay in bullish order and then price closes above the 8 EMA then we re-enter.
// We could make it more complex to be more profitable, but this suffices as a demo.
// Once again we use enums (defined in the COMMON SETUP section above) for type safety.

// Variables for concurrent FSM demo
var string currentTradingState = s(TradingState.idle)
var float  entryPrice = na
var float  stopPrice = na
var float  exitPrice = na
string tradingEvent = s(TradingEvent.noEvent)
float  ema8 = na
float  ema21 = na
var bool isStopHit = false  // You can't have `na` values for bool variables from Pine v6 onwards.

// Only run for concurrent FSM type with working demo
if in_fsmType == FSMType.concurrentMap and in_demoType == DemoType.workingDemo

    // Trading indicators
    ema8 := ta.ema(close, 8)
    ema21 := ta.ema(close, 21)
    float rsi = ta.rsi(close, 14)
    float atr = ta.atr(14)

    // Raw market conditions
    bool isEmaCrossUp    = ta.crossover(ema8, ema21)
    bool isEmaGapPresent = low > ema8  // Gap exists between price and EMA8
    bool isPullbackTouch = low <= ema8 and close > ema21
    bool isFractalLow    = not na(ta.pivotlow(1, 1))
    bool isRsiOverbought = rsi > 70
    bool isFractalHigh   = not na(ta.pivothigh(1, 1))
    bool isReversal      = ta.crossunder(ema8, ema21)
    bool isTrendBroken   = close < ema21
    bool isExitBelow21   = close < ema21
    bool isReentryAbove8 = close > ema8 and ema8 > ema21  // Only re-enter if EMAs are bullish
    isStopHit := not na(stopPrice) and low <= stopPrice

    // Combined conditions (AND logic for Map FSM)
    bool isCrossWithGap = isEmaCrossUp and isEmaGapPresent
    bool isCrossWithoutGap = isEmaCrossUp and not isEmaGapPresent
    bool isExitSignal = isFractalHigh and isRsiOverbought

    // Convert enums to strings
    string s_idle               = s(TradingState.idle)
    string s_awaitingGap        = s(TradingState.awaitingGap)
    string s_awaitingPullback   = s(TradingState.awaitingPullback)
    string s_awaitingEntry      = s(TradingState.awaitingEntry)
    string s_inPosition         = s(TradingState.inPosition)
    string s_monitoringExit     = s(TradingState.monitoringExit)
    string s_exited             = s(TradingState.exited)
    string s_lookingForReentry  = s(TradingState.lookingForReentry)

    string e_startSetup      = s(TradingEvent.startSetup)
    string e_confirmGap      = s(TradingEvent.confirmGap)
    string e_confirmPullback = s(TradingEvent.confirmPullback)
    string e_enterTrade      = s(TradingEvent.enterTrade)
    string e_monitorExit     = s(TradingEvent.monitorExit)
    string e_exitTrade       = s(TradingEvent.exitTrade)
    string e_cancelSetup     = s(TradingEvent.cancelSetup)
    string e_resumePosition  = s(TradingEvent.resumePosition)
    string e_lookForReentry  = s(TradingEvent.lookForReentry)
    string e_reenterTrade    = s(TradingEvent.reenterTrade)
    string e_raiseStop       = s(TradingEvent.raiseStop)
    string e_noEvent         = s(TradingEvent.noEvent)

    // Build Map FSM for event generation - maps conditions to events based on state
    var map<string, o_eventRuleWrapper> map_tradingConditions = map.new<string, o_eventRuleWrapper>()
    if barstate.isfirst
        // From idle state
        // NOTE: Even though the map is keyed off the state string, adding a second rule with the same state does NOT overwrite the first one. The method cleverly combines them into one entry.
        map_tradingConditions.m_addRuleToEventMap(s_idle,             "isCrossWithGap",    e_confirmGap)
        map_tradingConditions.m_addRuleToEventMap(s_idle,             "isCrossWithoutGap", e_startSetup)
        
        // From awaitingGap  
        map_tradingConditions.m_addRuleToEventMap(s_awaitingGap,      "isEmaGapPresent",   e_confirmGap)
        map_tradingConditions.m_addRuleToEventMap(s_awaitingGap,      "isTrendBroken",     e_cancelSetup)
        
        // From awaitingPullback
        map_tradingConditions.m_addRuleToEventMap(s_awaitingPullback, "isPullbackTouch",   e_confirmPullback)
        map_tradingConditions.m_addRuleToEventMap(s_awaitingPullback, "isTrendBroken",     e_cancelSetup)
        
        // From awaitingEntry
        map_tradingConditions.m_addRuleToEventMap(s_awaitingEntry,    "isFractalLow",      e_enterTrade)
        map_tradingConditions.m_addRuleToEventMap(s_awaitingEntry,    "isTrendBroken",     e_cancelSetup)
        
        // From inPosition
        map_tradingConditions.m_addRuleToEventMap(s_inPosition,       "isStopHit",       e_exitTrade)
        map_tradingConditions.m_addRuleToEventMap(s_inPosition,       "isRsiOverbought",   e_monitorExit)
        map_tradingConditions.m_addRuleToEventMap(s_inPosition,       "isTrendBroken",     e_exitTrade)
        
        // From monitoringExit
        map_tradingConditions.m_addRuleToEventMap(s_monitoringExit,   "isStopHit",       e_exitTrade)
        map_tradingConditions.m_addRuleToEventMap(s_monitoringExit,   "isExitSignal",      e_raiseStop)
        map_tradingConditions.m_addRuleToEventMap(s_monitoringExit,   "!isRsiOverbought",  e_resumePosition)
        
        // From exited
        map_tradingConditions.m_addRuleToEventMap(s_exited,           "isExitBelow21",     e_lookForReentry)
        
        // From lookingForReentry
        map_tradingConditions.m_addRuleToEventMap(s_lookingForReentry, "isReentryAbove8",   e_reenterTrade)
        
        // State wildcards (apply to any state)
        map_tradingConditions.m_addRuleToEventMap("",                 "isReversal",        e_cancelSetup)  // Global reversal handling

    // Build Matrix FSM for state management - maps events to state transitions
    var matrix<string> mat_tradingStates = matrix.new<string>(0, 3, "")
    if barstate.isfirst
        // From idle
        mat_tradingStates.m_addRuleToMatrix(s_idle,             e_startSetup,      s_awaitingGap)
        mat_tradingStates.m_addRuleToMatrix(s_idle,             e_confirmGap,      s_awaitingPullback)
        
        // From awaitingGap
        mat_tradingStates.m_addRuleToMatrix(s_awaitingGap,      e_confirmGap,      s_awaitingPullback)
        
        // From awaitingPullback
        mat_tradingStates.m_addRuleToMatrix(s_awaitingPullback, e_confirmPullback, s_awaitingEntry)
        
        // From awaitingEntry
        mat_tradingStates.m_addRuleToMatrix(s_awaitingEntry,    e_enterTrade,      s_inPosition)
        
        // From inPosition
        mat_tradingStates.m_addRuleToMatrix(s_inPosition,       e_monitorExit,     s_monitoringExit)
        mat_tradingStates.m_addRuleToMatrix(s_inPosition,       e_exitTrade,       s_exited)
        
        // From monitoringExit
        mat_tradingStates.m_addRuleToMatrix(s_monitoringExit,   e_exitTrade,       s_exited)
        mat_tradingStates.m_addRuleToMatrix(s_monitoringExit,   e_raiseStop,       s_monitoringExit)  // Stay in monitoring state with raised stop
        mat_tradingStates.m_addRuleToMatrix(s_monitoringExit,   e_resumePosition,  s_inPosition)
        
        // From exited
        mat_tradingStates.m_addRuleToMatrix(s_exited,           e_lookForReentry,  s_lookingForReentry)
        // Removed event-wildcard auto-transition from exited → idle
        
        // From lookingForReentry
        mat_tradingStates.m_addRuleToMatrix(s_lookingForReentry, e_reenterTrade,    s_inPosition)
        
        // Cancellation from any state
        mat_tradingStates.m_addRuleToMatrix("",                 e_cancelSetup,     s_idle)  // Wildcard

    // Build states array for validation
    var array<string> a_tradingStates = array.new<string>()
    if barstate.isfirst
        a_tradingStates.push(s_idle)
        a_tradingStates.push(s_awaitingGap)
        a_tradingStates.push(s_awaitingPullback)
        a_tradingStates.push(s_awaitingEntry)
        a_tradingStates.push(s_inPosition)
        a_tradingStates.push(s_monitoringExit)
        a_tradingStates.push(s_exited)
        a_tradingStates.push(s_lookingForReentry)

    // Validate Matrix FSM
    if barstate.isfirst and in_showValidationTable
        bool isMatrixValid = m_validateRulesMatrix(
          _mat_rules=mat_tradingStates,
          _a_states=a_tradingStates,
          _showTable=true,
          _tablePosition=position.middle_right)

    // Print the concurrent FSM states and rules table (map-based rules)
    if barstate.isfirst and in_showStatesRulesTable
        f_printFSMMap(map_tradingConditions, a_tradingStates, position.top_center)

    // State management
    var string lastEvent = e_noEvent

    // Build array of active conditions for current bar. We load ALL true events in the one array.
    array<string> a_activeConditions = array.new<string>()
    if isCrossWithGap
        a_activeConditions.push("isCrossWithGap")
    if isCrossWithoutGap
        a_activeConditions.push("isCrossWithoutGap")
    if isEmaGapPresent
        a_activeConditions.push("isEmaGapPresent")
    if isPullbackTouch
        a_activeConditions.push("isPullbackTouch")
    if isFractalLow
        a_activeConditions.push("isFractalLow")
    if isRsiOverbought
        a_activeConditions.push("isRsiOverbought")
    if isExitSignal
        a_activeConditions.push("isExitSignal")
    if not isRsiOverbought
        a_activeConditions.push("!isRsiOverbought")
    if isReversal
        a_activeConditions.push("isReversal")
    if isTrendBroken
        a_activeConditions.push("isTrendBroken")
    if isStopHit
        a_activeConditions.push("isStopHit")
    if isExitBelow21
        a_activeConditions.push("isExitBelow21")
    if isReentryAbove8
        a_activeConditions.push("isReentryAbove8")

    // Get single event from Map FSM
    tradingEvent := currentTradingState.m_getEventFromConditionsMap(a_activeConditions, map_tradingConditions)
    
    // If no event found, use noEvent
    if na(tradingEvent)
        tradingEvent := e_noEvent

    // Apply state transition via Matrix FSM
    if tradingEvent != e_noEvent
        string nextState = m_getStateFromMatrix(
          _mat_rules=mat_tradingStates,
          _currentState=currentTradingState,
          _event=tradingEvent,
          _strictInput=true,
          _strictTransitions=false)
        
        if not na(nextState)
            currentTradingState := nextState
            lastEvent := tradingEvent  

    // Side effects on state transitions
    if currentTradingState == s_inPosition and currentTradingState[1] == s_awaitingEntry
        // Only set entry/stop on actual entry, not when resuming from monitoring
        entryPrice := close
        stopPrice := entryPrice - atr
        exitPrice := na  // Clear previous exit price when entering new position
    
    // Set entry/stop on re-entry from lookingForReentry state
    if currentTradingState == s_inPosition and currentTradingState[1] == s_lookingForReentry
        entryPrice := close
        stopPrice := entryPrice - atr
        exitPrice := na  // Clear previous exit price when re-entering
    
    // Raise stop when we get exit signal (instead of exiting immediately)
    if tradingEvent == e_raiseStop
        stopPrice := low - atr / 4
    
    // Set exit price only when actually exiting a position (not cancelling a setup)
    if tradingEvent == e_exitTrade and (currentTradingState[1] == s_inPosition or currentTradingState[1] == s_monitoringExit)
        exitPrice := close
    
    // Clear exit price when cancelling a setup
    if tradingEvent == e_cancelSetup
        exitPrice := na
    
    // Clear all prices when leaving exited state (going to idle or lookingForReentry)
    if currentTradingState[1] == s_exited and currentTradingState != s_exited
        entryPrice := na
        stopPrice := na
        exitPrice := na
    
    if currentTradingState == s_idle
        entryPrice := na
        stopPrice := na
        exitPrice := na

    // Visual feedback
    colour_background := switch currentTradingState
        s_idle             => na
        s_awaitingGap      => color.new(color.blue, 95)
        s_awaitingPullback => color.new(color.blue, 90)
        s_awaitingEntry    => color.new(color.orange, 90)
        s_inPosition       => color.new(color.green, 85)
        s_monitoringExit   => color.new(color.yellow, 85)
        s_exited           => color.new(color.red, 85)
        s_lookingForReentry => color.new(color.purple, 90)
        => na

// Reset variables when not in concurrent demo
bool is_concurrentDemo = in_fsmType == FSMType.concurrentMap and in_demoType == DemoType.workingDemo

if not is_concurrentDemo
    tradingEvent := s(TradingEvent.noEvent)
    ema8 := na
    ema21 := na
    exitPrice := na
    isStopHit := false


// Plot events above/below bars
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.startSetup),                  char="S", location=above, color=color.blue,   size=size.tiny, title="Start Setup")       
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.confirmGap),                  char="G", location=above, color=color.blue,   size=size.tiny, title="Confirm Gap")       
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.confirmPullback),             char="P", location=above, color=color.orange, size=size.tiny, title="Confirm Pullback")  
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.enterTrade),                  char="E", location=above, color=color.green,  size=size.tiny, title="Enter Trade")       
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.monitorExit),                 char="M", location=below, color=color.yellow, size=size.tiny, title="Monitor Exit")      
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.exitTrade) and isStopHit,     char="⚠", location=below, color=color.purple, size=size.small, title="Stop Hit")          
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.exitTrade) and not isStopHit, char="X", location=below, color=color.red,    size=size.small, title="Exit Trade")        
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.cancelSetup),                 char="C", location=below, color=color.red,    size=size.tiny, title="Cancel Setup")      
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.resumePosition),              char="R", location=below, color=color.green,  size=size.tiny, title="Resume Position")   
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.lookForReentry),              char="L", location=below, color=color.purple, size=size.tiny, title="Look For Re-entry") 
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.reenterTrade),                char="E", location=above, color=color.green,  size=size.tiny, title="Re-enter Trade")   
plotchar(series=is_concurrentDemo and tradingEvent == s(TradingEvent.raiseStop),                   char="↑", location=below, color=color.orange, size=size.tiny, title="Raise Stop")       


// Plot EMAs and entry/stop levels
plot(series=is_concurrentDemo ? ema8 : na,       color=color.blue,  linewidth=1, title="EMA 8")
plot(series=is_concurrentDemo ? ema21 : na,      color=color.red,   linewidth=2, title="EMA 21")
plot(series=is_concurrentDemo ? entryPrice : na, color=color.green, linewidth=2, style=plot.style_circles, title="Entry Price")
plot(series=is_concurrentDemo ? stopPrice : na,  color=color.red,   linewidth=2, style=plot.style_circles, title="Stop Price")
plot(series=is_concurrentDemo ? exitPrice : na,  color=color.orange, linewidth=5, style=plot.style_cross, title="Exit Price")

//#endregion CONCURRENT FSM DEMO



//#region 🔴🔴🔴  ERROR DEMONSTRATION  🔴🔴🔴

//#region 🔴🔴 SIMPLE FSM ERROR DEMO 🔴🔴

// This demonstration shows the range of validation errors by feeding the matrix FSM intentionally malformed rules

if barstate.isfirst and in_fsmType == FSMType.simpleMatrix and in_demoType == DemoType.errorDemo

    // This section demonstrates every type of validation error that can be detected by m_validateRulesMatrix().
    // Note that m_validateRulesMatrix() does not throw runtime errors, but m_getStateFromMatrix() and m_loadMatrixRulesFromText() can.
    // Convert error demo enums to string variables for consistency and robustness
    string errorWaiting      = s(ErrorState.waiting)
    string errorProcessing   = s(ErrorState.processing)
    string errorCompleted    = s(ErrorState.completed)
    string errorOrphaned     = s(ErrorState.orphaned)
    string errorTrapped      = s(ErrorState.trapped)
    string errorInvalidState = s(ErrorState.invalidState)
    string errorInvalidNext  = s(ErrorState.invalidNext)
    
    string errorStart        = s(ErrorEvent.start)
    string errorFinish       = s(ErrorEvent.finish)
    string errorReset        = s(ErrorEvent.reset)
    string errorCancel       = s(ErrorEvent.cancel)
    string errorEvent        = s(ErrorEvent.event)
    string errorTimeout      = s(ErrorEvent.timeout)
    string errorError        = s(ErrorEvent.error)
    string errorTrap         = s(ErrorEvent.trap)
    // Create malformed states array with intentional problems
    array<string> a_errorStates = array.new<string>()
    a_errorStates.push(errorWaiting)      // Valid state
    a_errorStates.push(errorProcessing)   // Valid state  
    a_errorStates.push(errorCompleted)    // Valid state
    a_errorStates.push(errorWaiting)      // Duplicate state (ERROR)
    a_errorStates.push(errorOrphaned)     // State with no entry rules (ERROR)
    a_errorStates.push(errorTrapped)      // State with no exit rules (ERROR)

    // Create malformed rules matrix
    matrix<string> mat_errorRules = matrix.new<string>(0, 3, "")
    
    // Add rules that demonstrate every type of error
    mat_errorRules.m_addRuleToMatrix(errorWaiting,    errorStart,  errorProcessing)  // Valid rules
    mat_errorRules.m_addRuleToMatrix(errorProcessing, errorFinish, errorCompleted)   
    mat_errorRules.m_addRuleToMatrix(errorCompleted,  errorReset,  errorWaiting)      
    
    // ERROR: Empty next state
    mat_errorRules.m_addRuleToMatrix(errorWaiting, errorCancel, "")
    
    // ERROR: Invalid states not in states array
    mat_errorRules.m_addRuleToMatrix(errorInvalidState,  errorEvent, errorWaiting)   // Invalid current state
    mat_errorRules.m_addRuleToMatrix(errorWaiting,       errorEvent, errorInvalidNext)    // Invalid next state
    
    // ERROR: Exact duplicate rules
    mat_errorRules.m_addRuleToMatrix(errorWaiting, errorStart, errorProcessing)      // Duplicate rule
    
    // ERROR: Conflicting transitions (same current state + event, different next state)
    mat_errorRules.m_addRuleToMatrix(errorWaiting, errorStart, errorCompleted)      
    
    // WARNING: Empty current state and event (treated as wildcards)
    mat_errorRules.m_addRuleToMatrix("",           errorTimeout, errorWaiting)             
    mat_errorRules.m_addRuleToMatrix(errorProcessing, "",        errorWaiting)          
    
    // WARNING: Redundant wildcards
    mat_errorRules.m_addRuleToMatrix(errorWaiting, "", errorCompleted)           // Event wildcard
    mat_errorRules.m_addRuleToMatrix(errorWaiting, "", errorProcessing)          // Redundant event wildcard
    
    mat_errorRules.m_addRuleToMatrix("", errorError, errorWaiting)               // State wildcard
    mat_errorRules.m_addRuleToMatrix("", errorError, errorCompleted)             // Redundant state wildcard
    
    mat_errorRules.m_addRuleToMatrix("", "", errorWaiting)                       // Full wildcard
    mat_errorRules.m_addRuleToMatrix("", "", errorCompleted)                     // Redundant full wildcard
    
    // ERROR: trapped" state has no exit rules
    mat_errorRules.m_addRuleToMatrix(errorCompleted, errorTrap, errorTrapped)    // Entry to trapped state
    
    // ERROR: "orphaned" state has no entry rules

    // Print the error demo states and rules table
    if in_showStatesRulesTable
        f_printFSMMatrix(mat_errorRules, a_errorStates, position.top_center)

        // Validate the malformed FSM to demonstrate all error types
    bool isErrorDemoValid = m_validateRulesMatrix(
      _mat_rules=mat_errorRules,
      _a_states=a_errorStates,
      _showTable=in_showValidationTable,
      _tablePosition=position.top_right)

//#endregion SIMPLE FSM ERROR DEMO


//#region 🔴🔴 CONCURRENT FSM ERROR DEMO 🔴🔴

// This demonstration shows the range of validation errors by feeding the map FSM intentionally malformed rules.
// Note that the "Concurrent" working demo uses BOTH the map and matrix FSMs but the error demos only show one or the other.

if barstate.isfirst and in_fsmType == FSMType.concurrentMap and in_demoType == DemoType.errorDemo

    // This section demonstrates every type of validation error that can be detected by m_validateEventRulesMap().
    // Note that m_validateEventRulesMap() does not throw runtime errors, but m_getEventFromConditionsMap() can.
    // Convert error demo enums to string variables for consistency and robustness
    string errorWaiting      = s(ErrorState.waiting)
    string errorProcessing   = s(ErrorState.processing)
    string errorCompleted    = s(ErrorState.completed)
    string errorOrphaned     = s(ErrorState.orphaned)
    string errorTrapped      = s(ErrorState.trapped)
    string errorInvalidState = s(ErrorState.invalidState)
    
    string errorStart        = s(ErrorEvent.start)
    string errorFinish       = s(ErrorEvent.finish)
    string errorReset        = s(ErrorEvent.reset)
    string errorCancel       = s(ErrorEvent.cancel)
    string errorEvent        = s(ErrorEvent.event)
    string errorEmergency    = s(ErrorEvent.emergency)
    string errorPanic        = s(ErrorEvent.panic)
    string errorTrap         = s(ErrorEvent.trap)
    string errorTrigger      = s(ErrorEvent.trigger)
    string errorInvalidEvent = s(ErrorEvent.invalidEvent)
    // Create malformed states array with intentional problems
    array<string> a_errorStates = array.new<string>()
    a_errorStates.push(errorWaiting)      // Valid state
    a_errorStates.push(errorProcessing)   // Valid state  
    a_errorStates.push(errorCompleted)    // Valid state
    a_errorStates.push(errorWaiting)      // Duplicate state (ERROR)
    a_errorStates.push(errorOrphaned)     // State with no entry rules (ERROR)
    a_errorStates.push(errorTrapped)      // State with no exit rules (ERROR)

    // Create malformed event rules map with intentional errors
    map<string, o_eventRuleWrapper> map_errorRules = map.new<string, o_eventRuleWrapper>()
    
    // Add valid rules first
    map_errorRules.m_addRuleToEventMap(errorWaiting, errorStart, errorProcessing)
    map_errorRules.m_addRuleToEventMap(errorProcessing, errorFinish, errorCompleted)
    map_errorRules.m_addRuleToEventMap(errorCompleted, errorReset, errorWaiting)
    
    // ERROR: Wildcard states (will generate warnings)
    map_errorRules.m_addRuleToEventMap("", errorEmergency, errorWaiting)       // Empty state wildcard
    map_errorRules.m_addRuleToEventMap("ANY", errorPanic, errorWaiting)        // "ANY" state wildcard
    
    // ERROR: Wildcard conditions (will generate warnings)
    map_errorRules.m_addRuleToEventMap(errorWaiting, "", errorProcessing)      // Empty condition wildcard
    map_errorRules.m_addRuleToEventMap(errorProcessing, "ANY", errorCompleted) // "ANY" condition wildcard
    
    // ERROR: Empty output (will generate warnings)
    map_errorRules.m_addRuleToEventMap(errorWaiting, errorCancel, "")          // Empty output
    
    // ERROR: Invalid state not in states array
    map_errorRules.m_addRuleToEventMap(errorInvalidState, errorEvent, errorWaiting) // Invalid state key
    
    // ERROR: Duplicate conditions within same state (will generate warnings)
    map_errorRules.m_addRuleToEventMap(errorWaiting, errorStart, errorCompleted)  // Duplicate condition "start" in "waiting"
    
    // ERROR: Redundant wildcards (will generate warnings)
    map_errorRules.m_addRuleToEventMap(errorWaiting, "", errorCompleted)       // Redundant condition wildcard
    map_errorRules.m_addRuleToEventMap("", errorEmergency, errorCompleted)     // Redundant state wildcard
    map_errorRules.m_addRuleToEventMap("ANY", errorPanic, errorCompleted)      // Redundant state wildcard (ANY version)
    map_errorRules.m_addRuleToEventMap("", "", errorWaiting)                // Full wildcard
    map_errorRules.m_addRuleToEventMap("ANY", "ANY", errorCompleted)        // Redundant full wildcard
    
    // ERROR: State with no exit rules ("trapped" state has entry but no exit)
    map_errorRules.m_addRuleToEventMap(errorCompleted, errorTrap, errorTrapped)   // Entry to trapped state
    
    // ERROR: "orphaned" state has no entry rules (no rules point to it)
    
    // Create array of valid events for validation (some events will be invalid)
    array<string> a_validEvents = array.new<string>()
    a_validEvents.push(errorProcessing)   // Valid event
    a_validEvents.push(errorCompleted)    // Valid event  
    a_validEvents.push(errorWaiting)      // Valid event
    // Note: "invalid_event" is NOT in this array, so it will cause validation errors
    
    // Add rules with invalid outputs (not in valid events array)
    map_errorRules.m_addRuleToEventMap(errorWaiting, errorTrigger, errorInvalidEvent)  // Output not in valid events
    
    // Print the concurrent FSM error table (map-based rules)
    if in_showStatesRulesTable
        f_printFSMMap(map_errorRules, a_errorStates, position.top_center)
    
    // Print error demo validation table 
    if in_showValidationTable
        bool isErrorMapValid = m_validateEventRulesMap(
          _map_eventRules=map_errorRules,
          _a_states=a_errorStates,
          _a_validEvents=a_validEvents,
          _showTable=true,
          _tablePosition=position.top_right)

//#endregion CONCURRENT FSM ERROR DEMO

//#endregion ERROR DEMONSTRATION

// Apply background color for all demos
bgcolor(color = colour_background)

//#endregion EXAMPLE USAGE

// ============================================ //
//                                              //
//   (╯°□°）╯︵  oǝN ʻǝʇᴉuᴉⅎuᴉ sᴉ ǝʇɐʇs ɹno⅄    //
//                                              //
// ============================================ //
````
